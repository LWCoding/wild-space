title: AfterName
position: -31,-584
---
<<once>>
<<declare $playerName = "Player">>
<<hide_all_objects>>
Narrator: {$playerName}... Yes, you are {$playerName}.
<<speed 1>>
Narrator: You open your eyes, and realize that you are no longer suspended in mid-air.
Narrator: You're lying down. On a hard surface. Your head feels heavy, and your body aches all over.
Narrator: You slowly sit up, and take in your surroundings.
<<endonce>>
Narrator: You're at home. In your metal spaceship.
    -> Hello? Is anyone there?
        Narrator: A voice, soft and distant, responds to your call. You recognize it a few seconds later as your own. Every sound you make echoes off the walls.
        <<jump AfterName>>
    -> Where... am I?
        Narrator: An echo from your voice bounces off the walls. Other than yourself, nobody responds to your question.
        <<jump AfterName>>
    -> (take a look around)
        <<jump LookAround>>
===

title: LookAround
position: -33,-341
---
<<once>>
<<declare $bedrollExplored = false>>
<<declare $cannedFoodExplored = false>>
<<declare $monitorExplored = false>>
<<show MonitorOff>>
<<show Bedroll>>
<<show Soup>>
<<endonce>>
===

title: AfterInteractedWithItems
---
<<hide_all_objects>>
<<show Sitting>>
Narrator: This is the life that you've committed yourself to, ever since taking the job to pilot this small exploration vessel.
Narrator: Just to get a chance away from Earth, away from the people who never cared about you.
Narrator: Nothing of interest seems to be happening. You take a seat and ponder the cold, empty room around you.
    -> (do some push-ups)
        Narrator: You were never huge on fitness, but you were required to keep in shape for space travel.
        Narrator: You drop down and do a few push-ups to keep your strength up.
    -> (contemplate the meaning of life)
        Narrator: You ponder the vastness of existence and your place within it.
        Narrator: The silence of space seems to amplify your thoughts, making you feel both insignificant and connected to the universe.
        -> (hone in on the positive)
            Narrator: You focus on the small joys in your life, finding solace in the little things that bring you happiness.
        -> (dwell on the negative)
            Narrator: You find yourself sinking deeper into feelings of loneliness and despair, overwhelmed by the vast emptiness around you.
    -> (think of something happy)
        Narrator: You recall a fond memory that brings a smile to your face. It's a small comfort in the loneliness of space.
        Narrator: The memory gives you a brief respite from your current situation.
    -> (no use sitting around. stand back up!)
Narrator: You decide to get back to doing something. Anything is better than sitting around.
<<hide Sitting>>
<<jump MonitorOn>>
===

title: MonitorOn
---
<<hide Sitting>>
<<hide MonitorOff>>
<<show MonitorOn>>
<<show_character CatObscure Neutral right>>
Narrator: Suddenly, the monitor jumps to life. A dark figure appears on the screen, accompanied with a garbled, static-filled voice.
<<start_voice_blip CatObscure>>
???: Hello? <i>*bzzt*</i> Hello? Can... hear...?
    -> Who are you?
        <<start_voice_blip CatObscure>>
        ???: <i>*kshh*</i> If... you... there... <i>*bzzt*</i> please... respond...
        -> Say, "Hello!"
            <<jump EnterMessage>>
        -> Say, "Who are you?"
            <<jump EnterMessage>>
        -> Say, "Seen any good movies lately?"
            <<jump EnterMessage>>
        -> (stay silent)
            <<jump EnterMessage>>
    -> (stay silent)
        <<jump Silence>>
===

title: Silence
position: -29,-144
---
Narrator: The voice from the monitor continues to speak.
<<start_voice_blip CatObscure>>
???: Please... <i>*kshh*</i> if... there... yourself... known...
<<stop_voice_blip CatObscure>>
<<jump LookAround>>
===

title: EnterMessage
position: 320,-340
---
<<start_voice_blip CatObscure>>
???: Click... <i>*bzzt*</i> comm... cation... panel...
<<stop_voice_blip CatObscure>>
Narrator: You notice a set of dials on the monitor. It seems like you need to adjust the frequency to let the signal through.
    -> Turn the left dial.
        <<jump Introduction>>
    -> Turn the right dial.
        <<jump Introduction>>
===

title: Introduction
position: 316,-140
---
Narrator: Suddenly, the monitor in front of you jumps to life.
<<hide_character CatObscure>>
<<show_character Cat Neutral right>>
<<start_voice_blip Cat>>
???: Meow! Hello! Are you alright???
    -> ...What?
    -> Aww, you're so cute!
        <<positive_indicator Cat>>
        <<set $catLikesYou += 1>>
        Cat: Aw, you're too kind. Meow!
    -> (scream. what is happening.)
        <<stop_voice_blip Cat>>
        Narrator: You scream deafeningly loudly. The creature on the monitor jumps back in surprise.
        <<show_character Cat Neutral right>>
        <<start_voice_blip Cat>>
        ???: AH! That... surely is a strange introduction, otherbeing.
<<start_voice_blip Cat>>
<<show_character Cat Thinking right>>
???: Well, you don't seem to act like you're in danger. What're you doing in space all by yourself?
    -> What the fuck?
        <<show_character Cat Neutral right>>
        <<set $catDislikesYou += 1>>
        ???: ...don't be rude.
        ???: It's not everyday that you meet another lifeform just floating around in space.
    -> Who... are you?
<<show_character Cat Happy right>>
Cat: Oh! I'm so sorry, I should introduce myself. My name is 猫喵猫猫喵, but you can just call me Cat. 
Cat: I'm what you'd probably call an "alien lifeform". Meowdy!
    -> An alien lifeform? Those don't exist.
        <<show_character Cat Thinking right>>
        <<set $catDislikesYou += 1>>
        Cat: Hm? How do I explain this...
        <<show_character Cat Neutral right>>
        Cat: I'm not from your planet of origin. I come from a place far away.
        Cat: Do they not have manners where you come from?
    -> 猫喵猫猫喵? What does that mean?
        <<show_character Cat Thinking right>>
        Cat: It's... hard to explain. It's probably not something I can explain to you.
        <<show_character Cat Happy right>>
        Cat: Just call me Cat, it's easier that way.
    -> Meowdy?
        Cat: Meowdy! It's just a fun way of saying "hello".
<<show_character Cat Happy right>>
Cat: But anyways, what's your name?
    -> My name's {$playerName}.
        Cat: {$playerName}? What a nice name!
    -> (stay silent)
        Cat: Oh, you don't have a name? That's okay, I guess.
<<declare $catKnowsAboutOtherFigure = false>>
<<stop_voice_blip Cat>>
<<show_character DogObscure Neutral left>>
Narrator: Just as Cat finishes their sentence, another silhouette appears on the monitor.
Narrator: Engaging with this new figure would close the transmission with Cat for now.
    -> Who is that?
        <<show_character Cat Neutral right>>
        <<start_voice_blip Cat>>
        <<set $catKnowsAboutOtherFigure = true>>
        Cat: Hm? Do you see someone else?
            -> Oh, uh... yeah, I do.
                Cat: That's strange. I guess I'm not the only one out here who found you.
                Cat: If you want, you can talk with them. I'll probably hang around here for a bit.
                -> (ignore the newcomer)
                <<jump ContinueWithCat>>
                -> (invite them in)
                    <<jump DogIntro>>
            -> (ignore the newcomer)
                <<jump ContinueWithCat>>
            -> (invite them in)
                <<jump DogIntro>>
    -> (ignore the newcomer)
        <<jump ContinueWithCat>>
    -> (invite them in)
        <<jump DogIntro>>
===

title: ContinueWithCat
---
<<stop_voice_blip Cat>>
<<hide_character DogObscure>>
Narrator: You decide not to engage the new figure, yet. They linger on the monitor, but you block them out of your mental space for now.
<<start_voice_blip Cat>>
<<if $catKnowsAboutOtherFigure>>
    <<positive_indicator Cat>>
    <<set $catLikesYou += 1>>
    <<show_character Cat Happy right>>
    Cat: Aw, that's awfully nice of you.
<<endif>>
<<jump End>>
===

title: DogIntro
---
<<declare $dogKnowsAboutOtherFigure = false>>
<<stop_voice_blip Cat>>
Narrator: As you turn the dial, the monitor flickers, and the new figure becomes clearer. Cat fades into the background as the new figure takes center stage.
<<hide_character Cat>>
<<show_character Dog Happy left>>
<<start_voice_blip Dog>>
???: Woofdy.
    -> Who are you?
    -> Woofdy.
    -> (stay silent)
???: What're you doing out here all by yourself? Woof.
Dog: I'm Dog. Woof. I come in peace. Woof.
Dog: I saw your signal, and thought I'd come check it out. Woof.
    -> Dog?
        Dog: Woof! It's nice to meet you!
    -> Signal?
        Dog: Woof! It's nice to meet you!
        Dog: And yes, I saw your signal. I hope you don't mind me dropping by unannounced. Woof.
    -> Hi Dog. I'm {$playerName}.
        <<positive_indicator Dog>>
        Dog: Woof! It's nice to meet you, {$playerName}!
Dog: Just curious, has anyone else come by recently? Woof.
    -> No.
        Dog: Good, good. Woof. I don't want any competition.
            -> Competition?
                Dog: Woof! Just kidding. Woof. But seriously, I don't want any competition.
    -> Yes.
        Dog: Oh? Who was it? Woof.
            -> Cat.
                Dog: ...
                Dog: Woof! I know Cat! They're a good friend of mine. Woof.
            -> Another alien lifeform.
                Dog: Oh, that's bad news. As a rule, I don't trust other alien lifeforms. Woof.
            -> Actually, I didn't see anyone.
                Dog: Oh? Okay. Woof.
<<stop_voice_blip Dog>>
<<jump End>>
===