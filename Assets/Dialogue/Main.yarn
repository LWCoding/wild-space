title: AfterName
position: -31,-584
---
<<once>>
<<declare $playerName = "Player">>
<<declare $saidHello = false>>
<<hide_all_objects>>
Narrator: {$playerName}... Yes, you are {$playerName}.
<<speed 1>>
Narrator: You open your eyes, and realize that you are no longer suspended in mid-air.
Narrator: You're lying down. On a hard surface. Your head feels heavy, and your body aches all over.
Narrator: You slowly sit up, and take in your surroundings.
<<endonce>>
Narrator: You're at home. In your metal spaceship.
    -> Hello? <<if $saidHello == false>>
        <<set $saidHello = true>>
        Narrator: A voice, soft and distant, responds to your call. You recognize it a few seconds later as your own. Every sound you make echoes off the walls.
        <<jump AfterName>>
    -> (take a look around)
        <<jump LookAround>>
===

title: LookAround
position: -33,-341
---
<<once>>
<<declare $bedrollExplored = false>>
<<declare $cannedFoodExplored = false>>
<<declare $monitorExplored = false>>
<<show MonitorOff>>
<<show Bedroll>>
<<show Soup>>
Narrator: Might as well re-familiarize yourself with your surroundings.
<<show_ui ClickToInteract>>
<<endonce>>
===

title: AfterInteractedWithItems
---
<<hide_all_objects>>
<<hide_ui ClickToInteract>>
<<show Sitting>>
Narrator: This is the life that you've committed yourself to, ever since taking the job to pilot this small exploration vessel.
Narrator: You are tasked with reporting astronomical oddities back to Earth, as the first line of defense against the unknown.
Narrator: But deep inside, you knew this was just to get a chance away from Earth.
Narrator: Nothing of interest seems to be happening. You take a seat and ponder the cold, empty room around you.
    -> (do some push-ups)
        Narrator: You were never huge on fitness, but you were required to keep in shape for space travel.
        Narrator: You drop down and do a few push-ups to keep your strength up.
    -> (contemplate the meaning of life)
        Narrator: You ponder the vastness of existence and your place within it.
        Narrator: The silence of space seems to amplify your thoughts, making you feel both insignificant and connected to the universe.
        -> (hone in on the positive)
            Narrator: You focus on the small joys in your life, finding solace in the little things that bring you happiness.
            <<set $posSelfPerception += 1>>
        -> (dwell on the negative)
            Narrator: You find yourself sinking deeper into feelings of loneliness and despair, overwhelmed by the vast emptiness around you.
            <<set $negSelfPerception += 1>>
    -> (think of something happy)
        Narrator: You recall a fond memory that brings a smile to your face. It's a small comfort in the loneliness of space.
        Narrator: The memory gives you a brief respite from your current situation.
        <<set $posSelfPerception += 1>>
    -> (no use sitting around. stand back up!)
Narrator: You decide to get back to doing something. Anything is better than sitting around.
<<hide Sitting>>
<<jump DoJobFirstTime>>
===

title: DoJobFirstTime
---
Narrator: You stand up and turn on the computer. Time to get to work on your daily report.
<<show MonitorUI>>
<<show_ui TypeToInteract>>
===

title: AfterTypedFirstTime
---
<<hide_ui TypeToInteract>>
Narrator: Looks good.
<<hide MonitorUI>>
<<show MonitorOff>>
<<jump MonitorOn>>
===

title: MonitorOn
---
<<hide Sitting>>
<<hide MonitorOff>>
<<show MonitorOn>>
<<show_character CatObscure Neutral right>>
Narrator: Suddenly, the monitor jumps to life. A dark figure appears on the screen, accompanied with a garbled, static-filled voice.
<<start_voice_blip CatObscure>>
???: Hello? <i>*bzzt*</i> Hello? Can... hear...?
    -> Who are you?
        <<start_voice_blip CatObscure>>
        ???: <i>*kshh*</i> If... you... there... <i>*bzzt*</i> please... respond...
        -> Hello?
            <<jump EnterMessage>>
        -> Who are you?
            <<jump EnterMessage>>
        -> (stay silent)
            <<jump EnterMessage>>
    -> (stay silent)
        <<stop_voice_blip CatObscure>>
        Narrator: The voice from the monitor continues to speak.
        <<start_voice_blip CatObscure>>
        ???: Please... <i>*kshh*</i> if... there... yourself... known...
        <<stop_voice_blip CatObscure>>
        <<jump EnterMessage>>
===

title: EnterMessage
position: 320,-340
---
<<start_voice_blip CatObscure>>
???: Click... <i>*bzzt*</i> comm... cation... panel...
<<stop_voice_blip CatObscure>>
Narrator: You notice a set of dials on the monitor. It seems like you need to adjust the frequency to let the signal through.
    -> Turn the left dial.
        <<jump Introduction>>
    -> Turn the right dial.
        <<jump Introduction>>
===

title: Introduction
position: 316,-140
---
Narrator: Suddenly, the monitor in front of you jumps to life.
<<hide_character CatObscure>>
<<show_character Cat Neutral right>>
<<start_voice_blip Cat>>
???: Hey, I caught your signal from our ship! Are you alright?
    -> ...What?
    -> Aww, you're so cute!
        <<positive_indicator Cat>>
        <<set $catLikesYou += 1>>
        <<show_character Cat Happy right>>
        ???: Aw, you're too kind. 
        <<show_character Cat Neutral right>>
        ???: But seriously, are you okay out there? You look like you're all alone.
            -> Yeah, I'm fine.
                <<show_character Cat Thinking right>>
                ???: Hm, alright. That's at least somewhat reassuring.
            -> Not really.
                <<show_character Cat Neutral right>>
                ???: Oh... I'm sorry to hear that. Space can be a lonely place.
    -> (scream. what is happening.)
        <<stop_voice_blip Cat>>
        Narrator: You scream deafeningly loudly. The creature on the monitor jumps back in surprise.
        <<show_character Cat Neutral right>>
        <<start_voice_blip Cat>>
        ???: AH! That... surely is a strange introduction, otherbeing.
<<start_voice_blip Cat>>
<<show_character Cat Thinking right>>
???: Well, you don't seem to act like you're in danger. What're you doing in space?
    -> What the fuck? What <i>are</i> you?
        <<show_character Cat Neutral right>>
        <<set $catDislikesYou += 1>>
        ???: ...don't be rude.
        ???: It's not everyday that you meet another lifeform just floating around in space.
    -> Who... are you?
    -> It's none of your business.
        <<set $catDislikesYou += 1>>
        ???: I suppose it isn't, but can you blame someone for having a little bit of empathy?
<<if $catLikesYou > $catDislikesYou>>
<<show_character Cat Happy right>>
Cat: Oh! I'm so sorry, I should introduce myself. My name is 猫喵猫猫喵, but you can just call me Cat. 
<<else>>
Cat: But anyways, my name is 猫喵猫猫喵. But to keep things simple, you can just call me Cat. 
<<endif>>
Cat: I'm what you'd probably call an "alien".
    -> An alien? Those don't exist.
        <<show_character Cat Thinking right>>
        <<set $catDislikesYou += 1>>
        Cat: Hm? How do I explain this...
        <<show_character Cat Neutral right>>
        Cat: I'm not from your planet of origin. I come from a place far away.
        Cat: Do they not have manners where you come from?
    -> 猫喵猫猫喵? What does that mean?
        <<show_character Cat Thinking right>>
        Cat: It's... hard to explain. It's probably not something I can explain to you.
        <<show_character Cat Happy right>>
        Cat: Just call me Cat, it's easier that way.
    -> Meowdy.
        <<show_character Cat Thinking right>>
        Cat: ...Meowdy? That's an interesting way to greet someone.
        Cat: I guess, meowdy to you too.
<<show_character Cat Happy right>>
Cat: But anyways, who are you? What are you?
    -> My name's {$playerName}.
        Cat: {$playerName}? It's nice to meet you.
        <<set $catKnowsPlayerName = true>>
    -> I'm a human.
        <<show_character Cat Thinking right>>
        Cat: A human? I've only heard stories about your kind.
        Cat: Is it true that you walk on two legs and have opposable thumbs?
        -> Yes.
            <<show_character Cat Happy right>>
            Cat: That's incredible! You must be very dexterous.
        -> No.
            <<show_character Cat Neutral right>>
            Cat: Hm, that's disappointing. I was hoping to see some cool tricks.
        <<show_character Cat Happy right>>
        Cat: And what should I call you?
        -> It's {$playerName}.
            Cat: {$playerName}? That's a nice name.
            <<set $catKnowsPlayerName = true>>
        -> I don't have a name.
            Cat: Oh, huh. That's okay, I guess.
    -> (stay silent)
        Cat: Oh, you don't have a name? That's okay, I guess.
<<show_character Cat Neutral right>>
Cat: You should be careful here, though. Space is dangerous!
Cat: What are you doing out here? Wouldn't you rather be on your home planet?
    -> Exploring the cosmos.
        <<positive_indicator Cat>>
        <<set $catLikesYou += 1>>
        <<show_character Cat Happy right>>
        Cat: That's commendable! Not many have the courage to venture into the unknown.
    -> Escaping it all, I guess.
        <<show_character Cat Neutral right>>
        Cat: I understand that feeling. Sometimes, it's nice to get away from everything.
    -> I'm... not sure.
        <<show_character Cat Thinking right>>
        Cat: Hm, that's okay. Sometimes, we don't have all the answers right away.
Cat: Ah! But I shouldn't keep you here all day. You probably have things to do.
<<stop_voice_blip Cat>>
<<hide_all_characters>>
<<hide MonitorOn>>
<<show MonitorOff>>
Narrator: Suddenly, the screen flickers back to static. Cat is gone, for now.
    -> Cat? Can you hear me?
        Narrator: You tap the monitor and move around the dials, trying to re-establish contact.
        Narrator: The screen warps and flickers, but Cat is nowhere to be seen. You are alone, again.
    -> (take a seat. ponder what just happened)
        <<hide_all_objects>>
        <<show Thinking>>
        Narrator: You sit back down, trying to process the encounter you just had.
        Narrator: Who was that creature? An alien? How is that even possible?
<<if $catLikesYou > $catDislikesYou>>
    Narrator: Strangely enough, you found the encounter with them rather pleasant. They seemed friendly, in their own way.
    Narrator: You think about what it would be like to have a friend like that. Someone who understands you, even if they are from another world.
    Narrator: You wonder if you'll ever see them again.
    <<set $posSelfPerception += 1>>
<<else>>
    Narrator: To you, the whole encounter felt off. You couldn't shake the feeling that something was wrong about that creature.
    Narrator: You think about what others on Earth would say if they found out about this encounter. They'd probably call you crazy.
    Narrator: "Aliens don't exist," they'd say. "You must be imagining things."
    Narrator: The idea makes you feel even more isolated.
    <<set $negSelfPerception += 1>>
<<endif>>
<<hide MonitorOff>>
<<show MonitorOn>>
<<show_character DogObscure Neutral right>>
Narrator: As you sit there, lost in thought, the monitor flickers back to life once more.
<<jump DogIntro>>
===

title: DogIntro
---
<<hide_all_characters>>
<<hide_all_objects>>
<<show_character Dog Happy right>>
<<start_voice_blip Dog>>
???: Woofdy.
    -> Who are you?
    -> That doesn't make sense.
        <<set $dogDislikesYou += 1>>
    -> (stay silent)
???: What're you doing out here all by yourself? Woof.
Dog: I'm Dog. Woof. I come in peace. Woof.
Dog: I saw your signal, and thought I'd come check it out. Woof.
    -> It's nice to meet you, Dog.
        <<positive_indicator Dog>>
        <<set $dogLikesYou += 1>>
        Dog: Woof! It's nice to meet you too... whatever your name is.
        -> It's {$playerName}.
            Dog: Woof! {$playerName}, that's a nice name.
        -> (stay silent)
    -> Why do you keep saying woof?
        Dog: I don't know what you mean. Woof.
    -> Hi Dog. I'm {$playerName}.
        <<positive_indicator Dog>>
        Dog: Woof! It's nice to meet you, {$playerName}!
Dog: I hope you don't mind me dropping by unannounced. Woof.
    -> No.
        Dog: Good, good. Woof. I don't want any competition.
            -> Competition?
                Dog: Woof! Just kidding. Woof. But seriously, I don't want any competition.
    -> Yes.
        Dog: Oh? Who was it? Woof.
            -> Cat.
                Dog: ...
                Dog: Woof! I know Cat! They're a good friend of mine. Woof.
            -> Another alien lifeform.
                Dog: Oh, that's bad news. As a rule, I don't trust other alien lifeforms. Woof.
            -> Actually, I didn't see anyone.
                Dog: Oh? Okay. Woof.
<<stop_voice_blip Dog>>
<<jump End>>
===