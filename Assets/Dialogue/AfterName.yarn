title: AfterName
position: -169,-98
---
<<declare $playerName = "Player">>
<<once>>
Narrator: Great, hello {$playerName}.
<<endonce>>
Narrator: The room around you is cold, and seemingly empty.
    -> Hello? Is anyone there?
        Narrator: A voice, soft and distant, responds to your call. You recognize it a few seconds later as your own. Every sound you make echoes off the walls.
        <<jump AfterName>>
    -> Where... am I?
        Narrator: An echo from your voice bounces off the walls. Other than yourself, nobody responds to your question.
        <<jump AfterName>>
    -> (take a look around)
        <<jump LookAround>>
===

title: LookAround
position: 121,-186
---
<<once>>
Narrator: You take a look around. You're in some sort of... cold, dark, metallic room. Plates line the walls, and there is a faint humming sound coming from the walls.
Narrator: Suddenly, the monitor in front of you jumps to life.
<<show_character CatObscure Neutral right>>
<<endonce>>
<<start_voice_blip CatObscure>>
???: Hello? <i>*bzzt*</i> Hello? Can... hear...?
    -> Who are you?
        <<stop_voice_blip CatObscure>>
        Narrator: The voice does not seem to acknowledge your reply.
        <<start_voice_blip CatObscure>>
        ???: <i>*kshh*</i> If... you... there... <i>*bzzt*</i> please... respond...
        <<stop_voice_blip CatObscure>>
        <<show ComputerSetup>>
        Narrator: At this moment, you notice a keyboard. You may be able to communicate with it.
        -> Type, "Hello!"
            <<jump EnterMessage>>
        -> Type, "Who are you?"
            <<jump EnterMessage>>
        -> Type, "Seen any good movies lately?"
            <<jump EnterMessage>>
        -> Stay silent.
            <<jump EnterMessage>>
    -> Stay silent.
        <<jump Silence>>
===

title: Silence
---
Narrator: The voice from the monitor continues to speak.
<<start_voice_blip CatObscure>>
???: Please... <i>*kshh*</i> if... there... yourself... known...
<<stop_voice_blip CatObscure>>
<<jump LookAround>>
===

title: EnterMessage
---
<<start_voice_blip CatObscure>>
???: Click... <i>*bzzt*</i> comm... cation... panel...
<<stop_voice_blip CatObscure>>
Narrator: You notice a separated panel of dials. It seems like you need to adjust the frequency to let the signal through.
    -> Turn the left dial.
        <<jump MonitorFixed>>
    -> Turn the right dial.
        <<jump MonitorFixed>>
===

title: MonitorFixed
---
Narrator: Suddenly, the monitor in front of you jumps to life.
<<hide_character CatObscure>>
<<show_character Cat Happy right>>
<<start_voice_blip Cat>>
???: Meow! Hello!
    -> ...What?
        <<jump Introduction>>
    -> Aww, you're so cute!
        <<positive_indicator Cat>>
        <<jump Introduction>>
    -> (scream)
        <<stop_voice_blip Cat>>
        Narrator: You scream deafeningly loudly. The creature on the monitor jumps back in surprise.
        <<show_character Cat Neutral right>>
        <<start_voice_blip Cat>>
        ???: That... surely is a strange introduction, otherbeing.
        <<jump Introduction>>
===

title: Introduction
---
<<start_voice_blip Cat>>
<<show_character Cat Thinking right>>
???: Well, you seem to be in a bit of a pickle. What're you doing in space all by yourself?
    -> Space?!
        <<show_character Cat Happy right>>
        <<positive_indicator Cat>>
        ???: Oh! You didn't know? You're in space! How exciting!
        <<jump Introduction2>>
    -> Who... are you?
        <<jump Introduction2>>
===

title: Introduction2
---
Cat: Oh! I'm so sorry, I should introduce myself. My name is 猫喵猫喵, but you can just call me Cat. 
Cat: I'm what you'd probably call an "alien lifeform". Meowdy!
    -> An alien lifeform?
        <<show_character Cat Thinking right>>
        Cat: Just another way to say that I'm not... from your planet of origin.
        <<jump Introduction3>>
    -> 猫喵猫喵? What does that mean?
        Cat: It's nothing you should worry yourself about.
        <<jump Introduction3>>
    -> Meowdy?
        Cat: Meowdy.
        <<jump Introduction3>>
===

title: Introduction3
---
<<declare $catKnowsAboutOtherFigure = false>>
<<stop_voice_blip Cat>>
<<show_character DogObscure Neutral left>>
Narrator: Just as Cat finishes their sentence, another silhouette appears on the monitor.
    -> Who is that?
        <<show_character Cat Neutral right>>
        <<start_voice_blip Cat>>
        <<set $catKnowsAboutOtherFigure = true>>
        Cat: Hm? Do you see someone else?
        Cat: I wouldn't worry about it. Let's just keep talking!
            -> But what if they have something to say?
                Cat: Don't.
                Cat: It's probably not important.
                    -> (ignore the newcomer)
                        <<jump IgnoreNewcomer>>
                    -> (invite them in)
                        <<jump InviteThemIn>>
            -> (ignore the newcomer)
                <<jump IgnoreNewcomer>>
            -> (invite them in)
                <<jump InviteThemIn>>
    -> (ignore the newcomer)
        <<jump IgnoreNewcomer>>
    -> (invite them in)
        <<jump InviteThemIn>>
===

title: IgnoreNewcomer
---
<<stop_voice_blip Cat>>
<<hide_character DogObscure>>
Narrator: You decide not to engage the new figure.
<<if $catKnowsAboutOtherFigure>>
    <<positive_indicator Cat>>
    <<show_character Cat Happy right>>
    Cat: Good choice! You can never be too sure with strangers.
<<endif>>
Cat: Anyways, where were we?
<<jump EndOfDemo>>
===

title: InviteThemIn
---
<<stop_voice_blip Cat>>
Narrator: As you turn the dial, the monitor flickers, and the new figure becomes clearer. Cat fades into the background as the new figure takes center stage.
<<hide_character Cat>>
<<jump EndOfDemo>>
===

title: EndOfDemo
---
<<stop_voice_blip Cat>>
<<hide_character Cat>>
<<hide_character DogObscure>>
Narrator: You've reached the end of the demo.
Narrator: I couldn't be bothered to code anymore beyond this point. :')
===