title: AfterName
position: -31,-584
---
<<hide Stars>>
<<hide Drifting>>
<<declare $playerName = "Player">>
<<once>>
Narrator: {$playerName}... {$playerName}... that sounds right to you.
<<endonce>>
Narrator: You suddenly open your eyes, and realize that you are no longer suspended in mid-air.
Narrator: You find yourself in a small, enclosed room. The walls are metallic, and there is a faint humming sound coming from the walls.
    -> Hello? Is anyone there?
        Narrator: A voice, soft and distant, responds to your call. You recognize it a few seconds later as your own. Every sound you make echoes off the walls.
        <<jump AfterName>>
    -> Where... am I?
        Narrator: An echo from your voice bounces off the walls. Other than yourself, nobody responds to your question.
        <<jump AfterName>>
    -> (take a look around)
        <<jump LookAround>>
===

title: LookAround
position: -33,-341
---
<<once>>
<<show ComputerSetup>>
<<declare $toiletExplored = false>>
<<declare $bedrollExplored = false>>
<<declare $cannedFoodExplored = false>>
<<declare $meterExplored = false>>
<<declare $monitorExplored = false>>
<<endonce>>
Narrator: You take a look around. There's a monitor in front of you. The rest of the room is mostly empty, but seems stocked with a toilet, bedroll, canned food, and a strange meter on the wall.
    -> Inspect the toilet. <<if $toiletExplored == false>>
        Narrator: The toilet is a standard issue, but it looks like it hasn't been cleaned in a while. You'd rather not use it.
        <<set $toiletExplored = true>>
    -> Inspect the bedroll. <<if $bedrollExplored == false>>
        Narrator: The bedroll is a simple sleeping mat. It looks like it hasn't been used in a while, but it seems comfortable enough.
        <<set $bedrollExplored = true>>
    -> Inspect the canned food. <<if $cannedFoodExplored == false>>
        Narrator: The canned food is old, but it looks like it might still be edible. You decide to save it for later.
        <<set $cannedFoodExplored = true>>
    -> Inspect the meter on the wall. <<if $meterExplored == false>>
        Narrator: The meter is blinking red. It seems to be some sort of oxygen level indicator. You should probably get out of here before you run out of air.
        <<set $meterExplored = true>>
    -> Inspect the monitor. <<if $monitorExplored == false>>
        Narrator: The monitor is currently off. You wonder what it might display if it were on.
        <<set $monitorExplored = true>>

<<if $toiletExplored and $bedrollExplored and $cannedFoodExplored and $meterExplored and $monitorExplored>>
    Narrator: You've inspected everything in the room.
    <<hide ComputerSetup>>
    <<detour SitAround>>
    <<show ComputerSetup>>
    <<jump MonitorOn>>
<<else>>
    <<jump LookAround>>
<<endif>>
===

title: MonitorOn
---
Narrator: Suddenly, the monitor in front of you jumps to life. A dark figure appears on the screen, accompanied with a garbled, static-filled voice.
<<show_character CatObscure Neutral right>>
<<start_voice_blip CatObscure>>
???: Hello? <i>*bzzt*</i> Hello? Can... hear...?
    -> Who are you?
        <<start_voice_blip CatObscure>>
        ???: <i>*kshh*</i> If... you... there... <i>*bzzt*</i> please... respond...
        <<stop_voice_blip CatObscure>>
        Narrator: At this moment, you notice a keyboard. You may be able to communicate with it.
        -> Type, "Hello!"
            <<jump EnterMessage>>
        -> Type, "Who are you?"
            <<jump EnterMessage>>
        -> Type, "Seen any good movies lately?"
            <<jump EnterMessage>>
        -> (stay silent)
            <<jump EnterMessage>>
    -> (stay silent)
        <<jump Silence>>
===

title: Silence
position: -29,-144
---
Narrator: The voice from the monitor continues to speak.
<<start_voice_blip CatObscure>>
???: Please... <i>*kshh*</i> if... there... yourself... known...
<<stop_voice_blip CatObscure>>
<<jump LookAround>>
===

title: EnterMessage
position: 320,-340
---
<<start_voice_blip CatObscure>>
???: Click... <i>*bzzt*</i> comm... cation... panel...
<<stop_voice_blip CatObscure>>
Narrator: You notice a separated panel of dials. It seems like you need to adjust the frequency to let the signal through.
    -> Turn the left dial.
        <<jump Introduction>>
    -> Turn the right dial.
        <<jump Introduction>>
===

title: Introduction
position: 316,-140
---
Narrator: Suddenly, the monitor in front of you jumps to life.
<<hide_character CatObscure>>
<<show_character Cat Happy right>>
<<start_voice_blip Cat>>
???: Meow! Hello!
    -> ...What?
    -> Aww, you're so cute!
        <<positive_indicator Cat>>
    -> (scream)
        <<stop_voice_blip Cat>>
        Narrator: You scream deafeningly loudly. The creature on the monitor jumps back in surprise.
        <<show_character Cat Neutral right>>
        <<start_voice_blip Cat>>
        ???: That... surely is a strange introduction, otherbeing.
<<start_voice_blip Cat>>
<<show_character Cat Thinking right>>
???: Well, you seem to be in a bit of a pickle. What're you doing in space all by yourself?
    -> Space?!
        <<show_character Cat Happy right>>
        <<positive_indicator Cat>>
        ???: Oh! You didn't know? You're in space! How exciting!
    -> Who... are you?
Cat: Oh! I'm so sorry, I should introduce myself. My name is 猫喵猫喵, but you can just call me Cat. 
Cat: I'm what you'd probably call an "alien lifeform". Meowdy!
    -> An alien lifeform?
        <<show_character Cat Thinking right>>
        Cat: Just another way to say that I'm not... from your planet of origin.
    -> 猫喵猫喵? What does that mean?
        <<show_character Cat Thinking right>>
        Cat: It's... hard to explain. It's probably not something I can explain to you.
        <<show_character Cat Happy right>>
        Cat: Just call me Cat, it's easier that way.
    -> Meowdy?
        Cat: Meowdy.
Cat: But anyways, what's your name?
    -> My name's {$playerName}.
        Cat: {$playerName}? What a nice name!
    -> (stay silent)
        Cat: Oh, you don't have a name? That's okay, I guess.
<<declare $catKnowsAboutOtherFigure = false>>
<<stop_voice_blip Cat>>
<<show_character DogObscure Neutral left>>
Narrator: Just as Cat finishes their sentence, another silhouette appears on the monitor.
    -> Who is that?
        <<show_character Cat Neutral right>>
        <<start_voice_blip Cat>>
        <<set $catKnowsAboutOtherFigure = true>>
        Cat: Hm? Do you see someone else?
        Cat: I wouldn't worry about it.
            -> But what if they have something to say?
                Cat: Don't.
                Cat: It's probably not important.
                    -> (ignore the newcomer)
                        <<jump CatIntro>>
                    -> (invite them in)
                        <<jump DogIntro>>
            -> (ignore the newcomer)
                <<jump CatIntro>>
            -> (invite them in)
                <<jump DogIntro>>
    -> (ignore the newcomer)
        <<jump CatIntro>>
    -> (invite them in)
        <<jump DogIntro>>
===