title: AfterName
position: 22,-593
---
<<hide Drifting>>
<<speed 1>>
I snap out of it, realizing I am not suspended in mid-air.
<<show Bedroom>>
<<wait 9.5>>
I'm standing on a hard surface. My head feels heavy, and my body aches all over.
I'm at home. In my metal spaceship.
<<fade_transition 2>>
<<wait 1>>
<<hide_all_objects>>
<<show PCRoom>>
<<wait 1>>
It's a quiet day, as always.
    -> Hello?
        Nothing. Every sound I make echoes off the walls. I'm alone.
        -> (take a look around)
            <<jump LookAround>>
    -> (take a look around)
        <<jump LookAround>>
===

title: LookAround
position: 23,-416
---
<<once>>
<<fade_transition 2>>
<<wait 1>>
<<show MonitorOffDay1>>
<<show CalendarDay1>>
<<show RouterDay1>>
I re-familiarize myself with my surroundings.
<<show_ui ClickToInteract>>
<<endonce>>
===

title: AfterInteractedWithItems
position: 315,-648
---
<<fade_transition 2>>
<<wait 1>>
<<hide_all_objects>>
<<hide_ui ClickToInteract>>
<<show Stars>>
<<show Sitting>>
This is the life that I've committed myself to, ever since taking the job to pilot this small exploration vessel.
I am tasked with reporting astronomical oddities back to Earth, as the first line of defense against the unknown.
But deep inside, I knew this was just to get a chance away from Earth.
Nothing of interest seems to be happening. I start to spiral, sitting and pondering the emptiness around me.
    -> (contemplate my life on Earth)
        I ponder the vastness of existence and my place within it.
        The silence of space seems to amplify my thoughts, making me feel both insignificant and connected to the universe.
        -> (hone in on the positive)
            I think back to a time I felt connected with someone on Earth, recalling a time when my mother taught me how to bake cookies.
            Although she's gone now, the thought brings a smile to my face. 
            <<set $posSelfPerception += 1>>
            It's a small comfort in the loneliness of space. I feel <color=\#88FF88>slightly happier</color>.
        -> (dwell on the negative)
            I think back about a time I felt insignificant on Earth.
            Ever since I dropped out of college, I've felt like a disappointment to my family.
            I took the first contract that would launch me off-planet, telling myself distance would make it easier to breathe.
            <<set $negSelfPerception += 1>>
            I scoff at the concept of unconditional love. In doing so, I <color=\#FF8888>feel sadder</color>, overwhelmed by the vast emptiness around me.
    -> (think of something happy)
        I think back to my time on Earth, recalling a time when my mother taught me how to bake cookies.
        Although she's gone now, the thought brings a smile to my face. 
        <<set $posSelfPerception += 1>>
        It's a small comfort in the loneliness of space. I feel <color=\#88FF88>slightly happier</color>.
    -> (do some push-ups)
        I was never huge on fitness, but I was required to keep in shape for space travel.
        Neither of my parents supported my decision to leave Earth. And now, they're gone. 
        Part of me still wants to prove that leaving wasn't just another failure.
        <<set $posSelfPerception += 1>>
        I do a few push-ups to keep my strength up. Strangely enough, I feel <color=\#88FF88>a little happier</color> afterwards.
I decide to get back to doing something. Anything is better than sitting around.
<<hide_all_objects>>
<<show PCRoom>>
<<jump DoJobFirstTime>>
===

title: DoJobFirstTime
position: 308,-448
---
I stand up and turn on the computer. 
As part of my job, I need to complete a daily report on my findings.
Given they're all the same, I'm not sure HQ pays much attention to them. But at least, it gives me a sense of routine.
<<show MonitorReportDay1>>
<<show_ui TypeToInteract>>
===

title: AfterTypedFirstTime
position: 15,-241
---
<<hide_ui TypeToInteract>>
I sigh, after having written the same report for the 620th time. 
The report is done for the day.
    -> (turn off the monitor)
<<hide MonitorReportDay1>>
<<jump MonitorOn>>
===

title: MonitorOn
position: 15,-63
---
<<hide_all_objects>>
<<show PCRoom>>
<<hide MonitorOn>>
Just as I turn the monitor off, my hip bumps the communication panel.
<<play_sound Glitch 0.25>>
<<show MonitorOn>>
The tuner skitters across the dial with a burst of static.
<<show MonitorSilhouette>>
The noise thins, then resolves, locked onto a different frequency. The monitor jolts back to life.
<<show_character Cat Obscure right>>
A dark figure appears on the screen, accompanied with a static-filled voice.
<<start_voice_blip Cat>>
???: Hello? <i>*bzzt*</i> Hello? Can... hear...?
    -> Hello?
    -> What the...
    -> ...
    The voice from the monitor continues to speak.
    ???: Please... <i>*kshh*</i> if... there... yourself... known...
    <<stop_voice_blip Cat>>
    <<jump EnterMessage>>
===

title: EnterMessage
position: 316,-234
---
<<start_voice_blip Cat>>
???: Click... <i>*bzzt*</i> comm... cation... panel...
<<stop_voice_blip Cat>>
With my expertise, I think I can adjust the frequency and let the signal through.
    -> (let the signal through)
        <<jump CatIntro>>
    -> (ignore the signal. it's probably nothing.)
        I decide to ignore the signal. It's probably just static interference.
        <<hide_all_characters>>
        <<hide MonitorSilhouette>>
        <<wait 1>>
        <<show MonitorSilhouette>>
        <<set $negSelfPerception += 1>>
        After a few moments, the screen flickers back to static. I'm alone, again. I feel <color=\#FF8888>more isolated</color>.
        How should I pass the time?
            -> (block them out of your head)
                I decide to play a game on the monitor to pass the time.
                <<set $negSelfPerception += 1>>
                The game is simple, but its monotony makes me feel <color=\#FF8888>even more lonely</color>.
            -> (try to find the signal again)
                I fiddle with the dials and knobs, trying to find the signal again.
<<hide_all_objects>>
<<show_character Cat Obscure right>>
Suddenly, the same character from before reappears on the screen.
    -> (get it over with. let the signal through.)
        <<jump CatIntro>>
    -> (unplug the monitor)
        <<hide_all_characters>>
        <<hide MonitorSilhouette>>
        I decide to unplug the monitor, cutting off the signal entirely.
        After a few moments, the screen flickers back to static. I'm alone, again.
        <<jump ForeverAloneNoContact>>
===

title: CatIntro
position: 318,-2
---
Suddenly, the figure comes into view.
<<show_character Cat Sad right>>
<<start_voice_blip Cat>>
<<play_music CatTheme 0.5>>
???: Hey, I caught your signal from our ship! Are you alright?
    -> ...What?
    -> Aww, you're so cute!
        <<positive_indicator Cat>>
        <<set $catLikesYou += 1>>
        <<show_character Cat Neutral right>>
        ???: Oh, um... Thank... you?
        <<show_character Cat Sad right>>
        ???: But seriously, are you okay out there? You look like you're all alone.
            -> Yeah, I'm fine.
                <<show_character Cat Thinking right>>
                ???: Hm, alright. That's at least somewhat reassuring.
            -> Not really.
                <<show_character Cat Neutral right>>
                ???: Oh... I'm sorry to hear that. Space can be a lonely place.
            -> (scream in confusion)
                <<stop_voice_blip Cat>>
                <<negative_indicator Cat>>
                <<set $catDislikesYou += 1>>
                <<show_character Cat Surprised right>>
                I scream deafeningly loudly. The creature on the monitor jumps back in surprise.
                <<start_voice_blip Cat>>
                ???: AH! That... surely is a strange introduction.
                <<show_character Cat Sad right>>
                ???: Please try to spare my ears next time... these are quite sensitive.
    -> (scream. what is happening.)
        <<stop_voice_blip Cat>>
        <<negative_indicator Cat>>
        <<set $catDislikesYou += 1>>
        <<show_character Cat Surprised right>>
        I scream deafeningly loudly. The creature on the monitor jumps back in surprise.
        <<start_voice_blip Cat>>
        ???: AH! That... surely is a strange introduction.
        <<show_character Cat Sad right>>
        ???: Please try to spare my ears next time... these are quite sensitive.
<<start_voice_blip Cat>>
<<show_character Cat Thinking right>>
???: You don't seem to act like you're in danger. What're you doing in space?
    -> What the fuck? What <i>are</i> you?
        <<show_character Cat Neutral right>>
        <<negative_indicator Cat>>
        <<set $catDislikesYou += 1>>
        ???: ...don't be rude.
        ???: It's not everyday that you meet another lifeform just floating around in space.
    -> Who... what... are you?
    -> Why do you want to know?
        <<negative_indicator Cat>>
        <<set $catDislikesYou += 1>>
        <<show_character Cat Thinking right>>
        ???: I guess it's not entirely my business... but I was just curious.
<<if $catLikesYou >= $catDislikesYou>>
<<show_character Cat Happy right>>
{$catName}: Oh! I'm so sorry, I should introduce myself. My name is 猫喵猫猫喵, but you can just call me {$catName}.
<<show_character Cat Happy2 right>>
<<else>>
{$catName}: My name is 猫喵猫猫喵. But to keep things simple, you can just call me {$catName}.
<<show_character Cat Neutral right>>
<<endif>>
{$catName}: I'm from the planet Linnaeus. It's nice to meet you.
    -> You're an alien?
        <<show_character Cat Thinking right>>
        <<negative_indicator Cat>>
        <<set $catDislikesYou += 1>>
        {$catName}: Hm? I guess so?
        <<show_character Cat Neutral right>>
        {$catName}: I'm not from your planet of origin. I come from a place far away.
        <<show_character Cat Angry right>>
        {$catName}: "Alien" is a bit demeaning, though, don't you think?
    -> 猫喵猫猫喵? What name is that?
        <<show_character Cat Thinking right>>
        {$catName}: It's... hard to explain.
        <<show_character Cat Happy2 right>>
        {$catName}: Just call me {$catName}, it's easier that way.
    -> Meowdy.
        <<show_character Cat Thinking right>>
        {$catName}: ...Meowdy? That's an interesting way to greet someone.
        <<show_character Cat Flirt right>>
        <<set $catLikesYou += 1>>
        <<positive_indicator Cat>>
        {$catName}: I guess, meowdy to you too.
<<show_character Cat Happy right>>
{$catName}: But anyways, I'm glad you're safe... um... 
<<show_character Cat Thinking right>>
{$catName}: I guess... you're a human, right?
{$catName}: I've heard stories about your kind.
{$catName}: Is it true that you walk on two legs and have opposable thumbs?
-> Yeah, it's pretty cool.
    <<show_character Cat Happy2 right>>
    <<set $catLikesYou += 1>>
    <<positive_indicator Cat>>
    {$catName}: That's incredible! You must be very dexterous.
-> (lie) No. What?
    <<show_character Cat Sad right>>
    {$catName}: Aw, that's disappointing. I was hoping to see some cool tricks.
<<show_character Cat Thinking right>>
{$catName}: What should I call you?
    -> My name's {$playerName}.
        <<show_character Cat Happy right>>
        <<set $catLikesYou += 1>>
        <<positive_indicator Cat>>
        <<set $catKnowsPlayerName = true>>
        <<if $playerName == "Dipper">>
            {$catName}: {$playerName}? Like the star system? That's pretty cute.
        <<else>>
            {$catName}: Ah, {$playerName}! It's nice to meet you.
        <<endif>>
        <<set $catThinksPlayerNameIs = $playerName>>
    -> I don't know you...
        <<show_character Cat Sad right>>
        <<negative_indicator Cat>>
        <<set $catDislikesYou += 1>>
        {$catName}: Aw, that's fine. I guess I was a little forward.
        <<show_character Cat Neutral right>>
        {$catName}: But, just to make things easier, is there something you'd like me to call you?
        -> It's {$playerName}.
            {$catName}: {$playerName}? That's a nice name.
            <<set $catThinksPlayerNameIs = $playerName>>
        -> (lie) I don't have a name.
            <<show_character Cat Thinking right>>
            {$catName}: Oh, huh. That's okay. How would people refer to you then, though?
            <<show_character Cat Flirt right>>
            {$catName}: I guess I'll just call you "Human". I'm a master at nicknames.
    -> ...
        {$catName}: A bit shy? That's okay, I guess. 
        <<show_character Cat Flirt right>>
        {$catName}: I guess I'll just call you "Human", then. I'm a master at nicknames.
<<show_character Cat Neutral right>>
{$catName}: You should be careful here, though. Space is dangerous!
<<show_character Cat Thinking right>>
{$catName}: What brings you out here? Wouldn't you rather be on your home planet?
    -> Escaping it all, I guess.
        <<show_character Cat Neutral right>>
        <<positive_indicator Cat>>
        <<set $catLikesYou += 1>>
        {$catName}: I understand that feeling. Sometimes, it's nice to get away from everything.
    -> (lie) Exploring the cosmos.
        <<negative_indicator Cat>>
        <<set $catDislikesYou += 1>>
        <<show_character Cat Sad right>>
        {$catName}: Oh, wow... you'd risk your life for something like that?
        <<show_character Cat Neutral right>>
        {$catName}: That's... quite brave. Maybe a little suicidal, too.
    -> I'm... not sure.
        <<show_character Cat Thinking right>>
        {$catName}: Hm, that's okay. Sometimes, we don't have all the answers right away.
        {$catName}: It might be in your best interest to find somewhere safer, though. It's pretty unpredictable out here.
<<show_character Cat Surprised right>>
{$catName}: Ah! Right, I have a meeting to get to. It's not my plan to keep you here for over an orbit.
<<if $catLikesYou >= $catDislikesYou>>
    <<show_character Cat Happy right>>
    {$catName}: Let's talk again soon, though. I'm interested in getting to know more about you, {$catThinksPlayerNameIs}.
    <<show_character Cat Flirt right>>
    {$catName}: If you want to chat again, just give me a ping. See ya!
    <<set $catSecured = true>>
<<else>>
    <<show_character Cat Neutral right>>
    {$catName}: Well, I'll get going, {$catThinksPlayerNameIs}. Take care of yourself out here.
<<endif>>
<<stop_voice_blip Cat>>
<<hide_all_characters>>
<<hide MonitorSilhouette>>
<<show MonitorOn>>
<<play_music MainTheme>>
<<if $catSecured == false>>
    <<deactivate_icon Cat>>
<<endif>>
The transmission cuts off as {$catName} takes their leave.
<<if $catSecured == true>>
    <<set $charactersSecured += 1>>
    {$catName} uploads a sequence of radio waves in file format onto your device, letting you ping them again in the future.
    Suddenly, the screen flickers back to static. {$catName} is gone, for now.
        -> {$catName}? Can you hear me?
            I tap the monitor and move around the dials, but nobody responds. I'm alone, again.
        -> (sit down)
<<endif>>
<<jump ThinkingAfterCatLeaves>>
===

title: ThinkingAfterCatLeaves
position: 310,187
---
<<fade_transition 2>>
<<wait 1>>
<<hide_all_objects>>
<<show Stars>>
<<show Sitting>>
I sit back down, trying to process the encounter I just had.
Who was that creature? How is that even possible?
<<if $catLikesYou > $catDislikesYou>>
    <<set $posSelfPerception += 1>>
    Strangely enough, I <color=\#88FF88>enjoyed the encounter</color> with them. They seemed... friendly.
    What would it be like for me to have a friend like that?
    I wonder if I'll ever see them again.
<<else>>
    <<set $negSelfPerception += 1>>
    To me, the <color=\#FF8888>whole encounter felt off</color>. Aliens? Of all times, why now?
    I think about what others on Earth would say if they found out about this encounter. Although it is my job to report such things, would they really believe I met a friendly animal alien?
    Maybe, I'll think about it after a night's rest.
<<endif>>
<<hide_all_objects>>
<<fade_transition 2>>
<<wait 1>>
<<show PCRoom>>
The cabin falls silent again as the static fades back into the background hum.
I slip back into the same routine: filling out reports, heating up rations, staring at the same metal walls.
<<wait 1.5>>
Days blur together in a dull loop. Sometimes I have to check the logs just to remember how long I've been out here.
<<show Stars>>
<<show Sitting>>
Out here, time stretches until it feels like I've been alone on this ship for months.
I think about Earth—about the classes I walked out of, the friends I stopped returning messages from, and the way I grabbed this posting like a life raft.
Part of me wonders if there's anything left to go back to. Part of me isn't sure I want there to be.
<<jump EndDay1>>
===

title: EndDay1
position: 1063,-84
---
<<hide_all_characters>>
<<hide_all_objects>>
<<show PCRoom>>
The weight of the silence, and the memory of that strange call, finally catches up with me.
I decide to call it a day; maybe after I wake up, I'll have a clearer idea of what I'm doing out here, and who I'm trying to become.
<<fade_transition 3>>
<<wait 3>>
<<jump StartDay2>>
===