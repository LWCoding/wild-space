title: AfterName
position: -31,-584
---
<<hide Drifting>>
<<speed 1>>
You snap out of it, realizing you are not suspended in mid-air.
<<show Bedroom>>
<<wait 9.5>>
You're standing on a hard surface. Your head feels heavy, and your body aches all over.
You're at home. In your metal spaceship.
<<fade_transition 2>>
<<wait 1>>
<<hide_all_objects>>
<<show PCRoom>>
<<wait 1>>
It's a quiet day, as always.
    -> Hello?
        Nothing. Every sound you make echoes off the walls. You are alone.
        -> (take a look around)
            <<jump LookAround>>
    -> (take a look around)
        <<jump LookAround>>
===

title: LookAround
position: -30,-398
---
<<once>>
<<fade_transition 2>>
<<wait 1>>
<<show MonitorOffDay1>>
<<show CalendarDay1>>
<<show RouterDay1>>
You re-familiarize yourself with your surroundings.
<<show_ui ClickToInteract>>
<<endonce>>
===

title: AfterInteractedWithItems
position: 315,-648
---
<<fade_transition 2>>
<<wait 1>>
<<hide_all_objects>>
<<hide_ui ClickToInteract>>
<<show Stars>>
<<show Sitting>>
This is the life that you've committed yourself to, ever since taking the job to pilot this small exploration vessel.
You are tasked with reporting astronomical oddities back to Earth, as the first line of defense against the unknown.
But deep inside, you knew this was just to get a chance away from Earth.
Nothing of interest seems to be happening. You start to spiral, sitting and pondering the emptiness around you.
    -> (contemplate your life on Earth)
        You ponder the vastness of existence and your place within it.
        The silence of space seems to amplify your thoughts, making you feel both insignificant and connected to the universe.
        -> (hone in on the positive)
            You think back to a time you felt connected with someone on Earth, recalling a time when your mother taught you how to bake cookies.
            Although she's gone now, the thought brings a smile to your face. 
            <<set $posSelfPerception += 1>>
            It's a small comfort in the loneliness of space. You feel <color=\#88FF88>slightly happier</color>.
        -> (dwell on the negative)
            You think back about a time you felt insignificant on Earth.
            Ever since you dropped out of college, you've felt like a disappointment to your family.
            <<set $negSelfPerception += 1>>
            You scoff at the concept of unconditional love. In doing so, you <color=\#FF8888>feel sadder</color>, overwhelmed by the vast emptiness around you.
    -> (think of something happy)
        You think back to your time on Earth, recalling a time when your mother taught you how to bake cookies.
        Although she's gone now, the thought brings a smile to your face. 
        <<set $posSelfPerception += 1>>
        It's a small comfort in the loneliness of space. You feel <color=\#88FF88>slightly happier</color>.
    -> (do some push-ups)
        You were never huge on fitness, but you were required to keep in shape for space travel.
        Neither of your parents supported your decision to leave Earth. And now, they're gone. 
        You figure that staying in shape is the least you can do to prove them wrong.
        <<set $posSelfPerception += 1>>
        You do a few push-ups to keep your strength up. Strangely enough, you feel <color=\#88FF88>a little happier</color> afterwards.
You decide to get back to doing something. Anything is better than sitting around.
<<hide_all_objects>>
<<show PCRoom>>
<<jump DoJobFirstTime>>
===

title: DoJobFirstTime
position: 308,-448
---
You stand up and turn on the computer. 
As part of your job, you need to complete a daily report on your findings.
Given they're all the same, you're not sure HQ pays much attention to them. But at least, it gives you a sense of routine.
<<show MonitorReportDay1>>
<<show_ui TypeToInteract>>
===

title: AfterTypedFirstTime
position: 15,-241
---
<<hide_ui TypeToInteract>>
You sigh, after having written the same report for the 620th time. 
The report is done for the day.
    -> (turn off the monitor)
<<hide MonitorReportDay1>>
<<jump MonitorOn>>
===

title: MonitorOn
position: 15,-63
---
<<hide_all_objects>>
<<show PCRoom>>
<<show MonitorOn>>
Just as you turn the monitor off, it flickers and springs back to life.
<<show_character Cat Obscure right>>
A dark figure appears on the screen, accompanied with a static-filled voice.
<<start_voice_blip Cat>>
???: Hello? <i>*bzzt*</i> Hello? Can... hear...?
    -> Hello?
    -> ...
    The voice from the monitor continues to speak.
    ???: Please... <i>*kshh*</i> if... there... yourself... known...
    <<stop_voice_blip Cat>>
    <<jump EnterMessage>>
===

title: EnterMessage
position: 312,-227
---
<<start_voice_blip Cat>>
???: Click... <i>*bzzt*</i> comm... cation... panel...
<<stop_voice_blip Cat>>
With your expertise, you think you can adjust the frequency and let the signal through.
    -> (let the signal through)
        <<jump CatIntro>>
    -> (ignore the signal. it's probably nothing.)
        You decide to ignore the signal. It's probably just static interference.
        <<hide_all_characters>>
        <<hide MonitorOn>>
        <<show MonitorOn>>
        <<set $negSelfPerception += 1>>
        After a few moments, the screen flickers back to static. You are alone, again.
        How should you pass the time?
            -> (block them out of your head)
                <<set $negSelfPerception += 1>>
                You decide to play a game on the monitor to pass the time.
                The game is simple, but it helps distract you from the loneliness.
            -> (try to find the signal again)
                You fiddle with the dials and knobs, trying to find the signal again.
<<hide_all_objects>>
<<show PCRoom>>
<<show MonitorOn>>
<<show_character Cat Obscure right>>
Suddenly, the same character from before reappears on the screen.
    -> (get it over with. let the signal through.)
        <<jump CatIntro>>
    -> (unplug the monitor)
        <<hide_all_characters>>
        <<hide MonitorOn>>
        You decide to unplug the monitor, cutting off the signal entirely.
        After a few moments, the screen flickers back to static. You are alone, again.
        <<jump ForeverAloneNoContact>>
===

title: CatIntro
position: 305,91
---
Suddenly, the figure comes into view.
<<show_character Cat Sad right>>
<<start_voice_blip Cat>>
<<play_music CatTheme 0.5>>
???: Hey, I caught your signal from our ship! Are you alright?
    -> ...What?
    -> Aww, you're so cute!
        <<positive_indicator Cat>>
        <<set $catLikesYou += 1>>
        <<show_character Cat Neutral right>>
        ???: Oh, um... Thank... you?
        <<show_character Cat Sad right>>
        ???: But seriously, are you okay out there? You look like you're all alone.
            -> Yeah, I'm fine.
                <<show_character Cat Thinking right>>
                ???: Hm, alright. That's at least somewhat reassuring.
            -> Not really.
                <<show_character Cat Neutral right>>
                ???: Oh... I'm sorry to hear that. Space can be a lonely place.
            -> (scream in confusion)
                <<stop_voice_blip Cat>>
                <<set $catDislikesYou += 1>>
                <<show_character Cat Surprised right>>
                You scream deafeningly loudly. The creature on the monitor jumps back in surprise.
                <<start_voice_blip Cat>>
                ???: AH! That... surely is a strange introduction.
                <<show_character Cat Sad right>>
                ???: Please try to spare my ears next time... these are quite sensitive.
    -> (scream. what is happening.)
        <<stop_voice_blip Cat>>
        <<set $catDislikesYou += 1>>
        <<show_character Cat Surprised right>>
        You scream deafeningly loudly. The creature on the monitor jumps back in surprise.
        <<start_voice_blip Cat>>
        ???: AH! That... surely is a strange introduction.
        <<show_character Cat Sad right>>
        ???: Please try to spare my ears next time... these are quite sensitive.
<<start_voice_blip Cat>>
<<show_character Cat Thinking right>>
???: You don't seem to act like you're in danger. What're you doing in space?
    -> What the fuck? What <i>are</i> you?
        <<show_character Cat Neutral right>>
        <<set $catDislikesYou += 1>>
        ???: ...don't be rude.
        ???: It's not everyday that you meet another lifeform just floating around in space.
    -> Who... what... are you?
    -> Why do you want to know?
        <<set $catDislikesYou += 1>>
        <<show_character Cat Thinking right>>
        ???: I guess it's not entirely my business... but I was just curious.
<<if $catLikesYou >= $catDislikesYou>>
<<show_character Cat Happy right>>
{$catName}: Oh! I'm so sorry, I should introduce myself. My name is 猫喵猫猫喵, but you can just call me {$catName}.
<<show_character Cat Happy2 right>>
<<else>>
{$catName}: Anyways, my name is 猫喵猫猫喵. But to keep things simple, you can just call me {$catName}.
<<show_character Cat Neutral right>>
<<endif>>
{$catName}: I'm from the planet Linnaeus. It's nice to meet you.
    -> You're an alien?
        <<show_character Cat Thinking right>>
        <<set $catDislikesYou += 1>>
        {$catName}: Hm? I guess so?
        <<show_character Cat Neutral right>>
        {$catName}: I'm not from your planet of origin. I come from a place far away.
        <<show_character Cat Angry right>>
        {$catName}: "Alien" is a bit demeaning, though, don't you think?
    -> 猫喵猫猫喵? What name is that?
        <<show_character Cat Thinking right>>
        {$catName}: It's... hard to explain.
        <<show_character Cat Happy2 right>>
        {$catName}: Just call me {$catName}, it's easier that way.
    -> Meowdy.
        <<show_character Cat Thinking right>>
        {$catName}: ...Meowdy? That's an interesting way to greet someone.
        <<show_character Cat Flirt right>>
        {$catName}: I guess, meowdy to you too.
<<show_character Cat Happy right>>
{$catName}: But anyways, I'm glad you're safe... um... 
<<show_character Cat Thinking right>>
{$catName}: I guess... you're a human, right?
{$catName}: I've heard stories about your kind.
{$catName}: Is it true that you walk on two legs and have opposable thumbs?
-> Yeah, it's pretty cool.
    <<show_character Cat Happy2 right>>
    <<set $catLikesYou += 1>>
    <<positive_indicator Cat>>
    {$catName}: That's incredible! You must be very dexterous.
-> No. What?
    <<show_character Cat Sad right>>
    {$catName}: Aw, that's disappointing. I was hoping to see some cool tricks.
<<show_character Cat Thinking right>>
{$catName}: What should I call you?
    -> My name's {$playerName}.
        <<show_character Cat Happy right>>
        <<set $catLikesYou += 1>>
        <<positive_indicator Cat>>
        <<set $catKnowsPlayerName = true>>
        <<if $playerName == "Dipper">>
            {$catName}: {$playerName}? Like the star system? That's pretty cute.
        <<else>>
            {$catName}: Ah, {$playerName}! It's nice to meet you.
        <<endif>>
        <<set $catThinksPlayerNameIs = $playerName>>
    -> I don't know you...
        <<show_character Cat Sad right>>
        <<set $catDislikesYou += 1>>
        {$catName}: Aw, that's fine. I guess I was a little forward.
        <<show_character Cat Neutral right>>
        {$catName}: But, just to make things easier, is there something you'd like me to call you?
        -> It's {$playerName}.
            {$catName}: {$playerName}? That's a nice name.
            <<set $catThinksPlayerNameIs = $playerName>>
        -> I don't have a name.
            <<show_character Cat Thinking right>>
            {$catName}: Oh, huh. That's okay. How would people refer to you then, though?
            <<show_character Cat Flirt right>>
            {$catName}: I guess I'll just call you "Human". I'm a master at nicknames.
    -> ...
        {$catName}: A bit shy? That's okay, I guess. 
        <<show_character Cat Flirt right>>
        {$catName}: I guess I'll just call you "Human", then. I'm a master at nicknames.
<<show_character Cat Neutral right>>
{$catName}: You should be careful here, though. Space is dangerous!
<<show_character Cat Thinking right>>
{$catName}: What brings you out here? Wouldn't you rather be on your home planet?
    -> Escaping it all, I guess.
        <<show_character Cat Neutral right>>
        <<positive_indicator Cat>>
        <<set $catLikesYou += 1>>
        {$catName}: I understand that feeling. Sometimes, it's nice to get away from everything.
    -> (lie) Exploring the cosmos.
        <<set $catDislikesYou += 1>>
        <<show_character Cat Sad right>>
        {$catName}: Oh, wow... you'd risk your life for something like that?
        <<show_character Cat Neutral right>>
        {$catName}: That's... quite brave. Maybe a little suicidal, too.
    -> I'm... not sure.
        <<show_character Cat Thinking right>>
        {$catName}: Hm, that's okay. Sometimes, we don't have all the answers right away.
        {$catName}: It might be in your best interest to find somewhere safer, though. It's pretty unpredictable out here.
// <<show_character Cat Neutral right>>
// {$catName}: Well, I'm glad to hear that you're fine.
//     -> What... are you?
//         <<show_character Cat Happy right>>
//         {$catName}: As I mentioned, I'm from the planet Linnaeus, though I don't expect you've heard of it.
//         <<show_character Cat Happy2 right>>
//         {$catName}: We're a peaceful species, mostly just trying to explore the galaxy and make friends.
//         -> I find that hard to believe.
//             <<show_character Cat Sad right>>
//             <<set $catDislikesYou += 1>>
//             {$catName}: Why would you say that? I thought you seemed nice.
//         -> It's.. nice to meet you.
//             <<show_character Cat Happy right>>
//             <<set $catLikesYou += 1>>
//             {$catName}: Heh, likewise. It's been nice to meet you too, {$catThinksPlayerNameIs}!
//     -> Why are you talking to me?
//         <<show_character Cat Flirt right>>
//         {$catName}: Well, you know what they say. Curiosity killed the catus linnaeus!
//         <<show_character Cat Happy right>>
//         {$catName}: I was on patrol for my planet, and I picked up your ship's signal. I thought I'd check in and see if you were alright.
//         {$catName}: I don't suppose you've heard of Linnaeus, but we're a peaceful species, mostly just trying to explore the galaxy and make friends.
//         -> Peaceful? I find that hard to believe.
//             <<show_character Cat Sad right>>
//             <<set $catDislikesYou += 2>>
//             <<set $negSelfPerception += 1>>
//             {$catName}: There are plenty of nice folks out here. We're not all bad!
//         -> It's.. nice to meet you.
//             <<show_character Cat Happy right>>
//             <<set $catLikesYou += 1>>
//             {$catName}: Heh, likewise. It's been nice to meet you too, {$catThinksPlayerNameIs}!
<<show_character Cat Surprised right>>
{$catName}: Ah! Right, I have a meeting to get to. It's not my plan to keep you here for over an orbit.
<<if $catLikesYou >= $catDislikesYou>>
    <<show_character Cat Happy right>>
    {$catName}: Let's talk again soon, though. I'm interested in getting to know more about you, {$catThinksPlayerNameIs}.
    <<show_character Cat Flirt right>>
    {$catName}: If you want to chat again, just give me a ping. See ya!
    <<set $posSelfPerception += 1>>
    <<set $catSecured = true>>
<<else>>
    <<show_character Cat Neutral right>>
    {$catName}: Well, I'll get going, {$catThinksPlayerNameIs}. Take care of yourself out here.
<<endif>>
<<stop_voice_blip Cat>>
<<hide_all_characters>>
<<hide MonitorOn>>
<<show MonitorOn>>
<<play_music MainTheme>>
<<if $catSecured == false>>
    <<deactivate_icon Cat>>
<<endif>>
The transmission cuts off as {$catName} takes their leave.
<<if $catSecured == true>>
    {$catName} uploads a sequence of radio waves in file format onto your device, letting you ping them again in the future.
    Suddenly, the screen flickers back to static. {$catName} is gone, for now.
        -> {$catName}? Can you hear me?
            You tap the monitor and move around the dials, but nobody responds. You are alone, again.
        -> (sit down)
<<endif>>
<<jump ThinkingAfterCatLeaves>>
===

title: ThinkingAfterCatLeaves
---
<<fade_transition 2>>
<<wait 1>>
<<hide_all_objects>>
<<show Stars>>
<<show Sitting>>
You sit back down, trying to process the encounter you just had.
Who was that creature? How is that even possible?
<<if $catLikesYou > $catDislikesYou>>
    <<set $posSelfPerception += 1>>
    Strangely enough, you enjoyed the encounter with them. They seemed... friendly.
    What would it be like to have a friend like that?
    You wonder if you'll ever see them again.
<<else>>
    <<set $negSelfPerception += 1>>
    To you, the whole encounter felt off. Aliens? Of all times, why now?
    You think about what others on Earth would say if they found out about this encounter. Although it is your job to report such things, would they really believe you met friendly animal aliens?
    Maybe, you'll think about it after a night's rest.
<<endif>>
<<hide_all_objects>>
<<fade_transition 2>>
<<wait 1>>
<<show PCRoom>>
<<show MonitorOn>>
<<show_character Dog Obscure left>>
<<show_character Opossum Obscure center>>
<<show_character Bird Obscure right>>
Yet, as you sit there, lost in thought, the monitor flickers back to life once more.
You see a total of three figures, all different from the last one.
They all seem like they'll stick around for a bit. Who do you talk to first?
    -> (respond to the one with the floppy ears)
        <<set $talkedToDog = true>>
        <<jump DogIntro>>
    -> (respond to the one with the frazzled hair)
        <<set $talkedToOpossum = true>>
        <<jump OpossumIntro>>
    -> (respond to the one with the pointy muzzle)
        <<set $talkedToBird = true>>
        <<jump BirdIntro>>
===

title: TalkToOthers
---
<<hide_all_characters>>
<<show MonitorOn>>
<<if $talkedToDog == false>>
    <<show_character Dog Obscure left>>
<<endif>>
<<if $talkedToOpossum == false>>
    <<show_character Opossum Obscure center>>
<<endif>>
<<if $talkedToBird == false>>
    <<show_character Bird Obscure right>>
<<endif>>
You notice that others are still live on the feed. Who do you talk to next?
    -> (respond to the one with the floppy ears) <<if $talkedToDog == false>>
        <<set $talkedToDog = true>>
        <<jump DogIntro>>
    -> (respond to the one with the frazzled hair) <<if $talkedToOpossum == false>>
        <<set $talkedToOpossum = true>>
        <<jump OpossumIntro>>
    -> (respond to the one with the pointy muzzle) <<if $talkedToBird == false>>
        <<set $talkedToBird = true>>
        <<jump BirdIntro>>
===

title: DogIntro
position: 635,249
---
<<declare $knowsAboutBarklonian = false>>
<<hide_all_characters>>
<<hide_all_objects>>
<<show PCRoom>>
<<show MonitorOn>>
You pull up the communication panel and respond to the figure with the floppy ears.
<<show_character Dog Obscure right>>
You turn the dials to let the signal through.
<<show_character Dog Neutral right>>
<<start_voice_blip Dog>>
<<play_music DogTheme 0.5>>
???: Oh, woofdy. I didn't think anyone would answer my signal.
???: What're you doing out here? Woof.
    -> Who are you?
    -> What in the world? A purple dog?
        <<set $dogDislikesYou += 1>>
        <<stop_voice_blip Dog>>
        <<show_character Dog Thinking right>>
        The figure on the monitor tilts their head in confusion. They seem a little offended.
        <<start_voice_blip Dog>>
        {$dogName}: Woof? A dog? Woof. I'm not sure what you mean. Woof.
        {$dogName}: I'm a Barklonian, if you've heard of us. A former one. Woof.
        <<set $knowsAboutBarklonian = true>>
    -> ...
<<start_voice_blip Dog>>
<<show_character Dog Happy right>>
{$dogName}: I'm {$dogName}. Woof. I come in peace. Woof.
{$dogName}: I saw your signal, and thought I'd come check it out. Woof.
    -> What... are you?
        <<positive_indicator Dog>>
        <<set $dogLikesYou += 1>>
        <<if $knowsAboutBarklonian == false>>
            <<set $knowsAboutBarklonian = true>>
            {$dogName}: I don't know what you'd call it. I'm a Barklonian. Woof.
        <<else>>
            {$dogName}: Like I said, I'm a Barklonian. Woof.
        <<endif>>
        <<show_character Dog Thinking right>>
        {$dogName}: Just like you, I'm exploring space. Been out here for some days now. Woof.
        <<show_character Dog Neutral right>>
        {$dogName}: It's a big place out here. I kind of like it. Woof.
        -> But... where do you come from?
            {$dogName}: Hm? Oh, I come from a place far away. Woof. Haven't been there in a while. Not many of us stay.
            {$dogName}: It's a planet called Barklon. Woof.
            {$dogName}: It's a nice place. Lots of open fields to run around in. Woof.
        -> And you're a dog?
            <<show_character Dog Surprised right>>
            {$dogName}: A dog? What is that? Woof.
            <<show_character Dog Neutral right>>
            {$dogName}: I should look into this "dog" thing. Woof.
            <<show_character Dog Happy right>>
            {$dogName}: But no, I'm from a planet called Barklon. Woof.
            <<show_character Dog Sad right>>
            {$dogName}: It's... a pretty miserable place. Would not recommend it. Lots of bad people. Woof.
        -> ...
    -> Why do you keep saying woof?
        {$dogName}: I don't know what you mean. Woof.
        -> It's... weird.
            <<set $dogDislikesYou += 1>>
            <<set $negSelfPerception += 1>>
            <<show_character Dog Sad right>>
            {$dogName}: Hm. Woof. I'm sorry if it bothers you. Woof.
            {$dogName}: It's just how we talk back home. Woof.
        -> I think it's cute.
            <<positive_indicator Dog>>
            <<set $dogLikesYou += 1>>
            <<show_character Dog Happy right>>
            {$dogName}: Woof! Even though I don't know what I'm doing, I'm glad you like it. Woof.
            {$dogName}: It's just how we talk back home. Woof.
        -> ...
    -> It's... nice to meet you. I'm {$playerName}.
        <<positive_indicator Dog>>
        <<set $dogLikesYou += 1>>
        <<set $dogThinksPlayerNameIs = $playerName>>
        <<set $dogKnowsPlayerName = true>>
        <<show_character Dog Happy right>>
        {$dogName}: Woof! Hi {$dogThinksPlayerNameIs}!
        <<if $playerName == "Dipper">>
            {$dogName}: That's a pretty silly name. It rhymes with Bipper. Woof.
        <<endif>>
<<show_character Dog Neutral right>>
{$dogName}: I hope you don't mind me dropping by unannounced. Woof.
    -> Why... are you here?
        <<set $dogDislikesYou += 1>>
        <<show_character Dog Sad right>>
        {$dogName}: Woof. Do you want me to go? Woof.
        {$dogName}: I just saw your signal and thought I'd come check it out. Woof.
    -> What are you doing here?
        {$dogName}: Oh, I was just passing by. Woof.
    -> Do you meet others often?
        <<show_character Dog Happy right>>
        {$dogName}: So many people! It's hard to keep track. Woof.
        <<if $dogLikesYou > $dogDislikesYou>>
            <<show_character Dog Happy right>>
            {$dogName}: You're pretty friendly, though. Woof. Don't see many of those around here.
        <<else>>
            <<show_character Dog Neutral right>>
            {$dogName}: You seem okay, though. Woof. Not many friendly folks out here.
        <<endif>>
{$dogName}: But it's nice to meet someone as nice as you. Woof. 
<<show_character Dog Sad right>>
{$dogName}: Back where I come from, there are a lot of unfriendly folks. Woof. They... hurt each other a lot. Woof.
<<show_character Dog Neutral right>>
<<if $dogKnowsPlayerName == false>>
    <<set $dogKnowsPlayerName = true>>
    {$dogName}: But. Woof. What should I call you?
        -> {$playerName}.
            <<positive_indicator Dog>>
            <<set $dogLikesYou += 1>>
            <<set $dogThinksPlayerNameIs = $playerName>>
            <<set $dogKnowsPlayerName = true>>
            {$dogName}: Woof! {$dogThinksPlayerNameIs}, that's a nice name.
            <<if $playerName == "Dipper">>
                {$dogName}: It rhymes with Bipper. Woof.
            <<endif>>
        -> Hurt... each other?
            <<show_character Dog Sad right>>
            {$dogName}: Woof... it's terrible. People I love are still back there. Woof.
            {$dogName}: But... I don't really want to think about that now. Woof. 
            <<show_character Dog Neutral right>>
            {$dogName}: What should I call you? Woof.
                -> {$playerName}.
                    <<positive_indicator Dog>>
                    <<set $dogLikesYou += 1>>
                    <<set $dogThinksPlayerNameIs = $playerName>>
                    <<set $dogKnowsPlayerName = true>>
                    {$dogName}: Woof! {$dogThinksPlayerNameIs}, that's a nice name.
                    <<if $playerName == "Dipper">>
                        {$dogName}: It rhymes with Bipper. Woof.
                    <<endif>>
                -> ...
                    <<set $dogDislikesYou += 1>>
                    {$dogName}: Not a talker? Woof. I guess I'll just call you "Human". You're a human, right? Woof.
        -> ...
            <<set $dogDislikesYou += 1>>
            {$dogName}: Not a talker? Woof. I guess I'll just call you "Human". You're a human, right? Woof.
<<endif>>
{$dogName}: Anyways, I've been wandering around, recently. Woof. Picked up on your signal. Woof.
    -> That sounds pretty exciting. 
        <<show_character Dog Happy right>>
        <<set $posSelfPerception += 1>>
        {$dogName}: Yeah! I like to wander around and see new places. Woof. 
        {$dogName}:  I've traveled hundreds of barksecs from home, at this point. Woof.
    -> That sounds pretty lonely.
        <<show_character Dog Thinking right>>
        <<set $negSelfPerception += 1>>
        <<show_character Dog Sad right>>
        {$dogName}: Lonely? Woof. It can be, sometimes. Woof.
        {$dogName}: Haven't seen my family in a while. Woof.
        <<show_character Dog Neutral right>>
        {$dogName}: But, at least I get to meet new beings all the time. Woof.
    -> Do you do this often?
        <<show_character Dog Sad right>>
        {$dogName}: Mmm... not really. Woof.
    <<show_character Dog Angry right>>
    {$dogName}: My home planet is kind of a dump. I decided to leave and explore the galaxy. Woof.
<<show_character Dog Neutral right>>
{$dogName}: What about you? What are you doing out here? Woof.
    -> I'm escaping my home planet.
        <<set $dogLikesYou += 1>>
        <<positive_indicator Dog>>
        <<show_character Dog Happy right>>
        {$dogName}: Oh? Woof. That's very brave of you.
        {$dogName}: I did the same thing. Woof. Sometimes, it's nice to get away from everything. Woof.
    -> My job is to report alien life.
        <<show_character Dog Thinking right>>
        {$dogName}: Alien life? What for? Woof.
        {$dogName}: I don't see the point in all that. Woof. Why?
            -> My planet, Earth, is searching for them.
                <<show_character Dog Sad right>>
                <<set $dogDislikesYou += 1>>
                {$dogName}: That sounds a little scary... 
                {$dogName}: I hope that doesn't mean your planet is out to get me. Woof.
            -> I'm here to meet new beings.
                <<positive_indicator Dog>>
                <<set $dogLikesYou += 1>>
                <<show_character Dog Happy right>>
                {$dogName}: Woof. You should join me sometime. I see a lot of interesting beings everyday.
            -> Earth wants to destroy alien life.
                <<set $dogDislikesYou += 2>>
                <<show_character Dog Sad right>>
                {$dogName}: That sounds a little scary... 
                {$dogName}: I hope that doesn't mean your planet is out to get me. Woof.
    -> (lie) I'm adventuring, like you.
        <<positive_indicator Dog>>
        <<set $dogLikesYou += 1>>
        {$dogName}: Adventuring? Woof. We should go together sometime!
<<show_character Dog Neutral right>>
{$dogName}: Anyways, I should get going. Woof.
{$dogName}: Space is a big place, and I have a lot of wandering to do. Woof.
<<if $dogLikesYou >= $dogDislikesYou>>
    <<show_character Dog Flirt right>>
    {$dogName}: If you ever need anything, just send out a signal. Woof. I'll come find you.
    <<set $dogSecured = true>>
<<else>>
    <<show_character Dog Neutral right>>
    {$dogName}: Well, it was cool meeting you. Bye! Woof.
<<endif>>
<<stop_voice_blip Dog>>
<<hide_all_characters>>
<<play_music MainTheme>>
<<if $dogSecured == false>>
    <<deactivate_icon Dog>>
<<endif>>
The transmission cuts off as {$dogName} leaves to continue their wandering.
<<if $dogSecured == true>>
    You notice a transmission from {$dogName} saved onto your monitor. You can likely use this to ping them again in the future.
<<endif>>
<<if $talkedToOpossum == false || $talkedToBird == false>>
    <<jump TalkToOthers>>
<<else>>
    <<jump EndDay1>>
<<endif>>
===

title: OpossumIntro
position: 638,-61
---
<<hide_all_characters>>
<<hide_all_objects>>
<<show PCRoom>>
<<show MonitorOn>>
You pull up the communication panel and respond to the figure with the frazzled hair.
<<show_character Opossum Obscure right>>
You turn the dials to let the signal through.
<<show_character Opossum Neutral right>>
<<start_voice_blip Opossum>>
<<play_music_start_loop OpossumThemeStart OpossumThemeLoop 0.4 0.5>>
???: Oh, hey. I didn't expect anyone to actually answer.
???: I... don't really chat with others much. It's cool to meet you, I guess.
    -> Holy shit. Are you a rat?
        <<show_character Opossum Angry right>>
        ???: What the fuck is a rat...
        <<set $opossumDislikesYou += 1>>
        ???: Forget I asked. Peace.
            -> Wait, don't go...
                <<show_character Opossum Neutral right>>
                ???: ...
                {$opossumName}: The name's {$opossumName}.
            -> ...
                <<stop_voice_blip Opossum>>
                <<hide_all_characters>>
                <<play_music MainTheme>>
                <<deactivate_icon Opossum>>
                The transmission cuts off abruptly. It seems they didn't want to talk to you anymore.
                <<if $talkedToDog == false || $talkedToBird == false>>
                    <<jump TalkToOthers>>
                <<else>>
                    <<jump EndDay1>>
                <<endif>>
    -> What are you?
        {$opossumName}: The name's {$opossumName}. I'm... from nowhere, really.
    -> What's your story?
        <<show_character Opossum Confused right>>
        ???: Nothing I'm comfortable with sharing with a stranger. I'm just from a little messed up planet in the middle of nowhere.
        <<show_character Opossum Neutral right>>
        {$opossumName}: The name's {$opossumName}, by the way.
{$opossumName}: And you? What should I call you?
    -> I'm {$playerName}.
        <<set $opossumThinksPlayerNameIs = $playerName>>
        <<show_character Opossum Happy right>>
        <<set $opossumKnowsPlayerName = true>>
        <<positive_indicator Opossum>>
        <<set $opossumLikesYou += 1>>
        {$opossumName}: That's a pretty sweet name. I dig it.
    -> A human.
        <<show_character Opossum Thinking right>>
        <<positive_indicator Opossum>>
        <<set $opossumLikesYou += 1>>
        {$opossumName}: A human, huh? That's pretty metal. Never met one of you before.
        {$opossumName}: "Human" it is.
    -> What the fuck?
        <<show_character Opossum Confused right>>
        {$opossumName}: Whoa, no need to be rude. I can just go, if you want to.
            -> Yes, please.
                <<stop_voice_blip Opossum>>
                <<hide_all_characters>>
                <<play_music MainTheme>>
                <<deactivate_icon Opossum>>
                The transmission cuts off abruptly. You have a feeling they won't come back.
                <<if $talkedToDog == false || $talkedToBird == false>>
                    <<jump TalkToOthers>>
                <<else>>
                    <<jump EndDay1>>
                <<endif>>
            -> ...
                <<show_character Opossum Confused right>>
                {$opossumName}: Okay, I can sense when I'm not wanted.
                <<stop_voice_blip Opossum>>
                <<hide_all_characters>>
                <<play_music MainTheme>>
                <<deactivate_icon Opossum>>
                The transmission cuts off abruptly. You have a feeling they won't come back.
                <<if $talkedToDog == false || $talkedToBird == false>>
                    <<jump TalkToOthers>>
                <<else>>
                    <<jump EndDay1>>
                <<endif>>
            -> No, stay.
                <<show_character Opossum Flirt right>>
                <<positive_indicator Opossum>>
                <<set $opossumLikesYou += 1>>
                {$opossumName}: ...alright, if you say so.
<<show_character Opossum Thinking right>>
<<stop_voice_blip Opossum>>
You notice that {$opossumName} seems a bit distant, not making eye contact with you.
    -> So, what are you doing here?
        <<set $opossumDislikesYou += 1>>
        {$opossumName} sighs heavily. They seem annoyed by your question.
    -> What's wrong?
    -> ...
<<start_voice_blip Opossum>>
<<show_character Opossum Thinking right>>
{$opossumName}: I don't know. Space just has a lot of weirdos.
{$opossumName}: I feel like I just don't really have anywhere else to go.
<<show_character Opossum Angry right>>
{$opossumName}: God... I <i>hate</i> space. It's so... <i>empty</i>. 
    -> What are you doing out here, then?
        <<show_character Opossum Confused right>>
        <<set $opossumDislikesYou += 1>>
    -> I find it relaxing.
        <<show_character Opossum Confused right>>
        <<set $opossumDislikesYou += 1>>
    -> ...
<<show_character Opossum Sad right>>
{$opossumName}: Ah! Sorry, I didn't mean to unload all that on you.
<<show_character Opossum Flirt right>>
{$opossumName}: Look, I reached out because I'm looking for some people to talk to.
<<show_character Opossum Neutral right>>
{$opossumName}: I don't really have anywhere else to go right now, so it'd be nice to have a bit of company.
    -> Sure, I can do that.
        <<show_character Opossum Confused right>>
        <<positive_indicator Opossum>>
        <<set $opossumLikesYou += 1>>
        {$opossumName}: R-really? Just like that?
    -> I don't really know you.
        <<show_character Opossum Confused right>>
        {$opossumName}: That's... fair.
        <<show_character Opossum Neutral right>>
        {$opossumName}: Thanks for dealing with me being around for a while, anyways.
    -> Go away!
        <<show_character Opossum Angry right>>
        <<set $opossumDislikesYou += 2>>
        {$opossumName}: ...
        {$opossumName}: I don't even know why I try...
        <<stop_voice_blip Opossum>>
        <<hide_all_characters>>
        <<play_music MainTheme>>
        <<deactivate_icon Opossum>>
        The transmission cuts off abruptly. You have a feeling they won't come back.
        <<if $talkedToDog == false || $talkedToBird == false>>
            <<jump TalkToOthers>>
        <<else>>
            <<jump EndDay1>>
        <<endif>>
<<if $opossumLikesYou >= $opossumDislikesYou>>
    <<show_character Opossum Happy right>>
    <<set $opossumSecured = true>>
    {$opossumName}: Thanks for listening. Though... let's talk for real, later.
<<else>>
    <<show_character Opossum Thinking right>>
    {$opossumName}: Sorry for that. You probably don't want to hear about my problems. I'll leave you alone.
<<endif>>
<<if $opossumSecured == false>>
    <<deactivate_icon Opossum>>
<<endif>>
<<stop_voice_blip Opossum>>
<<hide_all_characters>>
<<play_music MainTheme>>
The transmission cuts off abruptly. {$opossumName} takes their leave.
<<if $opossumSecured == true>>
    You notice a transmission from {$opossumName} recorded on your monitor. They're letting you ping them again in the future.
<<endif>>
<<if $talkedToDog == false || $talkedToBird == false>>
    <<jump TalkToOthers>>
<<else>>
    <<jump EndDay1>>
<<endif>>
===

title: BirdIntro
position: 633,86
---
<<declare $birdExplainedIntention = false>>
<<hide_all_characters>>
<<hide_all_objects>>
<<show PCRoom>>
<<show MonitorOn>>
You pull up the communication panel and respond to the figure with the pointy muzzle.
<<show_character Bird Obscure right>>
You turn the dials to let the signal through.
<<show_character Bird Happy2 right>>
<<start_voice_blip Bird>>
<<play_music BirdTheme 0.25>>
???: Ah, hello, alien! It's so nice to meet you.
<<show_character Bird Nervous right>>
{$birdName}: I'm {$birdName}, a scientist of the planet Aviaris and... um, seeker of knowledge.
    -> What... are you?
        <<show_character Bird Confused right>>
        {$birdName}: Oh, right. I take it you haven't heard of us before.
        <<show_character Bird Neutral right>>
        {$birdName}: I'm an Avianthrope, hailing from the planet Aviaris.
    -> Aviaris? Is that a word?
        <<show_character Bird Confused right>>
        {$birdName}: Oh, right. I take it you haven't heard of us before.
        <<show_character Bird Neutral right>>
        {$birdName}: It's just the name of my home planet! Don't worry, I come in peace.
        {$birdName}: I'm an Avianthrope, and Aviaris is the name of our planet.
    -> ...
<<stop_voice_blip Bird>>
<<show_character Bird Nervous right>>
You notice that the figure seems a little nervous. They fidget slightly as they speak.
<<start_voice_blip Bird>>
{$birdName}: If you don't mind, I have a few questions about your species. It's nothing intrusive, I promise.
    -> Sure..?
        <<positive_indicator Bird>>
        <<set $birdLikesYou += 1>>
        <<show_character Bird Happy right>>
        {$birdName}: Wonderful! I appreciate your cooperation.
    -> I'd rather not.
        <<set $birdDislikesYou += 1>>
        <<show_character Bird Nervous right>>
        {$birdName}: Hm. I see. Are you sure? It wouldn't take up too much of your time.
            -> Yes, I'm sure.
                <<show_character Bird Sad right>>
                {$birdName}: Okay then, I know when I'm not wanted. Farewell.
                <<stop_voice_blip Bird>>
                <<hide_all_characters>>
                <<play_music MainTheme>>
                <<deactivate_icon Bird>>
                The transmission cuts off abruptly. That was strange.
                <<if $talkedToDog == false || $talkedToOpossum == false>>
                    <<jump TalkToOthers>>
                <<else>>
                    <<jump EndDay1>>
                <<endif>>
            -> ...fine.
                <<positive_indicator Bird>>
                <<set $birdLikesYou += 1>>
                <<show_character Bird Happy right>>
                {$birdName}: Thank you! I appreciate it.
    -> What are you studying?
            <<show_character Bird Happy right>>
            {$birdName}: Ah, well, I'm primarily interested in exploring the unknown.
            <<show_character Bird Thinking right>>
            {$birdName}: It's... hard to explain why I'm so interested in this. It's been a fascination of mine recently.
            {$birdName}: I'm just curious about other lifeforms. Understanding others helps me understand myself better, I think.
            <<set $birdExplainedIntention = true>>
    -> I have so many questions...
        <<set $birdDislikesYou += 1>>
        <<show_character Bird Sad right>>
        {$birdName}: Oh... I apologize if I was too forward!
        <<show_character Bird Nervous right>>
        {$birdName}: If you want, I suppose I can answer a question of yours first.
        -> What are you studying?
            <<show_character Bird Happy right>>
            {$birdName}: Ah, well, I'm primarily interested in exploring the unknown.
            <<show_character Bird Thinking right>>
            {$birdName}: It's... hard to explain why I'm so interested in this. It's been a fascination of mine recently.
            {$birdName}: I suppose I'm just curious about other lifeforms. Understanding others helps me understand myself better, I think.
            <<set $birdExplainedIntention = true>>
        -> What is like being a bird? Can you fly?
            <<show_character Bird Confused right>>
            {$birdName}: Oh, um. I don't know what a "bird" is. But, I can fly, yes.
            {$birdName}: It's... hard to explain what it's like. It's just part of who I am, I suppose.
            <<show_character Bird Sweat right>>
            {$birdName}: I'm not even going to ask how you knew I <i>had</i> wings.
        -> Why do you want to know about me?
            <<show_character Bird Thinking right>>
            {$birdName}: Hm... it's pretty hard to pinpoint.
            {$birdName}: I suppose I'm just curious about other lifeforms. Understanding others helps me understand myself better, I think.
            <<set $birdExplainedIntention = true>>
        -> I don't want to answer your questions.
            <<show_character Bird Sad right>>
            {$birdName}: Okay then. Thanks for your time anyways. Bye.
            <<stop_voice_blip Bird>>
            <<hide_all_characters>>
            <<play_music MainTheme>>
            <<deactivate_icon Bird>>
            The transmission cuts off abruptly. That was strange.
            <<if $talkedToDog == false || $talkedToOpossum == false>>
                <<jump TalkToOthers>>
            <<else>>
                <<jump EndDay1>>
            <<endif>>
<<show_character Bird Happy right>>
{$birdName}: Anyways, before we start, what should I call you?
    -> {$playerName}.
        <<positive_indicator Bird>>
        <<set $birdLikesYou += 1>>
        <<show_character Bird Happy2 right>>
        <<set $birdThinksPlayerNameIs = $playerName>>
        <<set $birdKnowsPlayerName = true>>
        {$birdName}: {$birdThinksPlayerNameIs}? Lovely name.
    -> I don't have a name.
        <<show_character Bird Thinking right>>
        {$birdName}: Oh, I see. That's... inconvenient. I'll call you human, then. That is what you are, no?
        -> Yes, human works.
            <<positive_indicator Bird>>
            <<set $birdLikesYou += 1>>
            {$birdName}: Excellent. Human it is.
        -> No. I'm not a human.
            <<set $birdDislikesYou += 1>>
            <<show_character Bird Confused right>>
            {$birdName}: Hm. That's... troubling. I'll just list down your galaxy and coordinates.
            <<show_character Bird Nervous right>>
            {$birdName}: It's... a pleasure to meet you, MW-012!
            <<set $birdThinksPlayerNameIs = "MW-012">>
    -> I don't want to take your quiz.
        <<set $birdDislikesYou += 1>>
        <<show_character Bird Nervous right>>
        {$birdName}: Aw, are you sure? It'd be quick.
            -> Yes, I'm sure.
                <<show_character Bird Sad right>>
                {$birdName}: Okay then. Thanks for your time anyways. Bye.
                <<stop_voice_blip Bird>>
                <<hide_all_characters>>
                <<play_music MainTheme>>
                <<deactivate_icon Bird>>
                The transmission cuts off abruptly. That was strange.
                <<if $talkedToDog == false || $talkedToOpossum == false>>
                    <<jump TalkToOthers>>
                <<else>>
                    <<jump EndDay1>>
                <<endif>>
            -> ...fine.
                <<positive_indicator Bird>>
                <<set $birdLikesYou += 1>>
                <<show_character Bird Happy right>>
                {$birdName}: Thank you! I appreciate it.
<<show_character Bird Happy right>>
{$birdName}: Okay, first question. What's your reason for being out here in space?
    -> Escaping humanity.
        <<show_character Bird Thinking right>>
        {$birdName}: Hm... Space can be a refuge for some.
        <<show_character Bird Nervous right>>
        {$birdName}: Tell me more about that. What do you not like about humanity?
            -> People are cruel.
                <<show_character Bird Sad right>>
                {$birdName}: Oh? I... I'm sorry to hear that.
                {$birdName}: I am curious, but perhaps that conversation is best for later.
            -> They don't understand me.
                <<show_character Bird Sad right>>
                {$birdName}: Ah... I'm sorry to hear that.
                {$birdName}: I'd like to know more about that, perhaps later.
            -> I just don't fit in.
                <<show_character Bird Sad right>>
                {$birdName}: I... can get that. Space can be a good place to clear one's mind.
            -> ...
                <<set $birdDislikesYou += 1>>
                <<show_character Bird Nervous right>>
                {$birdName}: Ah, perhaps I overstepped.
                <<show_character Bird Thinking right>>
                {$birdName}: Let's just... move onto the last question.
    -> (lie) Exploring the cosmos.
        <<show_character Bird Confused right>>
        {$birdName}: Oh, wow... that's... noble. You actually <i>enjoy</i> being in space?
        <<show_character Bird Neutral right>>
        {$birdName}: That's surprising. Can you tell me more?
            -> I like the quiet.
                <<positive_indicator Bird>>
                <<set $birdLikesYou += 1>>
                <<show_character Bird Happy right>>
                {$birdName}: Ah, I see. Space is indeed quite peaceful.
                {$birdName}: I'd be interested to learn more about that, perhaps later.
            -> I like the adventure.
                <<positive_indicator Bird>>
                <<set $birdLikesYou += 1>>
                <<show_character Bird Happy2 right>>
                {$birdName}: Ah, a thrill-seeker. I don't get out too much myself, but I admire it.
                {$birdName}: I'd like to know more about that, perhaps later.
            -> I'm here to report aliens to Earth.
                <<show_character Bird Disgusted right>>
                {$birdName}: Is that... a threat?
                <<set $birdDislikesYou += 1>>
                <<if $birdLikesYou < $birdDislikesYou>>
                    <<show_character Bird Sad right>>
                    {$birdName}: I... see. Well, um. I should take my leave, then. Farewell.
                    <<stop_voice_blip Bird>>
                    <<hide_all_characters>>
                    <<play_music MainTheme>>
                    <<deactivate_icon Bird>>
                    The transmission cuts off abruptly. That was strange.
                    <<if $talkedToDog == false || $talkedToOpossum == false>>
                        <<jump TalkToOthers>>
                    <<else>>
                        <<jump EndDay1>>
                    <<endif>>
                <<else>>
                    <<show_character Bird Nervous right>>
                    {$birdName}: I suppose... maybe I can understand that, from a scientific perspective.
                    {$birdName}: I'd like to know more about that, perhaps later.
                <<endif>>
    -> I'm not sure.
        <<show_character Bird Thinking right>>
        <<set $birdDislikesYou += 1>>
        {$birdName}: Hm, that's understandable. I'll put that down as...
        <<show_character Bird Sweat right>>
        {$birdName}: Um, I guess I'll leave that one as blank.
<<show_character Bird Happy right>>
{$birdName}: Next question! How would you describe the ambient temperature of your home planet?
    -> Around 15 degrees Celsius.
        <<positive_indicator Bird>>
        <<set $birdLikesYou += 1>>
        <<show_character Bird Happy right>>
        {$birdName}: Interesting! I appreciate the exact answer. You know your stuff.
    -> Very mild.
        <<show_character Bird Confused right>>
        {$birdName}: Can you... be more specific? Sorry, I just need a better idea of what it's like.
        -> It kind of changes? Like... it's hot sometimes, and cold other times.
            <<positive_indicator Bird>>
            <<set $birdLikesYou += 1>>
            <<show_character Bird Happy right>>
            {$birdName}: Ah, a planet with varying seasons. Noted!
        -> Like springtime.
            <<set $birdDislikesYou += 1>>
            <<show_character Bird Disgusted right>>
            {$birdName}: ...
            <<stop_voice_blip Bird>>
            {$birdName} seems unsatisfied by your vague answer, but seems not to ask further out of politeness.
            <<start_voice_blip Bird>>
    -> It varies... hot... and cold? Rainy? Windy?
        <<positive_indicator Bird>>
        <<set $birdLikesYou += 1>>
        <<show_character Bird Happy right>>
        {$birdName}: Ah, a planet with varying seasons. Noted!
<<show_character Bird Happy right>>
{$birdName}: And then, may I ask, how are relationships typically formed on your planet?
    -> Through, um, I guess, mutual interest?
        <<positive_indicator Bird>>
        <<set $birdLikesYou += 1>>
        <<show_character Bird Happy2 right>>
        {$birdName}: A very sensible approach. Very nice.
        <<show_character Bird Nervous right>>
        {$birdName}: Very... akin to how we do it back in Aviarian society.
    -> I... wouldn't really know.
        <<set $birdDislikesYou += 1>>
        <<set $negSelfPerception += 1>>
        <<show_character Bird Sad right>>
        {$birdName}: Aw, I'm sorry to hear that.
        <<show_character Bird Confused right>>
        {$birdName}: Surely love is still a construct that exists?
    -> Random chance?
        <<positive_indicator Bird>>
        <<set $birdLikesYou += 1>>
        <<show_character Bird Happy right>>
        {$birdName}: Ah, the thrill of serendipity! I like that.
        <<show_character Bird Nervous right>>
        {$birdName}: Very... akin to how we do it back in Aviarian society. You never really know when love will strike.
<<show_character Bird Happy right>>
{$birdName}: That concludes our little questionnaire. Thanks for your help!
    -> No problem.
        <<positive_indicator Bird>>
        <<set $birdLikesYou += 1>>
        <<show_character Bird Happy2 right>>
        {$birdName}: Wonderful! I appreciate your willingness to help further scientific knowledge.
    -> ...that's it?
        <<set $birdDislikesYou += 1>>
        <<show_character Bird Neutral right>>
        {$birdName}: Um, yes. Thank you for your time.
        <<show_character Bird Sweat right>>
        {$birdName}: I don't know what else to say here...
    -> Why are you so interested in this? <<if $birdExplainedIntention == false>>
        <<set $birdExplainedIntention = true>>
        <<show_character Bird Thinking right>>
        {$birdName}: Mmm, well, as a scientist, I'm always eager to learn more about different lifeforms.
        {$birdName}: Your species is particularly intriguing to me, given your unique characteristics.
        <<if $birdLikesYou >= $birdDislikesYou>>
            <<show_character Bird Happy right>>
            <<positive_indicator Bird>>
            <<set $birdLikesYou += 1>>
            {$birdName}: I hope to learn about you more in the future.
        <<else>>
            <<show_character Bird Neutral right>>
            <<set $birdDislikesYou += 1>>
            {$birdName}: But I understand if you're not interested in sharing more.
        <<endif>>
<<show_character Bird Nervous right>>
{$birdName}: I guess, um, I'll take my leave, then. Do you have any closing thoughts?
    -> Why talk to me? <<if $birdExplainedIntention == false>>
        <<set $birdExplainedIntention = true>>
        <<positive_indicator Bird>>
        <<set $birdLikesYou += 1>>
        <<show_character Bird Thinking right>>
        {$birdName}: Ah, that's a good question. One I haven't really thought much about myself yet.
        <<show_character Bird Nervous right>>
        {$birdName}: I suppose I'm just curious about other lifeforms. Understanding others helps me understand myself better, I think.
    -> It was nice talking to you.
        <<positive_indicator Bird>>
        <<set $birdLikesYou += 1>>
        <<if $birdLikesYou >= $birdDislikesYou>>
            <<show_character Bird Happy2 right>>
            {$birdName}: Likewise! I hope we can converse again in the future.
            <<set $birdSecured = true>>
        <<endif>>
    -> Bye...
        <<show_character Bird Neutral right>>
        {$birdName}: Farewell, then. Safe travels out there in the cosmos.
    -> ...
<<if $birdLikesYou >= $birdDislikesYou && $birdSecured == false>>
    <<show_character Bird Flirt right>>
    {$birdName}: If you ever wish to converse again, just ping me. I would be delighted to respond.
    <<set $birdSecured = true>>
<<endif>>
<<stop_voice_blip Bird>>
<<hide_all_characters>>
<<play_music MainTheme>>
<<if $birdSecured == false>>
    <<deactivate_icon Bird>>
<<endif>>
The transmission cuts off as {$birdName} takes their leave.
<<if $birdSecured == true>>
    You notice a transmission from {$birdName} saved onto your monitor. You can reach out at any time later.
<<endif>>
<<if $talkedToDog == false || $talkedToOpossum == false>>
    <<jump TalkToOthers>>
<<else>>
    <<jump EndDay1>>
<<endif>>
===

title: EndDay1
position: 925,83
---
<<hide_all_characters>>
<<hide_all_objects>>
<<show PCRoom>>
After talking to so many new figures, a sense of fatigue overcomes you.
You decide to call it a day. Perhaps after you wake up, you can decide what you want to do next.
<<fade_transition 3>>
<<wait 3>>
<<jump StartDay2>>
===