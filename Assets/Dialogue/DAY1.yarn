title: AfterName
position: -31,-584
---
<<once>>
<<declare $saidHello = false>>
<<hide_all_objects>>
<<show ScreenBlocker>>
Narrator: {$playerName}... Right, your name is {$playerName}.
<<hide ScreenBlocker>>
<<speed 1>>
Narrator: You open your eyes, and realize that you are no longer suspended in mid-air.
Narrator: You're lying down. On a hard surface. Your head feels heavy, and your body aches all over.
Narrator: You slowly sit up, and take in your surroundings.
<<endonce>>
Narrator: You're at home. In your metal spaceship.
    -> Hello? <<if $saidHello == false>>
        <<set $saidHello = true>>
        Narrator: A voice, soft and distant, responds to your call. You recognize it a few seconds later as your own. Every sound you make echoes off the walls.
        <<jump AfterName>>
    -> (take a look around)
        <<jump LookAround>>
===

title: LookAround
position: -30,-398
---
<<once>>
<<declare $bedrollExplored = false>>
<<declare $cannedFoodExplored = false>>
<<declare $monitorExplored = false>>
<<show MonitorOffDay1>>
<<show Bedroll>>
<<show Soup>>
Narrator: Might as well re-familiarize yourself with your surroundings.
<<show_ui ClickToInteract>>
<<endonce>>
===

title: AfterInteractedWithItems
position: 315,-648
---
<<hide_all_objects>>
<<hide_ui ClickToInteract>>
<<show Sitting>>
Narrator: This is the life that you've committed yourself to, ever since taking the job to pilot this small exploration vessel.
Narrator: You are tasked with reporting astronomical oddities back to Earth, as the first line of defense against the unknown.
Narrator: But deep inside, you knew this was just to get a chance away from Earth.
Narrator: Nothing of interest seems to be happening. You take a seat and ponder the cold, empty room around you.
    -> (do some push-ups)
        Narrator: You were never huge on fitness, but you were required to keep in shape for space travel.
        Narrator: You drop down and do a few push-ups to keep your strength up.
    -> (contemplate the meaning of life)
        Narrator: You ponder the vastness of existence and your place within it.
        Narrator: The silence of space seems to amplify your thoughts, making you feel both insignificant and connected to the universe.
        -> (hone in on the positive)
            Narrator: You focus on the small joys in your life, finding solace in the little things that bring you happiness.
            <<set $posSelfPerception += 1>>
        -> (dwell on the negative)
            Narrator: You find yourself sinking deeper into feelings of loneliness and despair, overwhelmed by the vast emptiness around you.
            <<set $negSelfPerception += 1>>
    -> (think of something happy)
        Narrator: You recall a fond memory that brings a smile to your face. It's a small comfort in the loneliness of space.
        Narrator: The memory gives you a brief respite from your current situation.
        <<set $posSelfPerception += 1>>
Narrator: You decide to get back to doing something. Anything is better than sitting around.
<<hide Sitting>>
<<jump DoJobFirstTime>>
===

title: DoJobFirstTime
position: 308,-448
---
Narrator: You stand up and turn on the computer. 
Narrator: As part of your job, you need to complete a daily report on your findings.
Narrator: You're not sure anyone actually reads these, but it gives you a sense of routine.
<<show MonitorReportDay1>>
<<show_ui TypeToInteract>>
===

title: AfterTypedFirstTime
position: 15,-241
---
<<hide_ui TypeToInteract>>
Narrator: You sigh, after having written the same report for the 620th time. 
Narrator: The report is done for the day.
    -> (turn off the monitor)
<<hide MonitorReportDay1>>
<<jump MonitorOn>>
===

title: MonitorOn
position: 15,-63
---
<<hide_all_objects>>
<<show MonitorOn>>
Narrator: Just as you turn the monitor off, it flickers and springs back to life.
<<show_character Cat Obscure right>>
Narrator: A dark figure appears on the screen, accompanied with a static-filled voice.
<<start_voice_blip Cat>>
???: Hello? <i>*bzzt*</i> Hello? Can... hear...?
    -> Who are you?
    -> (stay silent)
    Narrator: The voice from the monitor continues to speak.
    ???: Please... <i>*kshh*</i> if... there... yourself... known...
    <<stop_voice_blip Cat>>
    <<jump EnterMessage>>
===

title: EnterMessage
position: 312,-227
---
<<start_voice_blip Cat>>
???: Click... <i>*bzzt*</i> comm... cation... panel...
<<stop_voice_blip Cat>>
Narrator: With your expertise, you think you can adjust the frequency and let the signal through.
    -> (let the signal through)
        <<jump CatIntro>>
    -> (ignore the signal. it's probably nothing.)
        Narrator: You decide to ignore the signal. It's probably just static interference.
        <<hide_all_characters>>
        <<hide MonitorOn>>
        <<show MonitorOnNoSilhouette>>
        <<set $negSelfPerception += 1>>
        Narrator: After a few moments, the screen flickers back to static. You are alone, again.
        Narrator: How should you pass the time?
            -> (play a game on the monitor)
                Narrator: You decide to play a game on the monitor to pass the time.
                Narrator: The game is simple, but it helps distract you from the loneliness.
                <<jump Reinterrupted>>
            -> (try to find the signal again)
                Narrator: You fiddle with the dials and knobs, trying to find the signal again.
                <<jump Reinterrupted>>
===

title: Reinterrupted
position: 590,-315
---
<<hide_all_objects>>
<<show MonitorOn>>
<<show_character Cat Obscure right>>
Narrator: Suddenly, the same character from before reappears on the screen.
    -> (get it over with. let the signal through)
<<jump CatIntro>>
===

title: CatIntro
position: 305,91
---
Narrator: Suddenly, the figure comes into view.
<<show_character Cat Neutral right>>
<<start_voice_blip Cat>>
<<play_music CatTheme 0.5>>
???: Hey, I caught your signal from our ship! Are you alright?
    -> ...What?
    -> Aww, you're so cute!
        <<positive_indicator Cat>>
        <<set $catLikesYou += 1>>
        <<show_character Cat Happy2 right>>
        ???: Aw, you're too kind. 
        <<show_character Cat Neutral right>>
        ???: But seriously, are you okay out there? You look like you're all alone.
            -> Yeah, I'm fine.
                <<show_character Cat Thinking right>>
                ???: Hm, alright. That's at least somewhat reassuring.
            -> Not really.
                <<show_character Cat Neutral right>>
                ???: Oh... I'm sorry to hear that. Space can be a lonely place.
    -> (scream. what is happening.)
        <<stop_voice_blip Cat>>
        Narrator: You scream deafeningly loudly. The creature on the monitor jumps back in surprise.
        <<set $catDislikesYou += 1>>
        <<show_character Cat Surprised right>>
        <<start_voice_blip Cat>>
        ???: AH! That... surely is a strange introduction.
        <<show_character Cat Sad right>>
        ???: Please try to spare my ears next time... these are quite sensitive.
<<start_voice_blip Cat>>
<<show_character Cat Thinking right>>
???: Well, you don't seem to act like you're in danger. What're you doing in space?
    -> What the fuck? What <i>are</i> you?
        <<show_character Cat Neutral right>>
        <<set $catDislikesYou += 1>>
        ???: ...don't be rude.
        ???: It's not everyday that you meet another lifeform just floating around in space.
    -> Who... are you?
    -> It's none of your business.
        <<set $catDislikesYou += 1>>
        ???: I suppose it isn't, but can you blame someone for having a little bit of empathy?
<<if $catLikesYou >= $catDislikesYou>>
<<show_character Cat Happy right>>
{$catName}: Oh! I'm so sorry, I should introduce myself. My name is 猫喵猫猫喵, but you can just call me {$catName}.
<<show_character Cat Happy2 right>>
<<else>>
{$catName}: Anyways, my name is 猫喵猫猫喵. But to keep things simple, you can just call me {$catName}.
<<show_character Cat Neutral right>>
<<endif>>
{$catName}: To you, I'm an alien. To me, you're an alien. We're all aliens here.
    -> An alien? Those don't exist.
        <<show_character Cat Thinking right>>
        <<set $catDislikesYou += 1>>
        {$catName}: Hm? How do I explain this...
        <<show_character Cat Neutral right>>
        {$catName}: I'm not from your planet of origin. I come from a place far away.
        <<show_character Cat Angry right>>
        {$catName}: And, for the record, aliens do exist! Have some manners.
    -> 猫喵猫猫喵? What does that mean?
        <<show_character Cat Thinking right>>
        {$catName}: It's... hard to explain. It's probably not something I can explain to you.
        <<show_character Cat Happy2 right>>
        {$catName}: Just call me {$catName}, it's easier that way.
    -> Meowdy.
        <<show_character Cat Thinking right>>
        {$catName}: ...Meowdy? That's an interesting way to greet someone.
        {$catName}: I guess, meowdy to you too.
<<show_character Cat Happy right>>
{$catName}: But anyways, who are you? What are you?
    -> My name's {$playerName}.
        <<if $playerName == "Dipper">>
            {$catName}: {$playerName}? That sounds like it'd be a cool name for a group of stars. It's nice to meet you.
        <<else>>
            {$catName}: {$playerName}? That's a cool name! It's nice to meet you.
        <<endif>>
        <<set $catLikesYou += 1>>
        <<positive_indicator Cat>>
        <<set $catThinksPlayerNameIs = $playerName>>
    -> I'm a human.
        <<show_character Cat Thinking right>>
        {$catName}: A human? I've only heard stories about your kind.
        {$catName}: Is it true that you walk on two legs and have opposable thumbs?
        -> Yeah, it's pretty cool.
            <<show_character Cat Happy2 right>>
            <<set $catLikesYou += 1>>
            <<positive_indicator Cat>>
            {$catName}: That's incredible! You must be very dexterous.
        -> No. What?
            <<show_character Cat Sad right>>
            {$catName}: Aw, that's disappointing. I was hoping to see some cool tricks.
        <<show_character Cat Happy right>>
        {$catName}: And what should I call you?
        -> It's {$playerName}.
            {$catName}: {$playerName}? That's a nice name.
            <<set $catThinksPlayerNameIs = $playerName>>
        -> I don't have a name.
            <<show_character Cat Thinking right>>
            {$catName}: Oh, huh. That's okay. How would people refer to you then?
            <<show_character Cat Happy right>>
            {$catName}: I guess I'll just call you "Human". I'm a master at nicknames.
    -> (stay silent)
        {$catName}: Oh, you don't have a name? That's okay, I guess.
<<show_character Cat Neutral right>>
{$catName}: You should be careful here, though. Space is dangerous!
{$catName}: What are you doing out here? Wouldn't you rather be on your home planet?
    -> Exploring the cosmos.
        <<positive_indicator Cat>>
        <<set $catLikesYou += 1>>
        <<show_character Cat Happy right>>
        {$catName}: That's commendable! Not many have the courage to venture into the unknown.
    -> Escaping it all, I guess.
        <<show_character Cat Neutral right>>
        {$catName}: I understand that feeling. Sometimes, it's nice to get away from everything.
    -> I'm... not sure.
        <<show_character Cat Thinking right>>
        {$catName}: Hm, that's okay. Sometimes, we don't have all the answers right away.
{$catName}: Well, as long as you're okay. Have you met other lifeforms around here much?
    -> No. What... are you?
        <<set $catLikesYou += 1>>
        <<show_character Cat Happy right>>
        {$catName}: Aww, that's too bad. I'm glad I could be your first contact then!
        {$catName}: I'm from the planet Linnaeus, though I don't expect you've heard of it.
        <<show_character Cat Happy2 right>>
        {$catName}: We're a peaceful species, mostly just trying to explore the galaxy and make friends.
        -> I find that hard to believe.
            <<show_character Cat Sad right>>
            <<set $catDislikesYou += 1>>
            {$catName}: Why would you say that? I thought you seemed nice.
        -> It's.. nice to meet you.
            <<show_character Cat Happy right>>
            <<set $catLikesYou += 1>>
            {$catName}: Heh, likewise. It's been nice to meet you too, {$catThinksPlayerNameIs}!
    -> Yes, I've met a few.
        <<show_character Cat Neutral right>>
        {$catName}: Really? I'm calling your bluff. Your kind hasn't even discovered intergalactic travel yet!
    -> Why are you talking to me?
        <<show_character Cat Flirt right>>
        {$catName}: Well, you know what they say. Curiosity killed the felis catus linnaeus!
        <<show_character Cat Happy right>>
        {$catName}: I was on patrol for my planet, and I picked up your ship's signal. I thought I'd check in and see if you were alright.
        {$catName}: I'm from the planet Linnaeus, though I don't expect you've heard of it.
        {$catName}: We're a peaceful species, mostly just trying to explore the galaxy and make friends.
        -> I find that hard to believe.
            <<show_character Cat Sad right>>
            <<set $catDislikesYou += 1>>
            {$catName}: Why would you say that? I thought you seemed nice.
        -> It's.. nice to meet you.
            <<show_character Cat Happy right>>
            <<set $catLikesYou += 1>>
            {$catName}: Heh, likewise. It's been nice to meet you too, {$catThinksPlayerNameIs}!
<<show_character Cat Surprised right>>
{$catName}: Ah! I just realized I had to do something. It's not my plan to keep you here for over an orbit.
<<if $catLikesYou >= $catDislikesYou>>
    <<show_character Cat Flirt right>>
    {$catName}: I'll talk to you again soon, {$catThinksPlayerNameIs}. This has been really nice.
    {$catName}: If you want to chat again, just give me a ping. See ya!~
    <<set $catSecured = true>>
<<else>>
    <<show_character Cat Neutral right>>
    {$catName}: Well, ${$catThinksPlayerNameIs}. I'll have to get back to my patrol. Bye!
<<endif>>
<<stop_voice_blip Cat>>
<<hide_all_characters>>
<<hide MonitorOn>>
<<show MonitorOnNoSilhouette>>
<<play_music MainTheme>>
<<if $catSecured == true>>
    Narrator: {$catName} uploads a sequence of radio waves in file format onto your device, letting you ping them again in the future.
    Narrator: Suddenly, the screen flickers back to static. {$catName} is gone, for now.
        -> {$catName}? Can you hear me?
            Narrator: You tap the monitor and move around the dials, but nobody responds. You are alone, again.
        -> (sit down)
<<endif>>
<<jump ThinkingAfterCatLeaves>>
===

title: ThinkingAfterCatLeaves
---
<<hide_all_objects>>
<<show Sitting>>
Narrator: You sit back down, trying to process the encounter you just had.
Narrator: Who was that creature? An alien? How is that even possible?
<<if $catLikesYou > $catDislikesYou>>
    Narrator: Strangely enough, you found the encounter with them rather pleasant. They seemed friendly, in their own way.
    Narrator: You think about what it would be like to have a friend like that. Someone who understands you, even if they are from another world.
    Narrator: You wonder if you'll ever see them again.
    <<set $posSelfPerception += 1>>
<<else>>
    Narrator: To you, the whole encounter felt off. Aliens? Of all times, why now?
    Narrator: You think about what others on Earth would say if they found out about this encounter. Although it is your job to report such things, would they really believe you met friendly animal aliens?
    Narrator: But you shake this idea off, for now. You'll think about it after a night's rest.
<<endif>>
<<hide_all_objects>>
<<show MonitorOn>>
<<show_character Dog Obscure left>>
<<show_character Lizard Obscure center>>
<<show_character Bird Obscure right>>
Narrator: As you sit there, lost in thought, the monitor flickers back to life once more.
Narrator: You see a total of three figures, all different from the last one.
Narrator: They all seem like they'll stick around for a bit. Who do you talk to first?
    -> (respond to the one with the floppy ears)
        <<set $talkedToDog = true>>
        <<jump DogIntro>>
    -> (respond to the one with the frazzled hair)
        <<set $talkedToLizard = true>>
        <<jump LizardIntro>>
    -> (respond to the one with the pointy muzzle)
        <<set $talkedToBird = true>>
        <<jump BirdIntro>>
===

title: TalkToOthers
---
<<hide_all_characters>>
<<show MonitorOn>>
<<if $talkedToDog == false>>
    <<show_character Dog Obscure left>>
<<endif>>
<<if $talkedToLizard == false>>
    <<show_character Lizard Obscure center>>
<<endif>>
<<if $talkedToBird == false>>
    <<show_character Bird Obscure right>>
<<endif>>
Narrator: You notice that others are still live on the feed. You can choose someone to talk to next.
    -> (respond to the one with the floppy ears) <<if $talkedToDog == false>>
        <<set $talkedToDog = true>>
        <<jump DogIntro>>
    -> (respond to the one with the frazzled hair) <<if $talkedToLizard == false>>
        <<set $talkedToLizard = true>>
        <<jump LizardIntro>>
    -> (respond to the one with the pointy muzzle) <<if $talkedToBird == false>>
        <<set $talkedToBird = true>>
        <<jump BirdIntro>>
===

title: DogIntro
position: 635,249
---
<<hide_all_characters>>
<<hide_all_objects>>
<<show MonitorOn>>
Narrator: You pull up the communication panel and respond to the figure with the floppy ears.
<<show_character Dog Obscure right>>
Narrator: You turn the dials to let the signal through.
<<show_character Dog Neutral right>>
<<start_voice_blip Dog>>
<<play_music DogTheme 0.5>>
???: Woofdy.
???: What're you doing out here all by yourself? Woof.
    -> Who are you?
    -> What in the world? A purple dog?
        <<set $dogDislikesYou += 1>>
        <<stop_voice_blip Dog>>
        <<show_character Dog Thinking right>>
        Narrator: The figure on the monitor tilts their head in confusion. They seem a little offended.
    -> (stay silent)
<<start_voice_blip Dog>>
<<show_character Dog Happy right>>
{$dogName}: I'm {$dogName}. Woof. I come in peace. Woof.
{$dogName}: I saw your signal, and thought I'd come check it out. Woof.
    -> What... are you?
        <<positive_indicator Dog>>
        <<set $dogLikesYou += 1>>
        {$dogName}: I don't know what you'd call it. I'm just a space creature. Woof.
        {$dogName}: Just like you, I'm exploring space. Woof.
        -> But... where do you come from?
            {$dogName}: Hm? Oh, I come from a place far away. Woof. Haven't been there in a while. Not many of us stay.
            {$dogName}: It's a planet called Barklon. Woof.
            {$dogName}: It's a nice place. Lots of open fields to run around in. Woof.
        -> And you're a dog?
            <<show_character Dog Surprised right>>
            {$dogName}: A dog? What is that? Woof.
            <<show_character Dog Neutral right>>
            {$dogName}: You're not the first one to ask this, though. Woof. I should look into this "dog" thing. Woof.
            <<show_character Dog Happy right>>
            {$dogName}: But no, I'm from a planet called Barklon. Woof.
            {$dogName}: It's a nice place. Lots of open fields to run around in. Woof.
        -> (stay silent)
    -> Why do you keep saying woof?
        {$dogName}: I don't know what you mean. Woof.
        -> Like that! It's weird.
            <<set $dogDislikesYou += 1>>
            <<show_character Dog Sad right>>
            {$dogName}: Hm. Woof. I'm sorry if it bothers you. Woof.
            {$dogName}: It's just how we talk back home. Woof.
        -> I think it's cute.
            <<positive_indicator Dog>>
            <<set $dogLikesYou += 1>>
            <<show_character Dog Happy right>>
            {$dogName}: Woof! Even though I don't know what I'm doing, I'm glad you like it. Woof.
            {$dogName}: It's just how we talk back home. Woof.
        -> (stay silent)
    -> Hi {$dogName}. I'm {$playerName}.
        <<positive_indicator Dog>>
        <<set $dogLikesYou += 1>>
        <<set $dogThinksPlayerNameIs = $playerName>>
        <<set $dogKnowsPlayerName = true>>
        {$dogName}: Woof! It's nice to meet you, {$dogThinksPlayerNameIs}!
<<show_character Dog Neutral right>>
{$dogName}: I hope you don't mind me dropping by unannounced. Woof.
    -> I do mind. What are you trying to do here?
        <<set $dogDislikesYou += 1>>
        <<show_character Dog Sad right>>
        {$dogName}: Oh... Woof. I'm sorry if I bothered you. Woof.
        {$dogName}: I just saw your signal and thought I'd come check it out. Woof.
    -> What are you doing here?
        {$dogName}: Oh, I was just passing by. Woof.
    -> Do you know {$catName}?
        {$dogName}: {$catName}? Can't say I'm familiar. We're not that interconnected here in space.
        {$dogName}: Besides, I meet tons of people. It's hard to keep track. Woof.
        <<if $dogLikesYou >= $dogDislikesYou>>
            <<show_character Dog Happy right>>
            {$dogName}: You're pretty friendly, though. Woof. Don't see many of those around here.
        <<else>>
            <<show_character Dog Neutral right>>
            {$dogName}: You seem okay, though. Woof. Not many friendly folks out here.
        <<endif>>
{$dogName}: It's rare that I even see another living being out here. Woof. 
<<show_character Dog Neutral right>>
<<if $dogKnowsPlayerName == false>>
    <<set $dogKnowsPlayerName = true>>
    {$dogName}: But. Woof. What should I call you?
        -> {$playerName}.
            {$dogName}: Woof! {$playerName}, that's a nice name.
            <<set $dogThinksPlayerNameIs = $playerName>>
        -> (stay silent)
            {$dogName}: Hm, that's okay. Woof. I guess I'll just call you "Human". You're a human, right? Woof.
<<endif>>
{$dogName}: Anyways, I mostly just wander around on my own. Woof. Picked up on your signal. Woof.
    -> Signal?
        {$dogName}: Yeah, I saw it while I was out exploring. Woof.
        <<show_character Dog Happy right>>
        {$dogName}: I like to wander around and see new places. Woof.
        {$dogName}: What about you? What are you doing out here? Woof.
        -> My job is to find alien life.
            <<show_character Dog Thinking right>>
            {$dogName}: Alien life? What for? Woof.
            {$dogName}: I don't see the point in all that. Woof. Why?
                -> I'm here to report them to Earth.
                    <<show_character Dog Sad right>>
                    {$dogName}: Report... aliens? Woof. That seems a bit harsh. Most of the aliens I've met aren't the nicest, but they're not all bad. Woof.
                    <<set $dogDislikesYou += 1>>
                -> I just want to make contact.
                    {$dogName}: Make contact? Woof. You should join me sometime. I see a lot of interesting beings everyday.
                    {$dogName}: The most interesting ones are the ones that ask a lot of questions. Woof.

        -> I'm exploring, just like you.
            <<positive_indicator Dog>>
            {$dogName}: Exploring? Woof. We should go together sometime!
{$dogName}: Anyways, I should get going. Woof.
{$dogName}: Space is a big place, and I have a lot of wandering to do. Woof.
<<if $dogLikesYou >= $dogDislikesYou>>
    <<show_character Dog Flirt right>>
    {$dogName}: If you ever need anything, just send out a signal. Woof. I'll come find you.
    <<set $dogSecured = true>>
<<else>>
    <<show_character Dog Neutral right>>
    {$dogName}: Well, it was cool meeting you. Bye! Woof.
<<endif>>
<<stop_voice_blip Dog>>
<<hide_all_characters>>
<<play_music MainTheme>>
Narrator: The transmission cuts off as {$dogName} leaves to continue their wandering.
<<if $dogSecured == true>>
    Narrator: You notice a transmission from {$dogName} saved onto your monitor. You can likely use this to ping them again in the future.
<<endif>>
<<if $talkedToLizard == false || $talkedToBird == false>>
    <<jump TalkToOthers>>
<<else>>
    <<jump EndDay1>>
<<endif>>
===

title: LizardIntro
position: 638,-61
---
<<hide_all_characters>>
<<hide_all_objects>>
Narrator: You pull up the communication panel and respond to the figure with the frazzled hair.
<<show_character Lizard Obscure right>>
Narrator: You turn the dials to let the signal through.
<<show_character Lizard Neutral right>>
<<start_voice_blip Lizard>>
<<play_music_start_loop LizardThemeStart LizardThemeLoop 0.4 0.5>>
???: Hi I'm a Lizard. This section is unimplemented.
???: As such, we will skip to the next section.
<<stop_voice_blip Lizard>>
<<play_music MainTheme>>
<<hide_all_characters>>
<<if $talkedToDog == false || $talkedToBird == false>>
    <<jump TalkToOthers>>
<<else>>
    <<jump EndDay1>>
<<endif>>
===

title: BirdIntro
position: 633,86
---
<<hide_all_characters>>
<<hide_all_objects>>
Narrator: You pull up the communication panel and respond to the figure with the pointy muzzle.
<<show_character Bird Obscure right>>
Narrator: You turn the dials to let the signal through.
<<show_character Bird Neutral right>>
<<start_voice_blip Bird>>
<<play_music BirdTheme 0.4>>
???: Hi I'm a Bird. This section is unimplemented.
???: As such, we will skip to the next section.
<<stop_voice_blip Bird>>
<<play_music MainTheme>>
<<hide_all_characters>>
<<if $talkedToDog == false || $talkedToLizard == false>>
    <<jump TalkToOthers>>
<<else>>
    <<jump EndDay1>>
<<endif>>
===

title: EndDay1
position: 925,83
---
<<hide_all_characters>>
<<hide_all_objects>>
Narrator: After talking to so many new figures, a sense of fatigue overcomes you.
Narrator: You decide to call it a day, and take a short nap. Perhaps afterwards, you can decide what you want to do next.
<<show ScreenBlocker>>
<<wait 2>>
<<hide ScreenBlocker>>
<<jump StartDay2>>
===