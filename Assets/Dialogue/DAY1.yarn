title: AfterName
position: -31,-584
---
<<once>>
<<hide_all_objects>>
<<show ScreenBlocker>>
Narrator: {$playerName}... Right, your name is {$playerName}.
<<hide ScreenBlocker>>
<<speed 1>>
Narrator: You open your eyes, and realize that you are no longer suspended in mid-air.
Narrator: You're lying down. On a hard surface. Your head feels heavy, and your body aches all over.
Narrator: You slowly sit up, and take in your surroundings.
<<endonce>>
Narrator: You're at home. In your metal spaceship.
    -> Hello?
        Narrator: A voice responds to your call. You recognize it a few seconds later as your own. Every sound you make echoes off the walls.
        -> (take a look around)
            <<jump LookAround>>
    -> (take a look around)
        <<jump LookAround>>
===

title: LookAround
position: -30,-398
---
<<once>>
<<declare $bedrollExplored = false>>
<<declare $cannedFoodExplored = false>>
<<declare $monitorExplored = false>>
<<show MonitorOffDay1>>
<<show Bedroll>>
<<show Soup>>
Narrator: Might as well re-familiarize yourself with your surroundings.
<<show_ui ClickToInteract>>
<<endonce>>
===

title: AfterInteractedWithItems
position: 315,-648
---
<<hide_all_objects>>
<<hide_ui ClickToInteract>>
<<show Sitting>>
Narrator: This is the life that you've committed yourself to, ever since taking the job to pilot this small exploration vessel.
Narrator: You are tasked with reporting astronomical oddities back to Earth, as the first line of defense against the unknown.
Narrator: But deep inside, you knew this was just to get a chance away from Earth.
Narrator: Nothing of interest seems to be happening. You take a seat and ponder the cold, empty room around you.
    -> (do some push-ups)
        Narrator: You were never huge on fitness, but you were required to keep in shape for space travel.
        Narrator: You drop down and do a few push-ups to keep your strength up.
    -> (contemplate the meaning of life)
        Narrator: You ponder the vastness of existence and your place within it.
        Narrator: The silence of space seems to amplify your thoughts, making you feel both insignificant and connected to the universe.
        -> (hone in on the positive)
            <<set $posSelfPerception += 1>>
            Narrator: You focus on the small joys in your life, finding solace in the little things that bring you happiness.
        -> (dwell on the negative)
            <<set $negSelfPerception += 1>>
            Narrator: You find yourself sinking deeper into feelings of loneliness and despair, overwhelmed by the vast emptiness around you.
    -> (think of something happy)
        <<set $posSelfPerception += 1>>
        Narrator: You recall a fond memory that brings a smile to your face. It's a small comfort in the loneliness of space.
        Narrator: The memory gives you a brief respite from your current situation.
Narrator: You decide to get back to doing something. Anything is better than sitting around.
<<hide Sitting>>
<<jump DoJobFirstTime>>
===

title: DoJobFirstTime
position: 308,-448
---
Narrator: You stand up and turn on the computer. 
Narrator: As part of your job, you need to complete a daily report on your findings.
Narrator: You're not sure anyone actually reads these, but it gives you a sense of routine.
<<show MonitorReportDay1>>
<<show_ui TypeToInteract>>
===

title: AfterTypedFirstTime
position: 15,-241
---
<<hide_ui TypeToInteract>>
Narrator: You sigh, after having written the same report for the 620th time. 
Narrator: The report is done for the day.
    -> (turn off the monitor)
<<hide MonitorReportDay1>>
<<jump MonitorOn>>
===

title: MonitorOn
position: 15,-63
---
<<hide_all_objects>>
<<show MonitorOn>>
Narrator: Just as you turn the monitor off, it flickers and springs back to life.
<<show_character Cat Obscure right>>
Narrator: A dark figure appears on the screen, accompanied with a static-filled voice.
<<start_voice_blip Cat>>
???: Hello? <i>*bzzt*</i> Hello? Can... hear...?
    -> Who are you?
    -> ...
    Narrator: The voice from the monitor continues to speak.
    ???: Please... <i>*kshh*</i> if... there... yourself... known...
    <<stop_voice_blip Cat>>
    <<jump EnterMessage>>
===

title: EnterMessage
position: 312,-227
---
<<start_voice_blip Cat>>
???: Click... <i>*bzzt*</i> comm... cation... panel...
<<stop_voice_blip Cat>>
Narrator: With your expertise, you think you can adjust the frequency and let the signal through.
    -> (let the signal through)
        <<jump CatIntro>>
    -> (ignore the signal. it's probably nothing.)
        Narrator: You decide to ignore the signal. It's probably just static interference.
        <<hide_all_characters>>
        <<hide MonitorOn>>
        <<show MonitorOnNoSilhouette>>
        <<set $negSelfPerception += 1>>
        Narrator: After a few moments, the screen flickers back to static. You are alone, again.
        Narrator: How should you pass the time?
            -> (block them out of your head)
                <<set $negSelfPerception += 1>>
                Narrator: You decide to play a game on the monitor to pass the time.
                Narrator: The game is simple, but it helps distract you from the loneliness.
            -> (try to find the signal again)
                Narrator: You fiddle with the dials and knobs, trying to find the signal again.
<<hide_all_objects>>
<<show MonitorOn>>
<<show_character Cat Obscure right>>
Narrator: Suddenly, the same character from before reappears on the screen.
    -> (get it over with. let the signal through.)
        <<jump CatIntro>>
    -> (unplug the monitor)
        <<hide_all_characters>>
        <<hide MonitorOn>>
        <<show MonitorOff>>
        Narrator: You decide to unplug the monitor, cutting off the signal entirely.
        Narrator: After a few moments, the screen flickers back to static. You are alone, again.
        <<jump ForeverAloneNoContact>>
===

title: CatIntro
position: 305,91
---
Narrator: Suddenly, the figure comes into view.
<<show_character Cat Neutral right>>
<<start_voice_blip Cat>>
<<play_music CatTheme 0.5>>
???: Hey, I caught your signal from our ship! Are you alright?
    -> ...What?
    -> Aww, you're so cute!
        <<positive_indicator Cat>>
        <<set $catLikesYou += 1>>
        <<show_character Cat Happy2 right>>
        ???: Aw, you're too kind. 
        <<show_character Cat Neutral right>>
        ???: But seriously, are you okay out there? You look like you're all alone.
            -> Yeah, I'm fine.
                <<show_character Cat Thinking right>>
                ???: Hm, alright. That's at least somewhat reassuring.
            -> Not really.
                <<show_character Cat Neutral right>>
                ???: Oh... I'm sorry to hear that. Space can be a lonely place.
    -> (scream. what is happening.)
        <<stop_voice_blip Cat>>
        Narrator: You scream deafeningly loudly. The creature on the monitor jumps back in surprise.
        <<set $catDislikesYou += 1>>
        <<show_character Cat Surprised right>>
        <<start_voice_blip Cat>>
        ???: AH! That... surely is a strange introduction.
        <<show_character Cat Sad right>>
        ???: Please try to spare my ears next time... these are quite sensitive.
<<start_voice_blip Cat>>
<<show_character Cat Thinking right>>
???: Well, you don't seem to act like you're in danger. What're you doing in space?
    -> What the fuck? What <i>are</i> you?
        <<show_character Cat Neutral right>>
        <<set $catDislikesYou += 1>>
        ???: ...don't be rude.
        ???: It's not everyday that you meet another lifeform just floating around in space.
    -> Who... are you?
    -> It's none of your business.
        <<set $catDislikesYou += 1>>
        ???: I suppose it isn't, but can you blame someone for having a little bit of empathy?
<<if $catLikesYou >= $catDislikesYou>>
<<show_character Cat Happy right>>
{$catName}: Oh! I'm so sorry, I should introduce myself. My name is 猫喵猫猫喵, but you can just call me {$catName}.
<<show_character Cat Happy2 right>>
<<else>>
{$catName}: Anyways, my name is 猫喵猫猫喵. But to keep things simple, you can just call me {$catName}.
<<show_character Cat Neutral right>>
<<endif>>
{$catName}: I'm a lifeform from the planet Linnaeus. It's nice to meet you.
    -> So, an alien?
        <<show_character Cat Thinking right>>
        <<set $catDislikesYou += 1>>
        {$catName}: Hm? I guess so...
        <<show_character Cat Neutral right>>
        {$catName}: I'm not from your planet of origin. I come from a place far away.
        <<show_character Cat Angry right>>
        {$catName}: But please, don't go labeling me as an "alien". It's a bit demeaning, don't you think?
    -> 猫喵猫猫喵? What does that mean?
        <<show_character Cat Thinking right>>
        {$catName}: It's... hard to explain. It's probably not something I can explain to you.
        <<show_character Cat Happy2 right>>
        {$catName}: Just call me {$catName}, it's easier that way.
    -> Meowdy.
        <<show_character Cat Thinking right>>
        {$catName}: ...Meowdy? That's an interesting way to greet someone.
        <<show_character Cat Flirt right>>
        {$catName}: I guess, meowdy to you too.
<<show_character Cat Happy right>>
{$catName}: But anyways, I'm out here on a mission to learn more about other lifeforms. What should I call you?
    -> My name's {$playerName}.
        <<if $playerName == "Dipper">>
            {$catName}: {$playerName}? That sounds like it'd be a cool name for a group of stars. It's nice to meet you.
        <<else>>
            {$catName}: {$playerName}? That's a cool name! It's nice to meet you.
        <<endif>>
        <<set $catLikesYou += 1>>
        <<positive_indicator Cat>>
        <<set $catThinksPlayerNameIs = $playerName>>
    -> I'm a human.
        <<show_character Cat Thinking right>>
        {$catName}: A human? I've only heard stories about your kind.
        {$catName}: Is it true that you walk on two legs and have opposable thumbs?
        -> Yeah, it's pretty cool.
            <<show_character Cat Happy2 right>>
            <<set $catLikesYou += 1>>
            <<positive_indicator Cat>>
            {$catName}: That's incredible! You must be very dexterous.
        -> No. What?
            <<show_character Cat Sad right>>
            {$catName}: Aw, that's disappointing. I was hoping to see some cool tricks.
        <<show_character Cat Happy right>>
        {$catName}: And what should I call you?
        -> It's {$playerName}.
            {$catName}: {$playerName}? That's a nice name.
            <<set $catThinksPlayerNameIs = $playerName>>
        -> I don't have a name.
            <<show_character Cat Thinking right>>
            {$catName}: Oh, huh. That's okay. How would people refer to you then, though?
            <<show_character Cat Happy right>>
            {$catName}: I guess I'll just call you "Human". I'm a master at nicknames.
    -> ...
        {$catName}: Oh, you don't have a name? That's okay, I guess.
<<show_character Cat Neutral right>>
{$catName}: You should be careful here, though. Space is dangerous!
<<show_character Cat Thinking right>>
{$catName}: What brings you out here? Wouldn't you rather be on your home planet?
    -> Exploring the cosmos.
        <<positive_indicator Cat>>
        <<set $catLikesYou += 1>>
        <<show_character Cat Happy right>>
        {$catName}: That's commendable! Not many have the courage to venture into the unknown.
    -> Escaping it all, I guess.
        <<show_character Cat Neutral right>>
        {$catName}: I understand that feeling. Sometimes, it's nice to get away from everything.
    -> I'm... not sure.
        <<show_character Cat Thinking right>>
        {$catName}: Hm, that's okay. Sometimes, we don't have all the answers right away.
{$catName}: Well, I'm glad to hear you're doing fine. Have you met others around here?
    -> No. What... are you?
        <<set $catLikesYou += 1>>
        <<show_character Cat Happy right>>
        {$catName}: Aww, that's too bad. I'm glad I could be your first contact then!
        {$catName}: As I mentioned, I'm from the planet Linnaeus, though I don't expect you've heard of it.
        <<show_character Cat Happy2 right>>
        {$catName}: We're a peaceful species, mostly just trying to explore the galaxy and make friends.
        -> I find that hard to believe.
            <<show_character Cat Sad right>>
            <<set $catDislikesYou += 1>>
            {$catName}: Why would you say that? I thought you seemed nice.
        -> It's.. nice to meet you.
            <<show_character Cat Happy right>>
            <<set $catLikesYou += 1>>
            {$catName}: Heh, likewise. It's been nice to meet you too, {$catThinksPlayerNameIs}!
    -> (lie) Yeah, I've met a few.
        <<show_character Cat Neutral right>>
        {$catName}: Oh? You should introduce to them sometime!
        {$catName}: Can you name some planets that you've talked to before?
        -> Okay, you got me. I don't know any others.
            <<positive_indicator Cat>>
            <<set $catLikesYou += 1>>
            <<show_character Cat Happy2 right>>
            {$catName}: Heh, alright. It's all in good fun, right?
        -> (lie) Frakasa.
            {$catName}: Hm, I've never heard of that place before.
        -> (lie) Zorblax-9.
            {$catName}: Hm, I've never heard of that place before.
        -> (lie) Nebulon Prime.
            {$catName}: Hm, I've never heard of that place before.
        -> (lie) Venus.
            {$catName}: Venus? Isn't that pretty nearby? I've checked that place already.
        <<positive_indicator Cat>>
        <<set $catDislikesYou += 1>>
        <<show_character Cat Neutral right>>
        {$catName}: Are you sure you're not making this up?
        <<show_character Cat Thinking right>>
        {$catName}: I'm not sure why your kind would be out here alone, though.
    -> Why are you talking to me?
        <<show_character Cat Flirt right>>
        {$catName}: Well, you know what they say. Curiosity killed the felis catus linnaeus!
        <<show_character Cat Happy right>>
        {$catName}: I was on patrol for my planet, and I picked up your ship's signal. I thought I'd check in and see if you were alright.
        {$catName}: I don't suppose you've heard of Linnaeus, but we're a peaceful species, mostly just trying to explore the galaxy and make friends.
        -> I find that hard to believe.
            <<show_character Cat Sad right>>
            <<set $catDislikesYou += 1>>
            {$catName}: Why would you say that? I thought you seemed nice.
        -> It's.. nice to meet you.
            <<show_character Cat Happy right>>
            <<set $catLikesYou += 1>>
            {$catName}: Heh, likewise. It's been nice to meet you too, {$catThinksPlayerNameIs}!
<<show_character Cat Surprised right>>
{$catName}: Ah! Right, I have a meeting to get to. It's not my plan to keep you here for over an orbit.
<<if $catLikesYou >= $catDislikesYou>>
    <<show_character Cat Flirt right>>
    {$catName}: It's been nice talking to you, though! We can chat again soon, {$catThinksPlayerNameIs}.
    {$catName}: If you want to chat again, just give me a ping. See ya!~
    <<set $catSecured = true>>
<<else>>
    <<show_character Cat Neutral right>>
    {$catName}: Well, ${$catThinksPlayerNameIs}. I'll be on my way then. Take care of yourself out there.
<<endif>>
<<stop_voice_blip Cat>>
<<hide_all_characters>>
<<hide MonitorOn>>
<<show MonitorOnNoSilhouette>>
<<play_music MainTheme>>
<<if $catSecured == false>>
    <<deactivate_icon Cat>>
<<endif>>
Narrator: The transmission cuts off as {$catName} takes their leave.
<<if $catSecured == true>>
    Narrator: {$catName} uploads a sequence of radio waves in file format onto your device, letting you ping them again in the future.
    Narrator: Suddenly, the screen flickers back to static. {$catName} is gone, for now.
        -> {$catName}? Can you hear me?
            Narrator: You tap the monitor and move around the dials, but nobody responds. You are alone, again.
        -> (sit down)
<<endif>>
<<jump ThinkingAfterCatLeaves>>
===

title: ThinkingAfterCatLeaves
---
<<hide_all_objects>>
<<show Sitting>>
Narrator: You sit back down, trying to process the encounter you just had.
Narrator: Who was that creature? An alien? How is that even possible?
<<if $catLikesYou > $catDislikesYou>>
    <<set $posSelfPerception += 1>>
    Narrator: Strangely enough, you found the encounter with them rather pleasant. They seemed friendly, in their own way.
    Narrator: You think about what it would be like to have a friend like that. Someone who understands you, even if they are from another world.
    Narrator: You wonder if you'll ever see them again.
<<else>>
    Narrator: To you, the whole encounter felt off. Aliens? Of all times, why now?
    Narrator: You think about what others on Earth would say if they found out about this encounter. Although it is your job to report such things, would they really believe you met friendly animal aliens?
    Narrator: But you shake this idea off, for now. You'll think about it after a night's rest.
<<endif>>
<<hide_all_objects>>
<<show MonitorOn>>
<<show_character Dog Obscure left>>
<<show_character Opossum Obscure center>>
<<show_character Bird Obscure right>>
Narrator: As you sit there, lost in thought, the monitor flickers back to life once more.
Narrator: You see a total of three figures, all different from the last one.
Narrator: They all seem like they'll stick around for a bit. Who do you talk to first?
    -> (respond to the one with the floppy ears)
        <<set $talkedToDog = true>>
        <<jump DogIntro>>
    -> (respond to the one with the frazzled hair)
        <<set $talkedToOpossum = true>>
        <<jump OpossumIntro>>
    -> (respond to the one with the pointy muzzle)
        <<set $talkedToBird = true>>
        <<jump BirdIntro>>
===

title: TalkToOthers
---
<<hide_all_characters>>
<<show MonitorOn>>
<<if $talkedToDog == false>>
    <<show_character Dog Obscure left>>
<<endif>>
<<if $talkedToOpossum == false>>
    <<show_character Opossum Obscure center>>
<<endif>>
<<if $talkedToBird == false>>
    <<show_character Bird Obscure right>>
<<endif>>
Narrator: You notice that others are still live on the feed. You can choose someone to talk to next.
    -> (respond to the one with the floppy ears) <<if $talkedToDog == false>>
        <<set $talkedToDog = true>>
        <<jump DogIntro>>
    -> (respond to the one with the frazzled hair) <<if $talkedToOpossum == false>>
        <<set $talkedToOpossum = true>>
        <<jump OpossumIntro>>
    -> (respond to the one with the pointy muzzle) <<if $talkedToBird == false>>
        <<set $talkedToBird = true>>
        <<jump BirdIntro>>
===

title: DogIntro
position: 635,249
---
<<declare $knowsAboutBarklonian = false>>
<<hide_all_characters>>
<<hide_all_objects>>
<<show MonitorOn>>
Narrator: You pull up the communication panel and respond to the figure with the floppy ears.
<<show_character Dog Obscure right>>
Narrator: You turn the dials to let the signal through.
<<show_character Dog Neutral right>>
<<start_voice_blip Dog>>
<<play_music DogTheme 0.5>>
???: Oh, woofdy. I didn't think anyone would answer my signal.
???: What're you doing out here? Woof.
    -> Who are you?
    -> What in the world? A purple dog?
        <<set $dogDislikesYou += 1>>
        <<stop_voice_blip Dog>>
        <<show_character Dog Thinking right>>
        Narrator: The figure on the monitor tilts their head in confusion. They seem a little offended.
        <<start_voice_blip Dog>>
        {$dogName}: Woof? A dog? Woof. I'm not sure what you mean. Woof.
        {$dogName}: I'm a Barklonian, if you've heard of us. A former one. Woof.
        <<set $knowsAboutBarklonian = true>>
    -> ...
<<start_voice_blip Dog>>
<<show_character Dog Happy right>>
{$dogName}: I'm {$dogName}. Woof. I come in peace. Woof.
{$dogName}: I saw your signal, and thought I'd come check it out. Woof.
    -> What... are you?
        <<positive_indicator Dog>>
        <<set $dogLikesYou += 1>>
        <<if $knowsAboutBarklonian == false>>
            <<set $knowsAboutBarklonian = true>>
            {$dogName}: I don't know what you'd call it. I'm a Barklonian. Woof.
        <<else>>
            {$dogName}: Like I said, I'm a Barklonian. Woof.
        <<endif>>
        <<show_character Dog Thinking right>>
        {$dogName}: Just like you, I'm exploring space. Been out here for some days now. Woof.
        <<show_character Dog Neutral right>>
        {$dogName}: It's a big place out here. I kind of like it. Woof.
        -> But... where do you come from?
            {$dogName}: Hm? Oh, I come from a place far away. Woof. Haven't been there in a while. Not many of us stay.
            {$dogName}: It's a planet called Barklon. Woof.
            {$dogName}: It's a nice place. Lots of open fields to run around in. Woof.
        -> And you're a dog?
            <<show_character Dog Surprised right>>
            {$dogName}: A dog? What is that? Woof.
            <<show_character Dog Neutral right>>
            {$dogName}: I should look into this "dog" thing. Woof.
            <<show_character Dog Happy right>>
            {$dogName}: But no, I'm from a planet called Barklon. Woof.
            {$dogName}: It's... a pretty miserable place. Would not recommend it. Lots of bad people. Woof.
        -> ...
    -> Why do you keep saying woof?
        {$dogName}: I don't know what you mean. Woof.
        -> Like that! It's weird.
            <<set $dogDislikesYou += 1>>
            <<show_character Dog Sad right>>
            {$dogName}: Hm. Woof. I'm sorry if it bothers you. Woof.
            {$dogName}: It's just how we talk back home. Woof.
        -> I think it's cute.
            <<positive_indicator Dog>>
            <<set $dogLikesYou += 1>>
            <<show_character Dog Happy right>>
            {$dogName}: Woof! Even though I don't know what I'm doing, I'm glad you like it. Woof.
            {$dogName}: It's just how we talk back home. Woof.
        -> ...
    -> Hi {$dogName}. I'm {$playerName}.
        <<positive_indicator Dog>>
        <<set $dogLikesYou += 1>>
        <<set $dogThinksPlayerNameIs = $playerName>>
        <<set $dogKnowsPlayerName = true>>
        {$dogName}: Woof! It's nice to meet you, {$dogThinksPlayerNameIs}!
        <<if $playerName == "Dipper">>
            {$dogName}: That's a pretty silly name. It rhymes with Bipper. Woof.
        <<endif>>
<<show_character Dog Neutral right>>
{$dogName}: I hope you don't mind me dropping by unannounced. Woof.
    -> Alien... why are you still here?
        <<set $dogDislikesYou += 1>>
        <<show_character Dog Sad right>>
        {$dogName}: Woof. I'm sorry if I bothered you. Woof.
        {$dogName}: I just saw your signal and thought I'd come check it out. Woof.
    -> What are you doing here?
        {$dogName}: Oh, I was just passing by. Woof.
    -> Do you know {$catName}?
        {$dogName}: {$catName}? Can't say I'm familiar. We're not that interconnected here in space.
        {$dogName}: Besides, I've been meeting tons of people. It's hard to keep track. Woof.
        <<if $dogLikesYou >= $dogDislikesYou>>
            <<show_character Dog Happy right>>
            {$dogName}: You're pretty friendly, though. Woof. Don't see many of those around here.
        <<else>>
            <<show_character Dog Neutral right>>
            {$dogName}: You seem okay, though. Woof. Not many friendly folks out here.
        <<endif>>
{$dogName}: It's rare that I even see another living being out here. Woof. 
<<show_character Dog Neutral right>>
<<if $dogKnowsPlayerName == false>>
    <<set $dogKnowsPlayerName = true>>
    {$dogName}: But. Woof. What should I call you?
        -> {$playerName}.
            <<positive_indicator Dog>>
            <<set $dogLikesYou += 1>>
            {$dogName}: Woof! {$playerName}, that's a nice name.
            <<if $playerName == "Dipper">>
                {$dogName}: It rhymes with Bipper. Woof.
            <<endif>>
            <<set $dogThinksPlayerNameIs = $playerName>>
        -> ...
            {$dogName}: Hm, that's okay. Woof. I guess I'll just call you "Human". You're a human, right? Woof.
<<endif>>
{$dogName}: Anyways, I've mostly wandered on my own, recently. Woof. Picked up on your signal. Woof.
    -> Signal? 
        {$dogName}: Yeah, I saw it while I was out exploring. Woof.
        <<show_character Dog Happy right>>
        {$dogName}: I like to wander around and see new places. Woof. I've traveled thousands of barksecs from my home planet. Woof.
    -> That sounds pretty lonely.
        <<show_character Dog Thinking right>>
        <<set $dogDislikesYou += 1>>
        {$dogName}: Lonely? Woof. Not really. Woof.
        {$dogName}: I like my own company. Woof. Plus, I meet new people all the time. Woof.
    -> Do you do this often?
        <<show_character Dog Sad right>>
        {$dogName}: Mmm... not really. 
    <<show_character Dog Angry right>>
    {$dogName}: My home planet is kind of a dump. I decided to leave and explore the galaxy. Woof.
<<show_character Dog Neutral right>>
{$dogName}: What about you? What are you doing out here? Woof.
    -> My job is to report alien life.
        <<show_character Dog Thinking right>>
        {$dogName}: Alien life? What for? Woof.
        {$dogName}: I don't see the point in all that. Woof. Why?
            -> My planet, Earth, is searching for them.
                <<show_character Dog Sad right>>
                {$dogName}: That sounds a little scary... 
                {$dogName}: I hope that doesn't mean your planet is out to get me. Woof.
                <<set $dogDislikesYou += 1>>
            -> I'm here to meet new beings.
                <<positive_indicator Dog>>
                <<set $dogLikesYou += 1>>
                <<show_character Dog Happy right>>
                {$dogName}: Woof. You should join me sometime. I see a lot of interesting beings everyday.
    -> I'm exploring, just like you.
        <<positive_indicator Dog>>
        <<set $dogLikesYou += 1>>
        {$dogName}: Exploring? Woof. We should go together sometime!
<<show_character Dog Neutral right>>
{$dogName}: Anyways, I should get going. Woof.
{$dogName}: Space is a big place, and I have a lot of wandering to do. Woof.
<<if $dogLikesYou >= $dogDislikesYou>>
    <<show_character Dog Flirt right>>
    {$dogName}: If you ever need anything, just send out a signal. Woof. I'll come find you.
    <<set $dogSecured = true>>
<<else>>
    <<show_character Dog Neutral right>>
    {$dogName}: Well, it was cool meeting you. Bye! Woof.
<<endif>>
<<stop_voice_blip Dog>>
<<hide_all_characters>>
<<play_music MainTheme>>
<<if $dogSecured == false>>
    <<deactivate_icon Dog>>
<<endif>>
Narrator: The transmission cuts off as {$dogName} leaves to continue their wandering.
<<if $dogSecured == true>>
    Narrator: You notice a transmission from {$dogName} saved onto your monitor. You can likely use this to ping them again in the future.
<<endif>>
<<if $talkedToOpossum == false || $talkedToBird == false>>
    <<jump TalkToOthers>>
<<else>>
    <<jump EndDay1>>
<<endif>>
===

title: OpossumIntro
position: 638,-61
---
<<hide_all_characters>>
<<hide_all_objects>>
Narrator: You pull up the communication panel and respond to the figure with the frazzled hair.
<<show_character Opossum Obscure right>>
Narrator: You turn the dials to let the signal through.
<<show_character Opossum Neutral right>>
<<start_voice_blip Opossum>>
<<play_music_start_loop OpossumThemeStart OpossumThemeLoop 0.4 0.5>>
???: Oh, hey. I didn't expect anyone to actually answer.
???: I... don't really chat with others much. It's cool to meet you, I guess.
    -> Holy shit. Are you a rat?
        <<show_character Opossum Angry right>>
        ???: What the fuck is a rat...
        <<set $opossumDislikesYou += 1>>
        ???: Forget I asked. Peace.
        <<stop_voice_blip Opossum>>
        <<hide_all_characters>>
        <<play_music MainTheme>>
        <<deactivate_icon Opossum>>
        Narrator: The transmission cuts off abruptly. It seems they didn't want to talk to you anymore.
        <<if $talkedToDog == false || $talkedToBird == false>>
            <<jump TalkToOthers>>
        <<else>>
            <<jump EndDay1>>
        <<endif>>
    -> Who are you?
        {$opossumName}: The name's {$opossumName}. I'm... from nowhere, really.
    -> What's your story?
        <<show_character Opossum Confused right>>
        ???: Nothing I'm comfortable with sharing with a stranger. I'm just from a little messed up planet in the middle of nowhere.
        <<show_character Opossum Neutral right>>
        {$opossumName}: The name's {$opossumName}, by the way.
    {$opossumName}: And you? What should I call you?
        -> I'm {$playerName}.
            <<set $opossumThinksPlayerNameIs = $playerName>>
            <<show_character Opossum Happy right>>
            <<positive_indicator Opossum>>
            <<set $opossumLikesYou += 1>>
            {$opossumName}: That's a pretty sweet name. I dig it.
        -> A human.
            <<show_character Opossum Thinking right>>
            {$opossumName}: A human, huh? Never met one of you before.
            {$opossumName}: "Human" it is.
        -> What the fuck?
            <<show_character Opossum Confused right>>
            {$opossumName}: Whoa, no need to be rude. I can just go, if you want to.
                -> Yes, please.
                    <<stop_voice_blip Opossum>>
                    <<hide_all_characters>>
                    <<play_music MainTheme>>
                    <<deactivate_icon Opossum>>
                    Narrator: The transmission cuts off abruptly. You have a feeling they won't come back.
                    <<if $talkedToDog == false || $talkedToBird == false>>
                        <<jump TalkToOthers>>
                    <<else>>
                        <<jump EndDay1>>
                    <<endif>>
                -> ...
                    <<show_character Opossum Confused right>>
                    {$opossumName}: Okay, I can sense when I'm not wanted.
                    <<stop_voice_blip Opossum>>
                    <<hide_all_characters>>
                    <<play_music MainTheme>>
                    <<deactivate_icon Opossum>>
                    Narrator: The transmission cuts off abruptly. You have a feeling they won't come back.
                    <<if $talkedToDog == false || $talkedToBird == false>>
                        <<jump TalkToOthers>>
                    <<else>>
                        <<jump EndDay1>>
                    <<endif>>
                -> No, stay.
                    <<show_character Opossum Flirt right>>
                    <<positive_indicator Opossum>>
                    <<set $opossumLikesYou += 1>>
                    {$opossumName}: ...alright, if you say so.
<<show_character Opossum Thinking right>>
<<stop_voice_blip Opossum>>
Narrator: You notice that {$opossumName} seems a bit distant, not really making eye contact with you.
    -> Why are so many aliens showing up today?
        <<set $opossumDislikesYou += 1>>
        Narrator: {$opossumName} sighs heavily. They seem annoyed by your question.
    -> What's wrong?
        <<show_character Opossum Neutral right>>
        <<positive_indicator Opossum>>
        <<set $opossumLikesYou += 1>>
        <<start_voice_blip Opossum>>
        {$opossumName}: ...
    -> ...
    <<start_voice_blip Opossum>>
    <<show_character Opossum Thinking right>>
    {$opossumName}: I don't know. Space just has a lot of weirdos.
    {$opossumName}: I feel like I just don't really have anywhere else to go.
    <<show_character Opossum Angry right>>
    {$opossumName}: God... I <i>hate</i> space. It's so... <i>empty</i>. 
        -> What are you doing out here, then?
        -> I find it relaxing.
        -> ...
    <<show_character Opossum Sad right>>
    {$opossumName}: Ah! Sorry, I didn't mean to unload all that on you.
<<show_character Opossum Thinking right>>
{$opossumName}: <i>Why am I like this...</i>
<<show_character Opossum Flirt right>>
{$opossumName}: Look, I reached out because I'm looking for some people to talk to.
<<show_character Opossum Neutral right>>
{$opossumName}: I don't really have anywhere else to go right now, so it'd be nice to have a bit of company.
    -> Sure, I can do that.
        <<show_character Opossum Confused right>>
        <<positive_indicator Opossum>>
        <<set $opossumLikesYou += 1>>
        {$opossumName}: R-really? Just like that?
    -> I don't really know you.
        <<show_character Opossum Confused right>>
        {$opossumName}: That's... fair.
        <<show_character Opossum Neutral right>>
        {$opossumName}: Thanks for dealing with me being around for a while, anyways.
    -> Go away!
        <<show_character Opossum Angry right>>
        {$opossumName}: ...
        {$opossumName}: I don't even know why I try...
        <<stop_voice_blip Opossum>>
        <<hide_all_characters>>
        <<play_music MainTheme>>
        <<deactivate_icon Opossum>>
        Narrator: The transmission cuts off abruptly. You have a feeling they won't come back.
        <<if $talkedToDog == false || $talkedToBird == false>>
            <<jump TalkToOthers>>
        <<else>>
            <<jump EndDay1>>
        <<endif>>
<<if $opossumLikesYou >= $opossumDislikesYou>>
    <<show_character Opossum Happy right>>
    <<set $opossumSecured = true>>
    {$opossumName}: Thanks for listening, though. I enjoyed the short talk. Let's talk for real, later.
<<else>>
    <<show_character Opossum Thinking right>>
    {$opossumName}: Sorry for that. You probably don't want to hear about my problems. I'll leave you alone.
<<endif>>
<<if $opossumSecured == false>>
    <<deactivate_icon Opossum>>
<<endif>>
<<hide_all_characters>>
<<play_music MainTheme>>
Narrator: The transmission cuts off abruptly. {$opossumName} takes their leave.
<<if $opossumSecured == true>>
    Narrator: You notice a transmission from {$opossumName} recorded on your monitor. They're letting you ping them again in the future.
<<endif>>
<<if $talkedToDog == false || $talkedToBird == false>>
    <<jump TalkToOthers>>
<<else>>
    <<jump EndDay1>>
<<endif>>
===

title: BirdIntro
position: 633,86
---
<<declare $birdExplainedIntention = false>>
<<hide_all_characters>>
<<hide_all_objects>>
Narrator: You pull up the communication panel and respond to the figure with the pointy muzzle.
<<show_character Bird Obscure right>>
Narrator: You turn the dials to let the signal through.
<<show_character Bird Happy2 right>>
<<start_voice_blip Bird>>
<<play_music BirdTheme 0.25>>
???: Ah, hello alternate lifeform! It's so nice to meet you.
<<show_character Bird Nervous right>>
{$birdName}: I'm {$birdName}, a scientist of the planet Aviaris and... um, seeker of knowledge.
    -> What... are you?
        <<show_character Bird Confused right>>
        {$birdName}: Oh, right. I take it you haven't heard of us before.
        <<show_character Bird Neutral right>>
        {$birdName}: I'm an Avianthrope, hailing from the planet Aviaris.
    -> Aviaris? Is that a word?
        <<show_character Bird Confused right>>
        {$birdName}: Oh, right. I take it you haven't heard of us before.
        <<show_character Bird Neutral right>>
        {$birdName}: It's just the name of my home planet! Don't worry, I come in peace.
        {$birdName}: I'm an Avianthrope, and Aviaris is the name of our planet.
    -> ...
<<stop_voice_blip Bird>>
<<show_character Bird Nervous right>>
Narrator: You notice that the figure seems a little nervous. They fidget slightly as they speak.
<<start_voice_blip Bird>>
{$birdName}: If you don't mind, I have a few questions about your species. It's nothing intrusive, I promise.
    -> Sure..?
        <<positive_indicator Bird>>
        <<set $birdLikesYou += 1>>
        <<show_character Bird Happy right>>
        {$birdName}: Wonderful! I appreciate your cooperation.
    -> I'd rather not.
        <<set $birdDislikesYou += 1>>
        <<show_character Bird Nervous right>>
        {$birdName}: Hm. I see. Are you sure? It wouldn't take up too much of your time.
            -> Yes, I'm sure.
                <<show_character Bird Sad right>>
                {$birdName}: Okay then, I know when I'm not wanted. Farewell.
                <<stop_voice_blip Bird>>
                <<hide_all_characters>>
                <<play_music MainTheme>>
                <<deactivate_icon Bird>>
                Narrator: The transmission cuts off abruptly. That was strange.
                <<if $talkedToDog == false || $talkedToOpossum == false>>
                    <<jump TalkToOthers>>
                <<else>>
                    <<jump EndDay1>>
                <<endif>>
            -> ...fine.
                <<positive_indicator Bird>>
                <<set $birdLikesYou += 1>>
                <<show_character Bird Happy right>>
                {$birdName}: Thank you! I appreciate it.
    -> What are you studying?
            <<show_character Bird Happy right>>
            {$birdName}: Ah, well, I'm primarily interested in exploring the unknown.
            <<show_character Bird Thinking right>>
            {$birdName}: It's... hard to explain why I'm so interested in this. It's been a fascination of mine recently.
            {$birdName}: I'm just curious about other lifeforms. Understanding others helps me understand myself better, I think.
            <<set $birdExplainedIntention = true>>
    -> I have so many questions...
        <<set $birdDislikesYou += 1>>
        <<show_character Bird Sad right>>
        {$birdName}: Oh... I apologize if I was too forward!
        <<show_character Bird Nervous right>>
        {$birdName}: If you want, I suppose I can answer a question of yours first.
        -> What are you studying?
            <<show_character Bird Happy right>>
            {$birdName}: Ah, well, I'm primarily interested in exploring the unknown.
            <<show_character Bird Thinking right>>
            {$birdName}: It's... hard to explain why I'm so interested in this. It's been a fascination of mine recently.
            {$birdName}: I suppose I'm just curious about other lifeforms. Understanding others helps me understand myself better, I think.
            <<set $birdExplainedIntention = true>>
        -> What is like being a bird? Can you fly?
            <<show_character Bird Confused right>>
            {$birdName}: Oh, um. I don't know what a "bird" is. But, I can fly, yes.
            {$birdName}: It's... hard to explain what it's like. It's just part of who I am, I suppose.
            <<show_character Bird Sweat right>>
            {$birdName}: I'm not even going to ask how you knew I <i>had</i> wings.
        -> Why do you want to know about me?
            <<show_character Bird Thinking right>>
            {$birdName}: Hm... it's pretty hard to pinpoint.
            {$birdName}: I suppose I'm just curious about other lifeforms. Understanding others helps me understand myself better, I think.
            <<set $birdExplainedIntention = true>>
        -> I don't want to answer your questions.
            <<show_character Bird Sad right>>
            {$birdName}: Okay then. Thanks for your time anyways. Bye.
            <<stop_voice_blip Bird>>
            <<hide_all_characters>>
            <<play_music MainTheme>>
            <<deactivate_icon Bird>>
            Narrator: The transmission cuts off abruptly. That was strange.
            <<if $talkedToDog == false || $talkedToOpossum == false>>
                <<jump TalkToOthers>>
            <<else>>
                <<jump EndDay1>>
            <<endif>>
<<show_character Bird Happy right>>
{$birdName}: Anyways, before we start, what should I call you?
    -> {$playerName}.
        <<positive_indicator Bird>>
        <<set $birdLikesYou += 1>>
        <<show_character Bird Happy2 right>>
        <<set $birdThinksPlayerNameIs = $playerName>>
        {$birdName}: {$birdThinksPlayerNameIs}? Lovely name.
    -> I don't have a name.
        <<show_character Bird Thinking right>>
        {$birdName}: Oh, I see. That's... inconvenient. I'll call you human, then. That is what you are, no?
        -> Yes, human works.
            <<positive_indicator Bird>>
            <<set $birdLikesYou += 1>>
            {$birdName}: Excellent. Human it is.
        -> No. I'm not a human.
            <<set $birdDislikesYou += 1>>
            <<show_character Bird Confused right>>
            {$birdName}: Hm. That's... troubling. I'll just list down your galaxy and coordinates.
            <<show_character Bird Nervous right>>
            {$birdName}: It's... a pleasure to meet you, MW-012!
            <<set $birdThinksPlayerNameIs = "MW-012">>
    -> I don't want to take your quiz.
        <<set $birdDislikesYou += 1>>
        <<show_character Bird Nervous right>>
        {$birdName}: Aw, are you sure? It'd be quick.
            -> Yes, I'm sure.
                <<show_character Bird Sad right>>
                {$birdName}: Okay then. Thanks for your time anyways. Bye.
                <<stop_voice_blip Bird>>
                <<hide_all_characters>>
                <<play_music MainTheme>>
                <<deactivate_icon Bird>>
                Narrator: The transmission cuts off abruptly. That was strange.
                <<if $talkedToDog == false || $talkedToOpossum == false>>
                    <<jump TalkToOthers>>
                <<else>>
                    <<jump EndDay1>>
                <<endif>>
            -> ...fine.
                <<positive_indicator Bird>>
                <<set $birdLikesYou += 1>>
                <<show_character Bird Happy right>>
                {$birdName}: Thank you! I appreciate it.
<<show_character Bird Happy right>>
{$birdName}: Okay, first question. What's your reason for being out here in space?
    -> Exploring the cosmos.
        <<show_character Bird Confused right>>
        {$birdName}: Oh, wow... that's... noble. You actually <i>enjoy</i> being in space?
        <<show_character Bird Neutral right>>
        {$birdName}: That's surprising. Can you tell me more?
            -> I like the quiet.
                <<positive_indicator Bird>>
                <<set $birdLikesYou += 1>>
                <<show_character Bird Happy right>>
                {$birdName}: Ah, I see. Space is indeed quite peaceful.
                {$birdName}: I'd be interested to learn more about that, perhaps later.
            -> I like the adventure.
                <<positive_indicator Bird>>
                <<set $birdLikesYou += 1>>
                <<show_character Bird Happy2 right>>
                {$birdName}: Ah, a thrill-seeker. I don't get out too much myself, but I admire it.
                {$birdName}: I'd like to know more about that, perhaps later.
            -> I like reporting your kind.
                <<show_character Bird Disgusted right>>
                {$birdName}: Is that... a threat?
                <<set $birdDislikesYou += 2>>
                <<if $birdLikesYou <= $birdDislikesYou>>
                    <<show_character Bird Sad right>>
                    {$birdName}: I... see. Well, um. I should take my leave, then. Farewell.
                    <<stop_voice_blip Bird>>
                    <<hide_all_characters>>
                    <<play_music MainTheme>>
                    <<deactivate_icon Bird>>
                    Narrator: The transmission cuts off abruptly. That was strange.
                    <<if $talkedToDog == false || $talkedToOpossum == false>>
                        <<jump TalkToOthers>>
                    <<else>>
                        <<jump EndDay1>>
                    <<endif>>
                <<else>>
                    <<show_character Bird Nervous right>>
                    {$birdName}: I suppose... maybe I can understand that, from a scientific perspective.
                    {$birdName}: I'd like to know more about that, perhaps later.
                <<endif>>
    -> Escaping humanity.
        <<show_character Bird Thinking right>>
        {$birdName}: Hm... Space can be a refuge for some.
        <<show_character Bird Nervous right>>
        {$birdName}: Tell me more about that. What do you not like about humanity?
            -> People are cruel.
                <<show_character Bird Sad right>>
                {$birdName}: Oh? I... I'm sorry to hear that.
                {$birdName}: I am curious, but perhaps that conversation is best for later.
            -> They don't understand me.
                <<show_character Bird Sad right>>
                {$birdName}: Ah... I'm sorry to hear that.
                {$birdName}: I'd like to know more about that, perhaps later.
            -> I just don't fit in.
                <<show_character Bird Sad right>>
                {$birdName}: I... can get that. Space can be a good place to clear one's mind.
            -> ...
                <<set $birdDislikesYou += 1>>
                <<show_character Bird Nervous right>>
                {$birdName}: Ah, perhaps I overstepped.
                <<show_character Bird Thinking right>>
                {$birdName}: Let's just... move onto the last question.
    -> I'm not sure.
        <<show_character Bird Thinking right>>
        <<set $birdDislikesYou += 1>>
        {$birdName}: Hm, that's understandable. I'll put that down as...
        <<show_character Bird Sweat right>>
        {$birdName}: Um, I guess I'll leave that one as blank.
<<show_character Bird Happy right>>
{$birdName}: Next question! How would you describe the ambient temperature of your home planet?
    -> Around 15 degrees Celsius.
        <<positive_indicator Bird>>
        <<set $birdLikesYou += 1>>
        <<show_character Bird Happy right>>
        {$birdName}: Interesting! I appreciate the exact answer. You know your stuff.
    -> Very mild.
        <<show_character Bird Confused right>>
        {$birdName}: Can you... be more specific? Sorry, I just need a better idea of what it's like.
        -> It kind of changes? Like... it's hot sometimes, and cold other times.
            <<positive_indicator Bird>>
            <<set $birdLikesYou += 1>>
            <<show_character Bird Happy right>>
            {$birdName}: Ah, a planet with varying seasons. Noted!
        -> Like springtime.
            <<set $birdDislikesYou += 1>>
            <<show_character Bird Disgusted right>>
            {$birdName}: ...
            <<stop_voice_blip Bird>>
            Narrator: {$birdName} seems unsatisfied by your vague answer, but seems not to ask further out of politeness.
            <<start_voice_blip Bird>>
    -> It varies... hot... and cold? Rainy? Windy?
        <<positive_indicator Bird>>
        <<set $birdLikesYou += 1>>
        <<show_character Bird Happy right>>
        {$birdName}: Ah, a planet with varying seasons. Noted!
<<show_character Bird Happy right>>
{$birdName}: And then, may I ask, how are relationships typically formed on your planet?
    -> Through mutual interest.
        <<positive_indicator Bird>>
        <<set $birdLikesYou += 1>>
        <<show_character Bird Happy2 right>>
        {$birdName}: A very sensible approach. Very nice.
        <<show_character Bird Nervous right>>
        {$birdName}: Very... akin to how we do it back in Aviarian society.
    -> I wouldn't really know.
        <<set $birdDislikesYou += 1>>
        <<show_character Bird Sad right>>
        {$birdName}: Aw, I'm sorry to hear that.
        <<show_character Bird Confused right>>
        {$birdName}: Surely love is still a construct that exists?
    -> It's kind of random.
        <<positive_indicator Bird>>
        <<set $birdLikesYou += 1>>
        <<show_character Bird Happy right>>
        {$birdName}: Ah, the thrill of serendipity! I like that.
        <<show_character Bird Nervous right>>
        {$birdName}: Very... akin to how we do it back in Aviarian society. You never really know when love will strike.
<<show_character Bird Happy right>>
{$birdName}: That concludes our little questionnaire. Thank you for your cooperation.
    -> No problem.
        <<positive_indicator Bird>>
        <<set $birdLikesYou += 1>>
        <<show_character Bird Happy2 right>>
        {$birdName}: Wonderful! I appreciate your willingness to help further scientific knowledge.
    -> ...that's it?
        <<set $birdDislikesYou += 1>>
        <<show_character Bird Neutral right>>
        {$birdName}: Um, yes. Thank you for your time.
        <<show_character Bird Sweat right>>
        {$birdName}: I don't know what else to say here...
    -> Why are you so interested in this? <<if $birdExplainedIntention == false>>
        <<set $birdExplainedIntention = true>>
        <<show_character Bird Thinking right>>
        {$birdName}: Mmm, well, as a scientist, I'm always eager to learn more about different lifeforms.
        {$birdName}: Your species is particularly intriguing to me, given your unique characteristics.
        <<if $birdLikesYou >= $birdDislikesYou>>
            <<show_character Bird Happy right>>
            <<positive_indicator Bird>>
            <<set $birdLikesYou += 1>>
            {$birdName}: I hope to learn about you more in the future.
        <<else>>
            <<show_character Bird Neutral right>>
            <<set $birdDislikesYou += 1>>
            {$birdName}: But I understand if you're not interested in sharing more.
        <<endif>>
<<show_character Bird Nervous right>>
{$birdName}: I guess, um, I'll take my leave, then. Do you have any closing thoughts?
    -> Why talk to me? <<if $birdExplainedIntention == false>>
        <<set $birdExplainedIntention = true>>
        <<positive_indicator Bird>>
        <<set $birdLikesYou += 1>>
        <<show_character Bird Thinking right>>
        {$birdName}: Ah, that's a good question. One I haven't really thought much about myself yet.
        <<show_character Bird Nervous right>>
        {$birdName}: I suppose I'm just curious about other lifeforms. Understanding others helps me understand myself better, I think.
    -> It was nice talking to you.
        <<positive_indicator Bird>>
        <<set $birdLikesYou += 1>>
        <<if $birdLikesYou >= $birdDislikesYou>>
            <<show_character Bird Happy2 right>>
            {$birdName}: Likewise! I hope we can converse again in the future.
            <<set $birdSecured = true>>
        <<endif>>
    -> Bye...
        <<show_character Bird Neutral right>>
        {$birdName}: Farewell, then. Safe travels out there in the cosmos.
    -> ...
<<if $birdLikesYou >= $birdDislikesYou && $birdSecured == false>>
    <<show_character Bird Flirt right>>
    {$birdName}: If you ever wish to converse again, just ping me. I would be delighted to respond.
    <<set $birdSecured = true>>
<<endif>>
<<stop_voice_blip Bird>>
<<hide_all_characters>>
<<play_music MainTheme>>
<<if $birdSecured == false>>
    <<deactivate_icon Bird>>
<<endif>>
Narrator: The transmission cuts off as {$birdName} takes their leave.
<<if $birdSecured == true>>
    Narrator: You notice a transmission from {$birdName} saved onto your monitor. You can reach out at any time later.
<<endif>>
<<if $talkedToDog == false || $talkedToOpossum == false>>
    <<jump TalkToOthers>>
<<else>>
    <<jump EndDay1>>
<<endif>>
===

title: EndDay1
position: 925,83
---
<<hide_all_characters>>
<<hide_all_objects>>
Narrator: After talking to so many new figures, a sense of fatigue overcomes you.
Narrator: You decide to call it a day, and take a short nap. Perhaps afterwards, you can decide what you want to do next.
<<show ScreenBlocker>>
<<wait 2>>
<<hide ScreenBlocker>>
<<jump StartDay2>>
===