title: StartDay2
position: 90,-43
---
<<set $daysPassed = 2>>
<<play_sound AlarmClock>>
I wake up to a loud chime on the monitor, my head pounding yet again. 
I don't know what time it is, but I feel rested enough. My sleep cycle has been all over the place since I started this job.
Time for another day of work.
<<show_ui ClickToInteract>>
<<show MonitorOffDay2>>
===

title: OnClickedMonitorDay2
position: 85,114
---
<<hide_all_ui_objects>>
<<show MonitorOn>>
I turn on the monitor to see today's tasks.
Before I start my report, I notice a new message from HQ.
It reads, "Remember your duty to report accurately and without bias. Your observations are crucial for our understanding and eradication of foreign threats."
<<if $catSecured == false>>
    <<jump ForeverAlone>>
<<endif>>
I ponder my next report. Do I report the encounter with the alien, knowing that Earth may react harshly? Or keep it under wraps?
    -> (report the alien)
        I hesitate for a moment. HQ has been known to treat the unfamiliar as hostile, and they may get the wrong message.
        I'm not entirely sure what will happen to the alien, but I may never see them again.
        -> (report the alien)
            I get to writing my report.
            <<hide_all_objects>>
            <<show PCRoom>>
            <<show_ui TypeToInteract>>
            <<show MonitorReportDay2>>
        -> (report that you saw nothing)
            <<hide_all_objects>>
            <<show PCRoom>>
            I get to writing my report.
            <<show_ui TypeToInteract>>
            <<show MonitorDontReportDay2>>
    -> (report that you saw nothing)
        <<hide_all_objects>>
        <<show PCRoom>>
        I get to writing my report.
        <<show_ui TypeToInteract>>
        <<show MonitorDontReportDay2>>
===

title: KeptUnderWrapsDay2
position: 83,279
---
<<hide_all_ui_objects>>
<<set $negSelfPerception += 1>>
I hesitate, then send off the message. I feel <color=\#FF8888>a little guilty</color>. My job was to report alien life... but something about this situation feels different.
<<if $posSelfPerception > $negSelfPerception>>
    I feel a sense of pride in my decision. I trust my instincts and believe that not everything needs to be reported.
<<else>>
    I feel a pang of guilt. However, I rationalize that some things are better left unsaid.
<<endif>>
I turn the monitor off and take a seat.
<<fade_transition 2>>
<<wait 1>>
<<hide_all_objects>>
<<show Stars>>
<<show Sitting>>
Lost in another one of my spirals, I think about my family back on Earth.
My family was only ever close with me before I dropped out of college. 
I was surprised that they disagreed with my decision to leave Earth, even though I never quite felt like they loved me anymore.
Recalling my parents' disapproval makes me feel...
    -> (sad, because they're gone now)
        <<set $negSelfPerception += 1>>
        I feel <color=\#FF8888>a wave of sadness wash over me</color>. I wonder if they would have ever accepted my choices, but now I'll never know.
    -> (angry, because they never supported you)
        <<set $negSelfPerception += 1>>
        I feel a surge of anger. They never supported my decisions, and now that they're gone, I feel <color=\#FF8888>a sense of bitterness</color>.
    -> (conflicted, because they didn't want you gone)
        I feel conflicted. They didn't want me to leave, but they never made an effort to understand my reasons.
        <<set $posSelfPerception += 1>>
        I wish they could see me now, living life on my own terms. I feel <color=\#88FF88>independent</color>.
And what about my friends? I always felt like such an impostor around them. Every interaction felt like a performance.
I forced myself to do so many things with them. I only ever felt like I was "fun" when I was a couple of drinks in. I wonder if they ever really liked me for who I was.
Thinking about these people in my life makes me feel...
    -> (lonely, because you pushed them away)
        <<set $negSelfPerception += 1>>
        I realize that I pushed people away, and now I'm <color=\#FF8888>left with nothing but my own thoughts</color>.
    -> (regretful, because you never opened up to them)
        <<set $negSelfPerception += 1>>
        I <color=\#FF8888>wish I had opened up to people more</color>, but now it feels too late.
    -> (indifferent, because they weren't true friends)
        Maybe those people weren't true friends after all. I wonder if I'll be able to make genuine connections in the future.
        <<set $posSelfPerception += 1>>
        The idea of making real connections makes me <color=\#88FF88>feel a little hopeful</color>.

I then remember what happened yesterday, with the alien.
Supposedly, I can ping them again to have a chat. Maybe I should give it a try.
    -> (reestablish contact with the alien)
        <<jump CallCatDay2>>
===

title: CallCatDay2
position: 422,282
---
<<hide_all_objects>>
<<hide_all_ui_objects>>
<<show PCRoom>>
<<hide MonitorOn>>
<<show MonitorSilhouette>>
<<show_character Cat Obscure right>>
<<stop_voice_blip Cat>>
I call {$catName}. The line remains static for a little while, but I suddenly see movement on the other end.
<<play_music CatTheme 0.5>>
<<if $catLikesYou > $catDislikesYou>>
    <<show_character Cat Happy right>>
    {$catName} appears on the screen, looking relieved to see you.
    <<show_character Cat Happy2 right>>
    <<start_voice_blip Cat>>
    {$catName}: Hey {$catThinksPlayerNameIs}, I was just thinking about you! Do you need something?
        -> I want to get to know you better.
            <<positive_indicator Cat>>
            <<set $catLikesYou += 1>>
            <<show_character Cat Happy right>>
            {$catName}: I'd like that. It's nice to have someone to talk to out here.
<<else>>
    <<show_character Cat Neutral right>>
    {$catName} appears on the screen.
    <<start_voice_blip Cat>>
    {$catName}: Oh, it's you! Hey again, {$catThinksPlayerNameIs}. What's up?
        -> I want to get to know you better.
            <<show_character Cat Thinking right>>
            {$catName}: Huh. Well, I guess we can talk a bit more.
<<endif>>
<<declare $day2AskedCatQ1 = false>>
<<declare $day2AskedCatQ2 = false>>
<<declare $day2AskedCatQ3 = false>>
<<declare $day2AskedCatQ4 = false>>
<<declare $day2AskedCatQ5 = false>>
<<declare $knowCatPasttime = false>>
<<declare $knowCatJob = false>>
<<jump CatRecurringTreeDay2>>
===

title: CatRecurringTreeDay2
position: 647,285
---
<<start_voice_blip Cat>>
<<if $catLikesYou > $catDislikesYou>>
    <<show_character Cat Happy right>>
<<else>>
    <<show_character Cat Neutral right>>
<<endif>>
{$catName}: What did you want to talk about?
    -> What is it like out here? <<if $day2AskedCatQ1 == false>>
        <<set $day2AskedCatQ1 = true>>
        <<show_character Cat Neutral right>>
        {$catName}: Oh, I'm not actually usually around here. As I mentioned before, I'm from a planet far, far away from you.
        {$catName}: It's pretty dangerous back at home. Much worse than most other planets.
        <<show_character Cat Sad right>>
        {$catName}: The quakes on our planet are enough to break bones. So we have to climb around a lot to find safe spots away from the ground.
            -> That's terrifying.
                <<positive_indicator Cat>>
                <<set $catLikesYou += 1>>
                <<show_character Cat Sad right>>
                {$catName}: Yeah, it hasn't been the safest it could be too. Kits die all the time, unfortunately.
            -> Oh, those aren't bad. We have those too.
                <<show_character Cat Sad right>>
                <<negative_indicator Cat>>
                <<set $catDislikesYou += 1>>
                {$catName}: Oh? I guess that's possible. But I can say with a pretty good amount of confidence that they're not as bad as ours.
                {$catName}: We have a lot of casualties from them every year. It's pretty unfortunate.
        <<show_character Cat Neutral right>>
        {$catName}: We've had to adapt a lot to survive out here. All of our houses are practically on springs.
            -> How many of there are you?
                <<show_character Cat Thinking right>>
                {$catName}: Quite a few. We have small colonies all over this sector of space.
                {$catName}: Linnaeus is one of the major ones, though.
            -> Springs? Like... boi-oi-oing?
                <<negative_indicator Cat>>
                <<set $catDislikesYou += 1>>
                <<show_character Cat Angry right>>
                {$catName}: ...
                {$catName}: It's not really a silly matter. It's a constant fight for survival.
    -> What is your mission? <<if $day2AskedCatQ2 == false>>
        <<set $day2AskedCatQ2 = true>>
        {$catName}: Mission? I don't think we have... a "mission". We're just out here trying to survive, same as you.
            -> But what is your <i>mission</i>? You know, as an alien?
                <<show_character Cat Angry right>>
                {$catName}: You make it seem like we're some kind of invaders or something.
                <<show_character Cat Neutral right>>
                {$catName}: I'm telling you, we're just trying to get by out here. We're not looking to conquer anyone.
                <<negative_indicator Cat>>
                <<set $catDislikesYou += 1>>
            -> I guess, what do you work as? <<if $knowCatJob == false>>
                <<set $knowCatJob = true>>
                <<positive_indicator Cat>>
                <<set $catLikesYou += 1>>
                <<show_character Cat Happy right>>
                {$catName}: I hear that on your planet, they have people doing <i>super</i> specific tasks. Like working at one specific restaurant for their whole life!
                <<show_character Cat Neutral right>>
                {$catName}: I work for safety, as a whole. I've been working hard to try to keep people on our planet safe.
                {$catName}: A huge reason why I'm out here is because I'm seeking inspiration to make things safer for my people.
            -> Oh, I see.
    -> What do you do for work? <<if $knowCatJob == false>>
        <<set $knowCatJob = true>>
        <<positive_indicator Cat>>
        <<set $catLikesYou += 1>>
        <<show_character Cat Happy right>>
        {$catName}: I hear that on your planet, they have people doing <i>super</i> specific tasks. Like working at one specific restaurant for their whole life!
        <<show_character Cat Neutral right>>
        {$catName}: I work for safety, as a whole. I've been working hard to try to keep people on our planet safe.
        {$catName}: A huge reason why I'm out here is because I'm seeking inspiration to make things safer for my people.
    -> Do you have a family? <<if $day2AskedCatQ5 == false>>
        <<set $day2AskedCatQ5 = true>>
        <<show_character Cat Happy2 right>>
        {$catName}: I do! I'm very close with my family back home.
        {$catName}: I worry about them a lot, especially with how dangerous things are back home.
            -> That must be hard.
                <<positive_indicator Cat>>
                <<set $catLikesYou += 1>>
                <<show_character Cat Sad right>>
                {$catName}: Yeah, it is. But I try to stay strong for them.
            -> Why don't you bring them here?
                <<negative_indicator Cat>>
                <<set $catDislikesYou += 1>>
                <<show_character Cat Angry right>>
                {$catName}: It's not that simple! There are so many complications with intergalactic travel.
                {$catName}: I can't just bring them all here. It would be too dangerous for everyone involved.
        <<if $catLikesYou > $catDislikesYou>>
            <<stop_voice_blip Cat>>
            <<show_character Cat Sad right>>
            You notice that {$catName} seems a bit down while talking about their family.
                -> You seemed a little sad just now. Is everything okay?
                    <<start_voice_blip Cat>>
                    {$catName}: I'm fine, just thinking about them. I...
                    <<positive_indicator Cat>>
                    <<set $catLikesYou += 1>>
                    {$catName}: I actually recently lost a sibling back home. It was really sudden, and I've been having a hard time coping with it.
                    <<show_character Cat Neutral right>>
                    {$catName}: But talking to you has helped take my mind off of things, so thank you for that.
                    {$catName}: Thanks for asking, though. It means a lot to me.
                -> Change the subject.
                    <<jump CatRecurringTreeDay2>>
        <<else>>
            <<stop_voice_blip Cat>>
            <<show_character Cat Sad right>>
            You notice that {$catName} seems off, but you don't know them well enough to tell why.
                -> ...
                    <<jump CatRecurringTreeDay2>>
        <<endif>>
    -> What do you do for fun? <<if $knowCatPasttime == false>>
        <<set $knowCatPasttime = true>>
        {$catName}: Fun? Hm, I guess I like to play games on my monitor when I'm not working. It's a nice way to pass the time.
        <<if $catLikesYou >= $catDislikesYou>>
            <<show_character Cat Happy2 right>>
        <<else>>
            <<show_character Cat Neutral right>>
        <<endif>>
        {$catName}: The usual type of space game. Puzzle games, strategy games, that sort of thing. Nothing too exciting. I could even install one on your device, I think!
    -> You're cute. <<if $day2AskedCatQ4 == false && $catLikesYou >= $catDislikesYou>>
        <<set $day2AskedCatQ4 = true>>
        <<show_character Cat Flirt right>>
        <<positive_indicator Cat>>
        <<set $catLikesYou += 1>>
        {$catName}: Aw, you're too kind. You're pretty cute yourself, {$catThinksPlayerNameIs}.
    -> Do you want to play a game? <<if $day2AskedCatQ3 == false && $knowCatPasttime == true && $catLikesYou >= $catDislikesYou>>
        <<set $day2AskedCatQ3 = true>>
        <<show_character Cat Happy2 right>>
        {$catName}: A game? Sure, why not!
        // TODO: Make this an actual game
        <<stop_voice_blip Cat>>
        <<show_character Cat Happy right>>
        I decide to play a game with {$catName} on the monitor to pass the time.
        {$catName} beams the game to my device, and we both start playing.
        The game is a simple one where I have to match shapes and colors. It's not very exciting, but it helps to break the ice.
        <<show_character Cat Sad right>>
        It seems that {$catName}, however, is struggling quite a bit.
            -> (win at the game)
                <<positive_indicator Cat>>
                <<set $catLikesYou += 1>>
                <<show_character Cat Happy right>>
                <<start_voice_blip Cat>>
                {$catName}: Wow, you're really good at this! Maybe you can teach me sometime.
            -> (purposefully lose the game)
                <<show_character Cat Happy2 right>>
                <<positive_indicator Cat>>
                <<set $catLikesYou += 1>>
                <<start_voice_blip Cat>>
                {$catName}: Hah, I think I won that one! 
                <<show_character Cat Flirt right>>
                {$catName}: Maybe some day I'll teach you a thing or two. But it takes quite a lot of practice to get on my level.
    -> (don't ask any more questions)
        <<stop_voice_blip Cat>>
        <<if $posSelfPerception > $negSelfPerception>>
            I decide to end the conversation for now. It's been a while since I've talked with somebody new, but I feel more confident in myself.
        <<else>>
            I feel my social battery start to drain, so I decide to end the conversation for now.
        <<endif>>
        <<start_voice_blip Cat>>
        <<if $catLikesYou > $catDislikesYou>>
            <<show_character Cat Happy right>>
            {$catName}: Aww, already? I was just starting to have fun!
            {$catName}: But okay, if you need to go, I understand. Just ping me again sometime, okay {$catThinksPlayerNameIs}?
        <<else>>
            <<show_character Cat Neutral right>>
            {$catName}: Oh, alright. That sounds good to me.
            {$catName}: Thanks for the talk, I guess.
        <<endif>>
        <<stop_voice_blip Cat>>
        <<jump EndDay2>>
<<jump CatRecurringTreeDay2>>
===

title: EndDay2
position: 918,14
---
<<hide_all_characters>>
<<hide MonitorSilhouette>>
<<show MonitorOn>>
<<play_music MainTheme>>
I cut the connection and take a step back from my monitor.
    -> (turn off the monitor)
<<hide_all_objects>>
<<show PCRoom>>
Again, after the long conversation, I decide to go to bed.
<<if $posSelfPerception > $negSelfPerception>>
    After talking with these new figures, I've felt a sense of growth. I feel more confident in my ability to handle whatever comes my way.
<<else>>
    After talking with these new figures, I've felt a bit more uncertain about my place in this situation. I hope that I'll be able to figure things out soon.
<<endif>>
<<hide_all_objects>>
<<show PCRoom>>
<<fade_transition 3>>
<<wait 3>>
<<jump StartDay3>>
===