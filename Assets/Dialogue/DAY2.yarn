title: StartDay2
position: 51,-456
---
<<set $daysPassed = 2>>
<<play_sound AlarmClock>>
You wake up to a loud chime on the monitor, your head pounding yet again. 
You don't know what time it is, but you feel rested. Your sleep cycle has been all over the place since you started this job.
Time for another day of work.
<<show_ui ClickToInteract>>
<<show MonitorOffDay2>>
===

title: OnClickedMonitorDay2
position: 55,-311
---
<<hide_all_ui_objects>>
<<show MonitorOn>>
You turn on the monitor to see today's tasks.
Before you start your report, you notice a new message from HQ.
It reads, "Remember your duty to report accurately and without bias. Your observations are crucial for our understanding and eradication of foreign threats."
<<if $catSecured == false && $dogSecured == false && $opossumSecured == false && $birdSecured == false>>
    <<jump ForeverAlone>>
<<endif>>
You ponder your next report. Do you report the encounter with the aliens, knowing that Earth may react harshly? Or keep it under wraps?
    -> (report the aliens)
        You hesitate for a moment. HQ has been known to treat the unfamiliar as hostile, and they may get the wrong message.
        You're not entirely sure what will happen to the aliens, but you may never see them again.
        -> (report the aliens)
            You get to writing your report.
            <<hide_all_objects>>
            <<show PCRoom>>
            <<show_ui TypeToInteract>>
            <<show MonitorReportDay2>>
        -> (report that you saw nothing)
            <<hide_all_objects>>
            <<show PCRoom>>
            You get to writing your report.
            <<show_ui TypeToInteract>>
            <<show MonitorDontReportDay2>>
    -> (report that you saw nothing)
        <<hide_all_objects>>
        <<show PCRoom>>
        You get to writing your report.
        <<show_ui TypeToInteract>>
        <<show MonitorDontReportDay2>>
===

title: KeptUnderWrapsDay2
position: 167,29
---
<<hide_all_ui_objects>>
<<set $negSelfPerception += 1>>
You hesitate, and send off the message. You feel <color=\#FF8888>a little guilty</color>. Your job was to report alien life... but something about this situation feels different.
<<if $posSelfPerception > $negSelfPerception>>
    You feel a sense of pride in your decision. You trust your instincts and believe that not everything needs to be reported.
<<else>>
    You feel a pang of guilt. However, you rationalize that some things are better left unsaid.
<<endif>>
You turn the monitor off and take a seat.
<<fade_transition 2>>
<<wait 1>>
<<hide_all_objects>>
<<show Stars>>
<<show Sitting>>
Lost in another one of your spirals, you think about your family back on Earth.
Your family was only ever close with you before you dropped out of college. 
You were surprised that they disagreed with your decision to leave Earth, even though you never quite felt like they loved you anymore.
Recalling your parents' disapproval makes you feel...
    -> (sad, because they're gone now)
        <<set $negSelfPerception += 1>>
        You feel <color=\#FF8888>a wave of sadness wash over you</color>. You wonder if they would have ever accepted your choices, but now you'll never know.
    -> (angry, because they never supported you)
        <<set $negSelfPerception += 1>>
        You feel a surge of anger. They never supported your decisions, and now that they're gone, you feel <color=\#FF8888>a sense of bitterness</color>.
    -> (conflicted, because they didn't want you gone)
        You feel conflicted. They didn't want you to leave, but they never made an effort to understand your reasons.
        <<set $posSelfPerception += 1>>
        You wish they could see you now, living your life on your own terms. You feel <color=\#88FF88>independent</color>.
And what about your friends? You always felt like such an impostor around them. You felt like every interaction was a performance.
You forced yourself to do so many things with them. And you only ever felt like you were "fun" when you were a couple of drinks in. You wonder if they ever really liked you for who you were.
Thinking about these people in your life make you feel...
    -> (lonely, because you pushed them away)
        <<set $negSelfPerception += 1>>
        You realize that you pushed people away, and now you're <color=\#FF8888>left with nothing but your own thoughts</color>.
    -> (regretful, because you never opened up to them)
        <<set $negSelfPerception += 1>>
        You <color=\#FF8888>wish you had opened up to people more</color>, but now it's too late.
    -> (indifferent, because they weren't true friends)
        Maybe those people weren't true friends after all. You wonder if you'll be able to make genuine connections in the future.
        <<set $posSelfPerception += 1>>
        The idea of making real connections makes you <color=\#88FF88>feel a little hopeful</color>.

You then remember what happened yesterday, with the aliens.
Supposedly, you can ping them again to have a chat. Maybe you should give it a try.
    -> (reestablish contact with the aliens)
        <<hide_all_objects>>
        <<show PCRoom>>
        <<show_ui ClickToInteract>>
        <<show ChooseYourCharacterDay2>>
===

title: CallCatDay2
position: 422,282
---
<<hide_all_objects>>
<<hide_all_ui_objects>>
<<show PCRoom>>
<<show MonitorOn>>
<<show_character Cat Obscure right>>
<<stop_voice_blip Cat>>
You call {$catName}. The line remains static for a little while, but you suddenly see movement on the other end.
<<play_music CatTheme 0.5>>
<<if $catLikesYou > $catDislikesYou>>
    <<show_character Cat Happy right>>
    {$catName} appears on the screen, looking relieved to see you.
    <<show_character Cat Happy2 right>>
    <<start_voice_blip Cat>>
    {$catName}: Hey {$catThinksPlayerNameIs}, I was just thinking about you! Do you need something?
        -> I want to get to know you better.
            <<positive_indicator Cat>>
            <<set $catLikesYou += 1>>
            <<show_character Cat Happy right>>
            {$catName}: I'd like that. It's nice to have someone to talk to out here.
<<else>>
    <<show_character Cat Neutral right>>
    {$catName} appears on the screen.
    <<start_voice_blip Cat>>
    {$catName}: Oh, it's you! Hey again, {$catThinksPlayerNameIs}. What's up?
        -> I want to get to know you better.
            <<show_character Cat Thinking right>>
            {$catName}: Huh. Well, I guess we can talk a bit more.
<<endif>>
<<declare $day2AskedCatQ1 = false>>
<<declare $day2AskedCatQ2 = false>>
<<declare $day2AskedCatQ3 = false>>
<<declare $day2AskedCatQ4 = false>>
<<declare $day2AskedCatQ5 = false>>
<<declare $knowCatPasttime = false>>
<<declare $knowCatJob = false>>
<<jump CatRecurringTreeDay2>>
===

title: CatRecurringTreeDay2
---
<<start_voice_blip Cat>>
<<if $catLikesYou > $catDislikesYou>>
    <<show_character Cat Happy right>>
<<else>>
    <<show_character Cat Neutral right>>
<<endif>>
{$catName}: What did you want to talk about?
    -> What is it like out here? <<if $day2AskedCatQ1 == false>>
        <<set $day2AskedCatQ1 = true>>
        <<show_character Cat Neutral right>>
        {$catName}: Oh, I'm not actually usually around here. As I mentioned before, I'm from a planet far, far away from you.
        {$catName}: It's pretty dangerous back at home. Much worse than most other planets.
        <<show_character Cat Sad right>>
        {$catName}: The quakes on our planet are enough to break bones. So we have to climb around a lot to find safe spots away from the ground.
            -> That's terrifying.
                <<positive_indicator Cat>>
                <<set $catLikesYou += 1>>
                <<show_character Cat Sad right>>
                {$catName}: Yeah, it hasn't been the safest it could be too. Kits die all the time, unfortunately.
            -> Oh, those aren't bad. We have those too.
                <<show_character Cat Sad right>>
                <<set $catDislikesYou += 1>>
                {$catName}: Oh? I guess that's possible. But I can say with a pretty good amount of confidence that they're not as bad as ours.
                {$catName}: We have a lot of casualties from them every year. It's pretty unfortunate.
        <<show_character Cat Neutral right>>
        {$catName}: We've had to adapt a lot to survive out here. All of our houses are practically on springs.
            -> How many of there are you?
                <<show_character Cat Thinking right>>
                {$catName}: Quite a few. We have small colonies all over this sector of space.
                {$catName}: Linnaeus is one of the major ones, though.
            -> Springs? Like... boi-oi-oing?
                <<set $catDislikesYou += 1>>
                <<show_character Cat Angry right>>
                {$catName}: ...
                {$catName}: It's not really a silly matter. It's a constant fight for survival.
    -> What is your mission? <<if $day2AskedCatQ2 == false>>
        <<set $day2AskedCatQ2 = true>>
        {$catName}: Mission? I don't think we have... a "mission". We're just out here trying to survive, same as you.
            -> But what is your <i>mission</i>? You know, as an alien?
                <<show_character Cat Angry right>>
                {$catName}: You make it seem like we're some kind of invaders or something.
                <<show_character Cat Neutral right>>
                {$catName}: I'm telling you, we're just trying to get by out here. We're not looking to conquer anyone.
                <<set $catDislikesYou += 1>>
            -> I guess, what do you work as? <<if $knowCatJob == false>>
                <<set $knowCatJob = true>>
                <<positive_indicator Cat>>
                <<set $catLikesYou += 1>>
                <<show_character Cat Happy right>>
                {$catName}: I hear that on your planet, they have people doing <i>super</i> specific tasks. Like working at one specific restaurant for their whole life!
                <<show_character Cat Neutral right>>
                {$catName}: I work for safety, as a whole. I've been working hard to try to keep people on our planet safe.
                {$catName}: A huge reason why I'm out here is because I'm seeking inspiration to make things safer for my people.
            -> Oh, I see.
    -> What do you do for work? <<if $knowCatJob == false>>
        <<set $knowCatJob = true>>
        <<positive_indicator Cat>>
        <<set $catLikesYou += 1>>
        <<show_character Cat Happy right>>
        {$catName}: I hear that on your planet, they have people doing <i>super</i> specific tasks. Like working at one specific restaurant for their whole life!
        <<show_character Cat Neutral right>>
        {$catName}: I work for safety, as a whole. I've been working hard to try to keep people on our planet safe.
        {$catName}: A huge reason why I'm out here is because I'm seeking inspiration to make things safer for my people.
    -> Do you have a family? <<if $day2AskedCatQ5 == false>>
        <<set $day2AskedCatQ5 = true>>
        <<show_character Cat Happy2 right>>
        {$catName}: I do! I'm very close with my family back home.
        {$catName}: I worry about them a lot, especially with how dangerous things are back home.
            -> That must be hard.
                <<positive_indicator Cat>>
                <<set $catLikesYou += 1>>
                <<show_character Cat Sad right>>
                {$catName}: Yeah, it is. But I try to stay strong for them.
            -> Why don't you bring them here?
                <<set $catDislikesYou += 1>>
                <<show_character Cat Angry right>>
                {$catName}: It's not that simple! There are so many complications with intergalactic travel.
                {$catName}: I can't just bring them all here. It would be too dangerous for everyone involved.
        <<if $catLikesYou > $catDislikesYou>>
            <<stop_voice_blip Cat>>
            <<show_character Cat Sad right>>
            You notice that {$catName} seems a bit down while talking about their family.
                -> You seemed a little sad just now. Is everything okay?
                    <<start_voice_blip Cat>>
                    {$catName}: I'm fine, just thinking about them. I...
                    <<positive_indicator Cat>>
                    <<set $catLikesYou += 1>>
                    {$catName}: I actually recently lost a sibling back home. It was really sudden, and I've been having a hard time coping with it.
                    <<show_character Cat Neutral right>>
                    {$catName}: But talking to you has helped take my mind off of things, so thank you for that.
                    {$catName}: Thanks for asking, though. It means a lot to me.
                -> Change the subject.
                    <<jump CatRecurringTreeDay2>>
        <<else>>
            <<stop_voice_blip Cat>>
            <<show_character Cat Sad right>>
            You notice that {$catName} seems off, but you don't know them well enough to tell why.
                -> ...
                    <<jump CatRecurringTreeDay2>>
        <<endif>>
    -> What do you do for fun? <<if $knowCatPasttime == false>>
        <<set $knowCatPasttime = true>>
        {$catName}: Fun? Hm, I guess I like to play games on my monitor when I'm not working. It's a nice way to pass the time.
        <<if $catLikesYou >= $catDislikesYou>>
            <<show_character Cat Happy2 right>>
        <<else>>
            <<show_character Cat Neutral right>>
        <<endif>>
        {$catName}: The usual type of space game. Puzzle games, strategy games, that sort of thing. Nothing too exciting. I could even install one on your device, I think!
    -> You're cute. <<if $day2AskedCatQ4 == false && $catLikesYou >= $catDislikesYou>>
        <<set $day2AskedCatQ4 = true>>
        <<show_character Cat Flirt right>>
        <<positive_indicator Cat>>
        <<set $catLikesYou += 1>>
        {$catName}: Aw, you're too kind. You're pretty cute yourself, {$catThinksPlayerNameIs}.
    -> Do you want to play a game? <<if $day2AskedCatQ3 == false && $knowCatPasttime == true && $catLikesYou >= $catDislikesYou>>
        <<set $day2AskedCatQ3 = true>>
        <<show_character Cat Happy2 right>>
        {$catName}: A game? Sure, why not!
        // TODO: Make this an actual game
        <<stop_voice_blip Cat>>
        <<show_character Cat Happy right>>
        You decide to play a game with {$catName} on the monitor to pass the time.
        {$catName} beams the game to your device, and you both start playing.
        The game is a simple one where you have to match shapes and colors. It's not very exciting, but it helps to break the ice.
        <<show_character Cat Sad right>>
        It seems that {$catName}, however, is struggling quite a bit.
            -> (win at the game)
                <<positive_indicator Cat>>
                <<set $catLikesYou += 1>>
                <<show_character Cat Happy right>>
                <<start_voice_blip Cat>>
                {$catName}: Wow, you're really good at this! Maybe you can teach me sometime.
            -> (purposefully lose the game)
                <<show_character Cat Happy2 right>>
                <<positive_indicator Cat>>
                <<set $catLikesYou += 1>>
                <<start_voice_blip Cat>>
                {$catName}: Hah, I think I won that one! 
                <<show_character Cat Flirt right>>
                {$catName}: Maybe some day I'll teach you a thing or two. But it takes quite a lot of practice to get on my level.
    -> (don't ask any more questions)
        <<stop_voice_blip Cat>>
        <<if $posSelfPerception > $negSelfPerception>>
            You decide to end the conversation for now. It's been a while since you've talked with somebody new, but you feel more confident in yourself.
        <<else>>
            You feel your social battery start to drain, so you decide to end the conversation for now.
        <<endif>>
        <<start_voice_blip Cat>>
        <<if $catLikesYou > $catDislikesYou>>
            <<show_character Cat Happy right>>
            {$catName}: Aww, already? I was just starting to have fun!
            {$catName}: But okay, if you need to go, I understand. Just ping me again sometime, okay {$catThinksPlayerNameIs}?
        <<else>>
            <<show_character Cat Neutral right>>
            {$catName}: Oh, alright. That sounds good to me.
            {$catName}: Thanks for the talk, I guess.
        <<endif>>
        <<stop_voice_blip Cat>>
        <<jump EndDay2>>
<<jump CatRecurringTreeDay2>>
===

title: CallDogDay2
position: 408,123
---
<<hide_all_objects>>
<<hide_all_ui_objects>>
<<show PCRoom>>
<<show MonitorOn>>
<<show_character Dog Obscure right>>
<<stop_voice_blip Dog>>
You call {$dogName}. The line remains static for a little while, but you suddenly see movement on the other end.
<<play_music DogTheme 0.5>>
<<if $dogLikesYou > $dogDislikesYou>>
    <<show_character Dog Happy right>>
    {$dogName} appears on the screen, looking chill as always.
    <<show_character Dog Happy2 right>>
    <<start_voice_blip Dog>>
    {$dogName}: Hey {$dogThinksPlayerNameIs}! Woof. How's it going?
        -> I want to get to know you better.
            <<positive_indicator Dog>>
            <<set $dogLikesYou += 1>>
            <<show_character Dog Happy right>>
            {$dogName}: Sure! Woof. I like talking.
<<else>>
    <<show_character Dog Neutral right>>
    {$dogName} appears on the screen.
    <<start_voice_blip Dog>>
    {$dogName}: Oh. Woof. Hey again, {$dogThinksPlayerNameIs}. What's up?
        -> I want to get to know you better.
            <<show_character Dog Thinking right>>
            {$dogName}: Woof. Well, I guess we can talk a bit more.
<<endif>>
<<declare $day2AskedDogQ1 = false>>
<<declare $day2AskedDogQ2 = false>>
<<declare $day2AskedDogQ3 = false>>
<<declare $day2AskedDogQ4 = false>>
<<declare $day2AskedDogQ5 = false>>
<<declare $knowDogJob = false>>
<<declare $knowDogSecret = false>>
<<jump DogRecurringTreeDay2>>
===

title: DogRecurringTreeDay2
---
<<start_voice_blip Dog>>
<<if $dogLikesYou > $dogDislikesYou>>
    <<show_character Dog Happy right>>
<<else>>
    <<show_character Dog Neutral right>>
<<endif>>
{$dogName}: What did you want to talk about?
    -> What is it like out here? <<if $day2AskedDogQ1 == false>>
        <<set $day2AskedDogQ1 = true>>
        <<show_character Dog Happy right>>
        {$dogName}: Oh, so peaceful! Many friends. Woof. Many not-friends too, but enough friends to keep me company. Woof.
        <<show_character Dog Angry right>>
        {$dogName}: I'm happy that it's not like my home planet. Woof. It's too much sometimes. Woof.
            -> What's your home planet like?
                <<show_character Dog Sad right>>
                {$dogName}: Woof. It's very... strict. Woof. Barklon watches everything all the time. Woof.
            -> What's wrong with it?
                <<show_character Dog Sad right>>
                {$dogName}: Woof. It's very... strict. Woof. Barklon watches everything all the time. Woof.
            -> But you must love your planet. You're from it!
                <<set $dogDislikesYou += 1>>
                <<show_character Dog Angry right>>
                {$dogName}: Woof. I don't know about that. Woof. I just want to be where I feel safe. Woof.
                {$dogName}: Barklon is very... controlling. Woof. Everything is being watched all the time. Woof.
        <<show_character Dog Surprised right>>
        <<if $knowDogSecret == false>>
            {$dogName}: Actually, can you keep a secret? Woof.
                -> Go ahead. <<if $posSelfPerception >= $negSelfPerception>>
                    <<set $knowDogSecret = true>>
                    <<positive_indicator Dog>>
                    <<set $dogLikesYou += 1>>
                    <<show_character Dog Neutral right>>
                    {$dogName}: So, {$dogThinksPlayerNameIs}. Woof. I don't usually tell anyone this, but...
                    <<show_character Dog Angry right>>
                    {$dogName}: When I was still at Barklon, I noticed a group of young pups. Woof. Bullied, by a big shot. Big woofer.
                    <<show_character Dog Angry right>>
                    {$dogName}: They were in trouble. Woof. I couldn't just leave them there. Woof.
                    <<show_character Dog Happy right>>
                    {$dogName}: I nabbed them and brought them to a safe place. I felt so proud. Woof. But when I told my family...
                    <<show_character Dog Sad right>>
                    {$dogName}: They didn't like it. Woof. They thought I was being disloyal. Woof.
                    {$dogName}: I noticed that mom and dad were scared. Woof. They thought the big shots would come after us. Woof.
                    <<show_character Dog Angry right>>
                    {$dogName}: Then I noticed that they were being patrolled more often. Woof. They think they can do anything. Woof.
                    {$dogName}: Family was in danger if I stayed. Woof. So I left. Forever.
                        -> That's brave of you.
                            <<positive_indicator Dog>>
                            <<set $dogLikesYou += 1>>
                            <<show_character Dog Neutral right>>
                            {$dogName}: Woof... Thank you, {$dogThinksPlayerNameIs}.
                            <<if $dogLikesYou >= $dogDislikesYou>>
                                <<show_character Dog Flirt right>>
                                {$dogName}: I can tell you're one of the good ones, {$dogThinksPlayerNameIs}. Woof.
                            <<endif>>
                        -> That sounds impulsive.
                            <<set $dogDislikesYou += 1>>
                            <<show_character Dog Sad right>>
                            {$dogName}: Woof...
                        {$dogName}: But I don't regret it. Woof. I do wish I could see my parents again, though. Woof.
                -> I'm no good at keeping secrets. <<if $posSelfPerception < $negSelfPerception>>
                    <<show_character Dog Neutral right>>
                    {$dogName}: Oh, that's okay. Nevermind, then. Woof.
                -> ...
                    <<show_character Dog Neutral right>>
                    {$dogName}: ...Nevermind, then. Woof.
        <<endif>>
    -> What is your mission? <<if $day2AskedDogQ2 == false>>
        <<set $day2AskedDogQ2 = true>>
        {$dogName}: Mission? Woof. I want to wander and find a better place. Anywhere better than Barklon. Woof.
            -> But what is your <i>mission</i>? You know, as an alien?
                <<show_character Dog Sad right>>
                {$dogName}: Woof? I don't understand.
                <<set $dogDislikesYou += 1>>
                <<show_character Dog Neutral right>>
                {$dogName}: I just want to adventure. Woof. I've never felt as free as I do out here. Woof.
            -> Do you have a job? <<if $knowDogJob == false>>
                <<set $knowDogJob = true>>
                <<positive_indicator Dog>>
                <<set $dogLikesYou += 1>>
                <<show_character Dog Happy right>>
                {$dogName}: I worked in a fabric store. Woof. Made a lot of stuff. Woof.
                {$dogName}: Neck rags, paw covers, those sorts of things. Woof.
                <<show_character Dog Neutral right>>
                {$dogName}: Nothing I'm too proud of, but it paid the bills on Barklon. Woof.
            -> Oh, I see.
    -> What do you do for work? <<if $knowDogJob == false>>
        <<set $knowDogJob = true>>
        <<positive_indicator Dog>>
        <<set $dogLikesYou += 1>>
        <<show_character Dog Happy right>>
        {$dogName}: I worked with my family in the fabric store. Woof. We made a lot of stuff. Woof.
        {$dogName}: Neck rags, paw covers, those sorts of things. Woof.
        <<show_character Dog Neutral right>>
        {$dogName}: Nothing I'm too proud of, but it paid the bills on Barklon. Woof.
    -> What do you do for fun? <<if $day2AskedDogQ3 == false>>
        <<set $day2AskedDogQ3 = true>>
        {$dogName}: Fun? Woof. I like to run around and explore new places. Woof.
        {$dogName}: Back on Barklon, I never got to do that. Woof. Always had to stay inside and work. Woof.
    -> Do you have a family? <<if $day2AskedDogQ4 == false>>
        <<set $day2AskedDogQ4 = true>>
        <<show_character Dog Surprised right>>
        {$dogName}: Woof! Yeah, I do! Woof. I'm pretty close with them, even though I'm far away now. Woof.
        {$dogName}: They live a hard life. Woof. They make thingies with rags. Woof.
            -> That must be hard.
                <<positive_indicator Dog>>
                <<set $dogLikesYou += 1>>
                <<show_character Dog Sad right>>
                {$dogName}: It is. But there's not a big chance I'll see them again. Woof.
                <<show_character Dog Angry right>>
                {$dogName}: Maybe someday, I'll be able to help them out. Woof. Get rid of the bigger people. Woof.
            -> Why don't you bring them here?
                <<set $dogDislikesYou += 1>>
                <<show_character Dog Sad right>>
                {$dogName}: I asked. Woof. They said no. Woof.
                {$dogName}: They've always told me to keep my head down. Woof. Not make waves. Woof.
                <<show_character Dog Thinking right>>
                {$dogName}: I'll never understand why they think that way. Woof.
        <<if $knowDogSecret == false>>
            {$dogName}: Actually, can you keep a secret? Woof.
                -> Go ahead. <<if $posSelfPerception >= $negSelfPerception>>
                    <<positive_indicator Dog>>
                    <<set $dogLikesYou += 1>>
                    <<show_character Dog Neutral right>>
                    {$dogName}: So, {$dogThinksPlayerNameIs}. Woof. I don't usually tell anyone this, but...
                    <<show_character Dog Angry right>>
                    {$dogName}: When I was still at Barklon, I noticed a group of young pups. Woof. Bullied, by a big shot. Big woofer.
                    <<show_character Dog Sad right>>
                    {$dogName}: They were in trouble. Woof. I couldn't just leave them there. Woof.
                    <<show_character Dog Happy right>>
                    {$dogName}: I nabbed them and brought them to a safe place. I felt so proud. Woof. But when I told my family...
                    <<show_character Dog Sad right>>
                    {$dogName}: They didn't like it. Woof. They thought I was being disloyal. Woof.
                    {$dogName}: I noticed that mom and dad were scared. Woof. They thought the big shots would come after us. Woof.
                    <<show_character Dog Angry right>>
                    {$dogName}: Then I noticed that they were being patrolled more often. Woof. They think they can do anything. Woof.
                    {$dogName}: Family was in danger if I stayed. Woof. So I left. Forever.
                        -> That's brave of you.
                            <<positive_indicator Dog>>
                            <<set $dogLikesYou += 1>>
                            <<show_character Dog Neutral right>>
                            {$dogName}: Woof... Thanks.
                            <<if $dogLikesYou >= $dogDislikesYou>>
                                <<show_character Dog Flirt right>>
                                {$dogName}: I can tell you're one of the good ones, {$dogThinksPlayerNameIs}. Woof.
                            <<endif>>
                        -> That sounds impulsive.
                            <<set $dogDislikesYou += 1>>
                            <<show_character Dog Sad right>>
                            {$dogName}: Woof...
                        {$dogName}: But I don't regret it. Woof. I do wish I could see my parents again, though. Woof.
                -> I'm no good at keeping secrets. <<if $posSelfPerception < $negSelfPerception>>
                    <<show_character Dog Neutral right>>
                    {$dogName}: Oh, that's okay. Nevermind, then. Woof.
                -> ...
                    <<show_character Dog Neutral right>>
                    {$dogName}: ...Nevermind, then. Woof.
        <<endif>>
    -> You're cute. <<if $day2AskedDogQ5 == false && $dogLikesYou >= $dogDislikesYou>>
        <<set $day2AskedDogQ5 = true>>
        <<show_character Dog Happy right>>
        <<positive_indicator Dog>>
        <<set $dogLikesYou += 1>>
        {$dogName}: Aw, woof. That's kind of you, {$dogThinksPlayerNameIs}. Woof.
        <<show_character Dog Flirt right>>
        {$dogName}: You're not so bad yourself. Woof.
    -> (don't ask any more questions)
        <<stop_voice_blip Dog>>
        <<if $posSelfPerception > $negSelfPerception>>
            You decide to end the conversation for now. It's been a while since you've talked with somebody new, but you feel more confident in yourself.
        <<else>>
            You feel your social battery start to drain, so you decide to end the conversation for now.
        <<endif>>
        <<start_voice_blip Dog>>
        <<if $dogLikesYou > $dogDislikesYou>>
            <<show_character Dog Happy right>>
            {$dogName}: Aw, okay. Woof. It was really fun talking! Woof.
            {$dogName}: We should talk more, {$dogThinksPlayerNameIs}. Woof.
        <<else>>
            <<show_character Dog Neutral right>>
            {$dogName}: Oh, alright. Woof. Sounds good.
            {$dogName}: Thanks for talking. Woof.
        <<endif>>
        <<stop_voice_blip Dog>>
        <<jump EndDay2>>
<<jump DogRecurringTreeDay2>>
===

title: CallOpossumDay2
position: 409,-43
---
<<hide_all_objects>>
<<hide_all_ui_objects>>
<<show PCRoom>>
<<show MonitorOn>>
<<show_character Opossum Obscure right>>
<<stop_voice_blip Opossum>>
You call {$opossumName}. The line remains static for a little while, but you suddenly see movement on the other end.
<<play_music_start_loop OpossumThemeStart OpossumThemeLoop 0.4 0.5>>
<<if $opossumLikesYou > $opossumDislikesYou>>
    <<show_character Opossum Happy right>>
    {$opossumName} appears on the screen, looking strangely happy.
    <<start_voice_blip Opossum>>
    {$opossumName}: Hey {$opossumThinksPlayerNameIs}, how's it going? It's been a while...
        -> I want to get to know you better.
            <<positive_indicator Opossum>>
            <<set $opossumLikesYou += 1>>
            <<show_character Opossum Flirt right>>
            {$opossumName}: Sure, let's do that.
<<else>>
    <<show_character Opossum Neutral right>>
    {$opossumName} appears on the screen.
    <<start_voice_blip Opossum>>
    {$opossumName}: Oh, hey. {$opossumThinksPlayerNameIs}, right? What's on your mind?
        -> I want to get to know you better.
            <<show_character Opossum Thinking right>>
            {$opossumName}: Oh, sure. I wouldn't mind giving that a shot.
<<endif>>
<<jump OpossumRecurringTreeDay2>>
<<declare $day2AskedOpossumQ1 = false>>
<<declare $day2AskedOpossumQ2 = false>>
<<declare $day2AskedOpossumQ3 = false>>
<<declare $day2AskedOpossumQ4 = false>>
<<declare $day2AskedOpossumQ5 = false>>
<<declare $day2AskedOpossumQ6 = false>>
<<declare $knowOpossumBackstory = false>>
===

title: OpossumRecurringTreeDay2
---
<<start_voice_blip Opossum>>
<<show_character Opossum Happy right>>
{$opossumName}: What did you want to talk about?
    -> What is it like out here? <<if $day2AskedOpossumQ1 == false>>
        <<set $day2AskedOpossumQ1 = true>>
        <<show_character Opossum Confused right>>
        {$opossumName}: Honestly, pretty miserable. I didn't choose to leave home.
        {$opossumName}: But here I am, stuck on some fucking rock in space.
            -> Why did you leave home?
                <<show_character Opossum Sad right>>
                {$opossumName}: Yeah... I made some bad choices.
                <<show_character Opossum Neutral right>>
                {$opossumName}: Basically, I didn't have parents that cared about me. Abandoned at birth.
                {$opossumName}: I lived on the streets for a while, trying to survive however I could. 
                <<show_character Opossum Angry right>>
                {$opossumName}: I learned that there were a lot of bad people out in the world, and to survive, I had to be one of them.
                <<show_character Opossum Sad right>>
                {$opossumName}: I... got in with the wrong crowd. Did some things I'm not proud of.
                -> Sorry to hear that.
                    <<positive_indicator Opossum>>
                    <<set $opossumLikesYou += 1>>
                    <<show_character Opossum Sad right>>
                    {$opossumName}: Thanks. It still hurts a lot.
                    <<show_character Opossum Happy right>>
                    {$opossumName}: But I'm trying to turn my life around. Being out here is a fresh start.
                -> What did you do? <<if $opossumLikesYou > $opossumDislikesYou && $knowOpossumBackstory == false>>
                    <<show_character Opossum Angry right>>
                    {$opossumName}: I... I was pressured to kill a child.
                    <<show_character Opossum Sad right>>
                    {$opossumName}: It's something I regret every day. The amount of times I've apologized in my head aren't enough to make up for it.
                    {$opossumName}: It could've been me, you know? I could've been one of the unlucky ones. But somehow, I figured out how to survive on my own.
                    -> You're... terrible.
                        <<set $opossumDislikesYou += 3>>
                        <<show_character Opossum Sad right>>
                        {$opossumName}: Yeah... I know. 
                        {$opossumName}: I hate myself for it too.
                    -> I'm sorry you had to go through that.
                        <<positive_indicator Opossum>>
                        <<set $opossumLikesYou += 1>>
                        <<show_character Opossum Happy right>>
                        {$opossumName}: Thanks. It doesn't make up for what I did, but it means a lot to hear that.
                -> ...
                <<show_character Opossum Confused right>>
                {$opossumName}: ...
                {$opossumName}: Sorry, I can get a bit emotional.
                    -> No, you're okay.
                        <<positive_indicator Opossum>>
                        <<set $opossumLikesYou += 1>>
                        {$opossumName}: ...Thanks, I guess.
                    -> No, I get it. I'm the same way. <<if $posSelfPerception < $negSelfPerception>>
                        <<set $opossumDislikesYou += 2>>
                        <<show_character Opossum Happy right>>
                        {$opossumName}: Heh, yeah. I guess we all have our issues.
                    -> ...
                        <<set $opossumDislikesYou += 1>>
    -> What is your mission? <<if $day2AskedOpossumQ2 == false>>
        <<set $day2AskedOpossumQ2 = true>>
        {$opossumName}: ...Mission? Pfft, that's a funny way to put it.
        <<show_character Opossum Neutral right>>
        {$opossumName}: I don't really know what my mission is, I guess. That's what I'm trying to figure out.
        <<show_character Opossum Happy right>>
        {$opossumName}: Is it cheesy to say my mission is to find a mission?
            -> No! That's so brave.
                <<set $opossumDislikesYou += 1>>
                <<show_character Opossum Neutral right>>
                {$opossumName}: Hah, I don't know about brave... that's a little much.
            -> Maybe a little bit.
                <<set $opossumLikesYou += 1>>
                <<show_character Opossum Proud right>>
                {$opossumName}: Pfft, yeah. You're right to clock me on that one.
            -> Maybe we can find your mission, together. <<if $opossumLikesYou > $opossumDislikesYou>>
                <<positive_indicator Opossum>>
                <<set $opossumLikesYou += 3>>
                <<show_character Opossum Flirt right>>
                {$opossumName}: That sounds... really nice, actually.
    -> What do you do for work? <<if $day2AskedOpossumQ3 == false>>
        <<set $day2AskedOpossumQ3 = true>>
        <<show_character Opossum Happy right>>
        {$opossumName}: Work? Hah, I don't really have a job right now.
        <<show_character Opossum Thinking right>>
        {$opossumName}: Honestly, I don't think I'll be looking into that any time soon. Do you have a job?
            -> Yeah, I report aliens.
                <<show_character Opossum Happy right>>
                <<positive_indicator Opossum>>
                <<set $opossumLikesYou += 1>>
                {$opossumName}: Wait, really?? That's pretty intense.
                {$opossumName}: I'd take a job like that any day, especially over any other dead-end job.
            -> (lie) No, I don't have a job.
                <<show_character Opossum Happy right>>
                {$opossumName}: Yeah, jobs can be a pain.
    -> What do you do for fun? <<if $day2AskedOpossumQ4 == false>>
        <<set $day2AskedOpossumQ4 = true>>
        <<show_character Opossum Happy right>>
        {$opossumName}: Believe it or not, I'm a huge artist. Painting, drawing, sculpting...
        {$opossumName}: Heck, I even tried my paw at music once or twice. But that never really took off.
            -> What do you usually draw?
            -> Can you draw me?
                <<show_character Opossum Confused right>>
                <<set $opossumDislikesYou += 1>>
                {$opossumName}: Uh, I don't really draw others. Sorry.
            -> What? You? Art? No way.
                <<show_character Opossum Happy right>>
                <<positive_indicator Opossum>>
                <<set $opossumLikesYou += 1>>
                {$opossumName}: Yes way! I know it's not what people expect from me.
                {$opossumName}: But art is a great way to express myself. Helps me deal with... everything.
        <<show_character Opossum Thinking right>>
        {$opossumName}: Been drawing a lot of things I found in space. Random metal bits, space plants, that sort of thing.
        <<show_character Opossum Proud right>>
        {$opossumName}: Here! Take a look at this.
        <<stop_voice_blip Opossum>>
        {$opossumName} sends you a picture of their art. It shows a beautiful depiction of a small space plant growing out of a rock, with stars twinkling in the background.
            -> Wow, that's really good!
                <<positive_indicator Opossum>>
                <<set $opossumLikesYou += 1>>
                <<start_voice_blip Opossum>>
                <<show_character Opossum Flirt right>>
                {$opossumName}: Thanks! I really appreciate that.
            -> It's... okay, I guess.
                <<start_voice_blip Opossum>>
                <<set $opossumDislikesYou += 1>>
                <<show_character Opossum Angry right>>
                {$opossumName}: Hey, screw you. I'd like to see you do better.
    -> Do you have a family? <<if $knowOpossumBackstory == false && $day2AskedOpossumQ5 == false>>
        <<set $day2AskedOpossumQ5 = true>>
        <<if $knowOpossumBackstory == false>>
            <<show_character Opossum Sad right>>
            {$opossumName}: No, actually. I didn't have parents that cared about me. Abandoned at birth.
        <<else>>
            <<show_character Opossum Neutral right>>
            {$opossumName}: Like I told you before, I didn't have parents that cared about me. Abandoned at birth.
        <<endif>>
        {$opossumName}: Sometimes I wonder what it's like to have someone care about you like that.
            -> Yeah. I'm in the same boat.
                <<positive_indicator Opossum>>
                <<set $opossumLikesYou += 1>>
                <<show_character Opossum Sad right>>
                {$opossumName}: Oh? Sorry to hear about that, {$opossumThinksPlayerNameIs}. 
                <<show_character Opossum Happy right>>
                {$opossumName}: This may be a little selfish but, I'm glad to be with someone that understands that.
            -> That sucks.
                <<show_character Opossum Confused right>>
                <<set $opossumDislikesYou += 1>>
                {$opossumName}: Yeah, yeah. I know it does, I don't need your pity.
                {$opossumName}: I've been moving past it. I want to be my own being now.
            -> I'm here for you.
                <<show_character Opossum Flirt right>>
                {$opossumName}: ...
                {$opossumName}: Cheesy. But thanks, I guess.
    -> You're cute. <<if $day2AskedOpossumQ6 == false && $opossumLikesYou >= $opossumDislikesYou>>
        <<set $day2AskedOpossumQ6 = true>>
        <<show_character Opossum Confused right>>
        {$opossumName}: ...Thanks? I don't really think I'm going for "cute", though.
        <<show_character Opossum Flirt right>>
        <<if $opossumLikesYou >= $opossumDislikesYou>>
            <<positive_indicator Opossum>>
            <<set $opossumLikesYou += 1>>
            {$opossumName}: But, you are pretty cool, I have to admit.
        <<endif>>
    -> (don't ask any more questions)
        <<stop_voice_blip Opossum>>
        <<if $posSelfPerception > $negSelfPerception>>
            You decide to end the conversation for now. It's been a while since you've talked with somebody new, but you feel more confident in yourself.
        <<else>>
            You feel your social battery start to drain, so you decide to end the conversation for now.
        <<endif>>
        <<start_voice_blip Opossum>>
        <<if $opossumLikesYou > $opossumDislikesYou>>
            <<show_character Opossum Happy right>>
            {$opossumName}: Thanks for the talk. I appreciated it.
            <<show_character Opossum Flirt right>>
            {$opossumName}: As much as I'm a little embarrassed to say it, I enjoyed your company. We should talk more, {$opossumThinksPlayerNameIs}.
        <<else>>
            <<show_character Opossum Neutral right>>
            {$opossumName}: Oh, alright.
            {$opossumName}: Thanks for the chat, {$opossumThinksPlayerNameIs}.
        <<endif>>
        <<stop_voice_blip Opossum>>
        <<jump EndDay2>>
<<jump OpossumRecurringTreeDay2>>
===
title: CallBirdDay2
position: 411,-211
---
<<hide_all_objects>>
<<hide_all_ui_objects>>
<<show PCRoom>>
<<show MonitorOn>>
<<show_character Bird Obscure right>>
<<stop_voice_blip Bird>>
You call {$birdName}. The line remains static for a little while, but you suddenly see movement on the other end.
<<show_character Bird Happy right>>
<<play_music BirdTheme 0.25>>
{$birdName} appears on the screen.
<<start_voice_blip Bird>>
{$birdName}: Oh hi, {$birdThinksPlayerNameIs}! You caught me in the middle of some work...
    -> I want to get to know you better.
        <<positive_indicator Bird>>
        <<set $birdLikesYou += 1>>
        <<show_character Bird Nervous right>>
        {$birdName}: Hmm, I don't know. I have a lot of things to do. I guess... there's no deadline... but, y'know? Work is work.
        -> Just a quick chat? <<if $birdLikesYou > $birdDislikesYou>>
            <<show_character Bird Flirt right>>
            {$birdName}: Okay, okay. I can take a break for a bit.
        -> Okay, fine. I'll let you work.
            <<show_character Bird Neutral right>>
            {$birdName}: Thanks. Maybe next time, then.
            <<show_ui ClickToInteract>>
            <<show ChooseYourCharacterDay2>>
            <<stop>>
<<jump BirdRecurringTreeDay2>>
<<declare $day2AskedBirdQ1 = false>>
<<declare $day2AskedBirdQ2 = false>>
<<declare $day2AskedBirdQ3 = false>>
<<declare $day2AskedBirdQ4 = false>>
<<declare $day2AskedBirdQ5 = false>>
<<declare $day2AskedBirdQ6 = false>>
<<declare $day2AskedBirdQ7 = false>>
<<declare $knowBirdBackstory = false>>
<<declare $knowBirdSlowWork = false>>
<<declare $recommendedBirdSelfCare = false>>
===

title: BirdRecurringTreeDay2
---
<<start_voice_blip Bird>>
<<show_character Bird Neutral right>>
{$birdName}: What did you want to talk about?
    -> What is it like on your planet? <<if $day2AskedBirdQ1 == false>>
        <<set $day2AskedBirdQ1 = true>>
        <<show_character Bird Happy right>>
        {$birdName}: Oh, it's pretty nice! Lots of trees, open skies, and fresh air.
        <<show_character Bird Sad right>>
        <<set $knowBirdBackstory = true>>
        {$birdName}: It's all pretty peaceful, actually. Albeit... it's been very lonely the past couple of months.
            -> Lonely? Why's that?
                <<show_character Bird Sad right>>
                {$birdName}: It's... kind of a beakful.
                {$birdName}: Me and my partner were very close. I was pretty quiet and kept to myself, and they were <i>extremely</i> adventurous.
                <<show_character Bird Nervous right>>
                {$birdName}: They loved going out and exploring new places, meeting new people.
                {$birdName}: I admired that about them. They always encouraged me to come out of my shell.
                <<stop_voice_blip Bird>>
                <<show_character Bird Sad right>>
                {$birdName} pauses for a moment, looking down.
                    -> Then? What happened?
                        <<set $birdDislikesYou += 1>>
                    -> (give them some space)
                <<start_voice_blip Bird>>
                {$birdName}: Then, all of a sudden, they fell very ill. Aviaris had no cure for it.
                {$birdName}: They passed away a few months ago. I've been feeling pretty lost since then.
                    -> I'm so sorry for your loss.
                        <<positive_indicator Bird>>
                        <<set $birdLikesYou += 1>>
                        <<show_character Bird Neutral right>>
                        {$birdName}: Thank you, {$birdThinksPlayerNameIs}. It's been hard. But talking about it helps me more than you can imagine.
                    -> That's rough.
                        <<set $birdDislikesYou += 2>>
                        <<show_character Bird Sad right>>
                        {$birdName}: Yeah... it really is.
        <<show_character Bird Sweat right>>
        {$birdName}: But, um, I-I didn't mean to steal the spotlight.
        <<if $birdLikesYou >= $birdDislikesYou>>
            <<show_character Bird Happy right>>
        <<else>>
            <<show_character Bird Neutral right>>
        <<endif>>
        {$birdName}: Thanks for hearing me out.
    -> What is your mission? <<if $day2AskedBirdQ2 == false>>
        <<set $day2AskedBirdQ2 = true>>
        <<if $knowBirdSlowWork == false>>
            <<set $knowBirdSlowWork = true>>
            {$birdName}: Mission? I guess, I'm doing research right now on different planets and species.
            <<show_character Bird Thinking right>>
            {$birdName}: But, to be honest, it... it hasn't been getting me anywhere.
            <<show_character Bird Sad right>>
            {$birdName}: I feel like I'm just grasping at straws. But I know that I need to be doing this work.
                -> You need to be doing this? How come?
                -> Don't waste your time.
                    <<set $birdDislikesYou += 2>>
                {$birdName}: I... I just feel like I have to do this...
                <<stop_voice_blip Bird>>
                <<show_character Bird Thinking right>>
                {$birdName} looks off into the distance, contemplating their work.
                <<start_voice_blip Bird>>
                {$birdName}: You know, I think it's been hard to focus since... since my partner... disappeared.
                {$birdName}: They were always so driven and passionate about their work. I felt like I had to keep up.
                    -> I'm sure they'd be proud of you. <<if $birdLikesYou > $birdDislikesYou>>
                        <<positive_indicator Bird>>
                        <<set $birdLikesYou += 3>>
                        <<show_character Bird Flustered right>>
                        {$birdName}: Th-thank you, {$birdThinksPlayerNameIs}...
                        <<show_character Bird Neutral right>>
                        {$birdName}: That means a lot to me.
                        <<show_character Bird Happy right>>
                        {$birdName}: I just need to keep pushing forward.
                    -> Maybe it's time to take a break.
                        <<set $birdDislikesYou += 1>>
                        <<show_character Bird Sad right>>
                        {$birdName}: M-maybe... but I don't know what else to do.
                        {$birdName}: I feel like my work is all I have left of them.
        <<else>>
            <<show_character Bird Thinking right>>
            {$birdName}: Other than doing work, I guess my mission is to... find purpose again.
            <<show_character Bird Sad right>>
            {$birdName}: It's been hard since my partner passed away. I feel like I'm just going through the motions.
        <<endif>>
        <<if $recommendedBirdSelfCare == false>>
            {$birdName}: I just don't know how to move on.
                -> You should take spend more time having fun, or relaxing.
                    <<positive_indicator Bird>>
                    <<set $recommendedBirdSelfCare = true>>
                    <<show_character Bird Confused right>>
                    {$birdName}: I... I don't know if I can. There's just so much to do.
        <<endif>>
    -> What do you do for work? <<if $day2AskedBirdQ3 == false>>
        <<set $day2AskedBirdQ3 = true>>
        <<show_character Bird Neutral right>>
        {$birdName}: Well, as you know, I'm a researcher trying to gather information on other planets and species.
        {$birdName}: I've been stuck for a while, so I'm just in an interviewing and needfinding stage.
        <<show_character Bird Sad right>>
        {$birdName}: But it's... it's been a bit slow going, to be honest.
        <<if $knowBirdSlowWork == false>>
            <<set $knowBirdSlowWork = true>>
            {$birdName}: I feel like I'm just grasping at straws. But I know that I need to be doing this work.
                -> You need to be doing this? How come?
                -> Don't waste your time.
                    <<set $birdDislikesYou += 2>>
                {$birdName}: I... I just feel like I have to do this...
                <<stop_voice_blip Bird>>
                <<show_character Bird Thinking right>>
                {$birdName} looks off into the distance, contemplating their work.
                <<start_voice_blip Bird>>
                {$birdName}: .........
                <<show_character Bird Sad right>>
                {$birdName}: You know, now that I think about it.
                {$birdName}: I think it's been hard to focus since... since my partner... disappeared.
                {$birdName}: They were always so driven and passionate about their work. I felt like I had to keep up.
                    -> I'm sure they'd be proud of you. <<if $birdLikesYou > $birdDislikesYou>>
                        <<positive_indicator Bird>>
                        <<set $birdLikesYou += 3>>
                        <<show_character Bird Flustered right>>
                        {$birdName}: Th-thank you, {$birdThinksPlayerNameIs}...
                        <<show_character Bird Neutral right>>
                        {$birdName}: That means a lot to me.
                        <<show_character Bird Happy right>>
                        {$birdName}: I just need to keep pushing forward.
                    -> Maybe it's time to take a break.
                        <<set $birdDislikesYou += 1>>
                        <<show_character Bird Sad right>>
                        {$birdName}: M-maybe... but I don't know what else to do.
                        {$birdName}: I feel like my work is all I have left of them.
        <<else>>
            {$birdName}: I feel like I'm just going through the motions.
        <<endif>>
    -> What do you do for fun? <<if $day2AskedBirdQ4 == false>>
        <<set $day2AskedBirdQ4 = true>>
        <<show_character Bird Confused right>>
        {$birdName}: F-fun? Gosh, I don't really have time for that these days.
        {$birdName}: I used to enjoy flying around and exploring new places with my partner.
        <<show_character Bird Sad right>>
        {$birdName}: Those days are gone, though. Now, I just focus on my work.
            -> You should make time for fun.
                <<positive_indicator Bird>>
                <<set $birdLikesYou += 1>>
                <<show_character Bird Confused right>>
                {$birdName}: Y-yeah, maybe you're right. I should try to relax more often.
                <<set $recommendedBirdSelfCare = true>>
            -> Work is more important.
                <<set $birdDislikesYou += 1>>
                <<show_character Bird Sad right>>
                {$birdName}: I... I guess so. But it's hard to think about anything else right now.
    -> Do you have a family? <<if $day2AskedBirdQ5 == false>>
        <<set $day2AskedBirdQ5 = true>>
        <<show_character Bird Neutral right>>
        {$birdName}: Yes! I have a mother and father. 
        <<show_character Bird Thinking right>>
        {$birdName}: They're both living separately from me right now, though. I spend most of my days in the lab.
            -> Do you get to see them often?
            -> Are you close?
        {$birdName}: I don't see them all the time, but I fly over to visit when I can.
        {$birdName}: Maybe I should call them more often, now that I think about it... I've just been so busy with work.
    -> You're cute. <<if $day2AskedBirdQ6 == false && $birdLikesYou >= $birdDislikesYou>>
        <<set $day2AskedBirdQ6 = true>>
        <<show_character Bird Flustered right>>
        <<positive_indicator Bird>>
        <<set $birdLikesYou += 1>>
        {$birdName}: Oh! Uh, th-thank you, {$birdThinksPlayerNameIs}...
        {$birdName}: You're... you're very nice to me.
        <<show_character Bird Sweat right>>
        {$birdName}: And, um.
        <<show_character Bird Flirt right>>
        {$birdName}: For what it's worth, I think you're pretty cute too.
    -> Do you want to play a game? <<if $recommendedBirdSelfCare == true && $day2AskedBirdQ7 == false && $birdLikesYou >= $birdDislikesYou>>
        <<show_character Bird Sweat right>>
        {$birdName}: A game? I-I don't know. Will it take a while?
            -> It'll be quick.
            -> Okay, you're right. Maybe another time.
                <<show_character Bird Nervous right>>
                {$birdName}: Y-yeah. Sorry about that. I haven't played games in a long time anyways.
                <<jump BirdRecurringTreeDay2>>
        // TODO: Make this an actual game
        <<set $day2AskedBirdQ7 = true>>
        <<show_character Bird Nervous right>>
        {$birdName}: O-okay. I'll try. No promises I'll do good at it, though.
        <<stop_voice_blip Bird>>
        <<show_character Bird Neutral right>>
        You decide to play a game with {$birdName} on the monitor to pass the time.
        {$birdName} beams an old game they used to play to your device, and you both start playing.
        The game is a pretty complicated game, involving mixing chemical compounds to create new elements.
        You struggle immensely.
        <<show_character Bird Happy2 right>>
        It seems that {$birdName}, however, is doing very well.
            -> (try your best to win. you can do it!)
                You push yourself to focus, but ultimately, you just can't keep up with {$birdName}'s skill. You lose the game.
                <<positive_indicator Bird>>
                <<set $birdLikesYou += 3>>
                <<start_voice_blip Bird>>
                {$birdName}: Heh, good game! You tried your best, {$birdThinksPlayerNameIs}.
            -> (give up. you can't win this...)
                You decide to forfeit the game, admitting that you're outmatched.
                <<show_character Bird Happy right>>
                <<set $birdDislikesYou += 1>>
                <<start_voice_blip Bird>>
                {$birdName}: Heh, sorry. I didn't mean to crush you like that, {$birdThinksPlayerNameIs}.
                <<show_character Bird Happy2 right>>
                {$birdName}: You could've put up a bit more of a fight though! You could've had a good chance there.
        {$birdName}: That was fun! Thanks for playing with me, {$birdThinksPlayerNameIs}.
        <<show_character Bird Happy right>>
        {$birdName}: I... really needed that more than I realized.
    -> (don't ask any more questions)
        <<stop_voice_blip Bird>>
        <<if $posSelfPerception > $negSelfPerception>>
            You decide to end the conversation for now. It's been a while since you've talked with somebody new, but you feel more confident in yourself.
        <<else>>
            You feel your social battery start to drain, so you decide to end the conversation for now.
        <<endif>>
        <<start_voice_blip Bird>>
        <<if $birdLikesYou >= $birdDislikesYou>>
            <<show_character Bird Happy right>>
            {$birdName}: I... rather enjoyed that.
            <<show_character Bird Flirt right>>
            {$birdName}: We should talk more, {$birdThinksPlayerNameIs}. Bye for now!
        <<else>>
            <<show_character Bird Neutral right>>
            {$birdName}: Oh, alright. Enjoyed the chat!
            {$birdName}: Maybe I'll see you next time.
        <<endif>>
        <<stop_voice_blip Bird>>
        <<jump EndDay2>>
<<jump BirdRecurringTreeDay2>>
===

title: EndDay2
position: 762,12
---
<<hide_all_characters>>
<<play_music MainTheme>>
<<if $talkedToPersonDay2 == false>>
    <<set $talkedToPersonDay2 = true>>
    After finishing your conversation, you notice the day is still young. You could try reaching out to one other alien before ending the day.
        -> (call another alien)
            <<hide_all_objects>>
            <<show ChooseYourCharacterDay2>>
            <<stop>>
        -> (end the day)
<<endif>>
You cut the connection and take a step back from your monitor.
    -> (turn off the monitor)
<<hide_all_objects>>
<<show PCRoom>>
Again, after the long conversation, you decide to go to bed.
<<if $posSelfPerception > $negSelfPerception>>
    After talking with these new figures, you've felt a sense of growth. You feel more confident in your ability to handle whatever comes your way.
<<else>>
    After talking with these new figures, you've felt a bit more uncertain about your place in this situation. You hope that you'll be able to figure things out soon.
<<endif>>
<<hide_all_objects>>
<<show PCRoom>>
<<fade_transition 3>>
<<wait 3>>
<<jump StartDay3>>
===