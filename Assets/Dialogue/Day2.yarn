title: StartDay2
position: 51,-456
---
<<set $daysPassed = 2>>
<<play_sound AlarmClock>>
Narrator: You wake up to a loud chime on the monitor, your head pounding yet again. You don't know what time it is, but you feel rested. 
Narrator: Your sleep cycle has been all over the place since you started this job.
Narrator: You sit up and rub your temples. Time to get ready for another day of work.
<<show_ui ClickToInteract>>
<<show MonitorOffDay2>>
===

title: OnClickedMonitorDay2
position: 55,-311
---
<<hide_all_ui_objects>>
<<show MonitorOnNoSilhouette>>
Narrator: You turn on the monitor to see today's tasks.
Narrator: Before you start your report, you notice a new message from HQ.
Narrator: It reads, "Remember your duty to report accurately and without bias. Your observations are crucial for our understanding of these new lifeforms."
<<if $catSecured == false && $dogSecured == false && $opossumSecured == false && $birdSecured == false>>
    <<jump ForeverAlone>>
<<endif>>
Narrator: You ponder the details of your next report. Do you report the encounter with the aliens? Or keep it under wraps?
    -> (report the encounter)
        Narrator: You hesitate for a moment. You're not entirely sure what will happen if you report the aliens, but you may never see them again.
        -> (report the encounter)
            Narrator: You get to writing your report.
            <<hide_all_objects>>
            <<show_ui TypeToInteract>>
            <<show MonitorReportDay2>>
        -> (report that you saw nothing)
            <<hide_all_objects>>
            Narrator: You get to writing your report.
            <<show_ui TypeToInteract>>
            <<show MonitorDontReportDay2>>
    -> (report that you saw nothing)
        <<hide_all_objects>>
        Narrator: You get to writing your report.
        <<show_ui TypeToInteract>>
        <<show MonitorDontReportDay2>>
===

title: KeptUnderWrapsDay2
position: 167,29
---
<<hide_all_ui_objects>>
Narrator: You hesitate before sending the message. You disobeyed the rules of your job, but you feel it was the right choice.
<<if $posSelfPerception > $negSelfPerception>>
    Narrator: You feel a sense of pride in your decision. You trust your instincts and believe that not everything needs to be reported.
<<else>>
    Narrator: You feel a pang of guilt. However, you rationalize that some things are better left unsaid.
<<endif>>
Narrator: You turn the monitor off and take a seat.
<<hide_all_objects>>
<<show Sitting>>
Narrator: It's a fresh day. The aliens said that you can ping them if you wanted to chat. Maybe you should give it a try.
    -> (reestablish contact with the aliens)
        <<hide_all_objects>>
        <<show ChooseYourCharacterDay2>>
    -> (ignore the aliens)
        Narrator: This option isn't implemented yet. So instead, you re-establish contact with the aliens.
        <<hide_all_objects>>
        <<show ChooseYourCharacterDay2>>
        // TODO: Actually make this branch work!
===

title: CallCatDay2
position: 422,282
---
<<hide_all_objects>>
<<show MonitorOn>>
<<show_character Cat Obscure right>>
<<stop_voice_blip Cat>>
Narrator: You call {$catName}. The line remains static for a little while, but you suddenly see movement on the other end.
<<if $catLikesYou >= $catDislikesYou>>
    <<show_character Cat Happy right>>
    <<play_music CatTheme 0.5>>
    Narrator: {$catName} appears on the screen, looking relieved to see you.
    <<show_character Cat Happy2 right>>
    <<start_voice_blip Cat>>
    {$catName}: Hey {$playerName}, I was just thinking about you! Do you need something?
        -> I want to get to know you better.
            <<positive_indicator Cat>>
            <<set $catLikesYou += 1>>
            <<show_character Cat Happy right>>
            {$catName}: I'd like that. It's nice to have someone to talk to out here.
<<else>>
    <<show_character Cat Neutral right>>
    Narrator: {$catName} appears on the screen.
    <<start_voice_blip Cat>>
    {$catName}: Oh, it's you! Hey again, {$playerName}. What's up?
        -> I want to get to know you better.
            <<positive_indicator Cat>>
            <<set $catLikesYou += 1>>
            <<show_character Cat Thinking right>>
            {$catName}: Huh. Well, I guess we can talk a bit more.
<<endif>>
<<declare $day2AskedCatQ1 = false>>
<<declare $day2AskedCatQ2 = false>>
<<declare $day2AskedCatQ3 = false>>
<<declare $day2AskedCatQ4 = false>>
<<declare $knowCatPasttime = false>>
<<declare $knowCatJob = false>>
<<jump CatRecurringTreeDay2>>
===

title: CatRecurringTreeDay2
---
<<start_voice_blip Cat>>
<<if $catLikesYou >= $catDislikesYou>>
    <<show_character Cat Happy right>>
<<else>>
    <<show_character Cat Neutral right>>
<<endif>>
<<once>>
<<stop_voice_blip Cat>>
Narrator: <i>Note: From now on, some options may be available or unavailable depending on your choices. You can view special options based on parenthesized text before the option.</i>
    -> <b>(POSITIVE SELF-IMAGE)</b> Wow, that's cool! <<if $posSelfPerception >= $negSelfPerception>>
    -> <b>(NEGATIVE SELF-IMAGE)</b> Okay, whatever. <<if $posSelfPerception < $negSelfPerception>>
<<start_voice_blip Cat>>
<<endonce>>
{$catName}: What did you want to talk about?
    -> What is it like out here? <<if $day2AskedCatQ1 == false>>
        <<set $day2AskedCatQ1 = true>>
        {$catName}: I actually come from a planet far, far away from you.
        <<show_character Cat Neutral right>>
        {$catName}: We've done surveils of your planet before! Out here, we don't have the same comforts as you do. 
        {$catName}: For example, the quakes on our planet are enough to break bones. So we have to climb around a lot to find safe spots away from the ground.
            -> That's scary!
                <<positive_indicator Cat>>
                <<set $catLikesYou += 1>>
                <<show_character Cat Happy right>>
                {$catName}: Yeah, it can be. But we've learned to adapt over time.
            -> Oh, those aren't bad. We have those too.
                <<show_character Cat Sad right>>
                <<set $catDislikesYou += 1>>
                {$catName}: Sure, you do. But I can say with a pretty good amount of confidence that they're not as bad as ours.
                {$catName}: We have a lot of casualties from them every year. It's pretty unfortunate.
            -> Surveils of our planet?
                <<positive_indicator Cat>>
                <<set $catLikesYou += 1>>
                <<show_character Cat Happy right>>
                {$catName}: Yeah! We've been watching your planet for a while now. It's really interesting to see how different life is there compared to out here.
                {$catName}: Your technology is pretty advanced too, from what we've seen!
                <<show_character Cat Flirt right>>
                {$catName}: Just not as advanced as us, yet~ but I bet you'll discover intergalactic travel too, someday.
    -> What is your mission? <<if $day2AskedCatQ2 == false>>
        <<set $day2AskedCatQ2 = true>>
        {$catName}: Mission? I don't think we have... a "mission". We're just out here trying to survive, same as you.
            -> But what is your <i>mission</i>? You know, as an alien?
                <<show_character Cat Angry right>>
                {$catName}: You make it seem like we're some kind of invaders or something.
                <<show_character Cat Neutral right>>
                {$catName}: I'm telling you, we're just trying to get by out here. We're not looking to conquer anyone.
                <<set $catDislikesYou += 1>>
            -> I guess, what do you work as? <<if $knowCatJob == false>>
                <<set $knowCatJob = true>>
                <<positive_indicator Cat>>
                <<set $catLikesYou += 1>>
                <<show_character Cat Happy right>>
                {$catName}: I hear that on your planet, they have people doing <i>super</i> specific tasks. Like working at one specific restaurant for their whole life!
                {$catName}: I just work for safety. I make sure our planet is safe, our adventurers come back safely from space, and everything you can think of in between.
                <<show_character Cat Thinking right>>
                {$catName}: Safety across the galaxy is tougher than you think. There are a lot of different species out here, and not all of them are friendly.
            -> Oh, I see.
    -> What do you do for work? <<if $knowCatJob == false>>
        <<set $knowCatJob = true>>
        <<positive_indicator Cat>>
        <<set $catLikesYou += 1>>
        <<show_character Cat Happy right>>
        {$catName}: I hear that on your planet, they have people doing <i>super</i> specific tasks. Like working at one specific restaurant for their whole life!
        {$catName}: My job is pretty simple. I just work for safety.
        <<show_character Cat Thinking right>>
        {$catName}: I make sure our infrastructure is safe, our adventurers come back safely from space, and everything you can think of in between.
        {$catName}: Safety across the galaxy is tougher than you think. There are a lot of different species out here, and not all of them are friendly.
    -> What do you do for fun? <<if $knowCatPasttime == false>>
        <<set $knowCatPasttime = true>>
        {$catName}: Fun? Hm, I guess I like to play games on my monitor when I'm not working. It's a nice way to pass the time.
        <<if $catLikesYou >= $catDislikesYou>>
            <<show_character Cat Happy2 right>>
        <<else>>
            <<show_character Cat Neutral right>>
        <<endif>>
        {$catName}: The usual type of space game. Puzzle games, strategy games, that sort of thing. Nothing too exciting. I could even install one on your device, I think!
    -> <b>(POSITIVE RELATIONSHIP)</b> You're cute. <<if $day2AskedCatQ4 == false && $catLikesYou >= $catDislikesYou>>
        <<set $day2AskedCatQ4 = true>>
        <<show_character Cat Flirt right>>
        <<positive_indicator Cat>>
        <<set $catLikesYou += 1>>
        {$catName}: Aw, you're too kind. You're pretty cute yourself, {$catThinksPlayerNameIs}.
    -> <b>(POSITIVE RELATIONSHIP)</b> Do you want to play a game? <<if $day2AskedCatQ3 == false && $knowCatPasttime == true && $catLikesYou >= $catDislikesYou>>
        <<set $day2AskedCatQ3 = true>>
        <<show_character Cat Happy2 right>>
        {$catName}: A game? Sure, why not!
        // TODO: Make this an actual game
        <<stop_voice_blip Cat>>
        <<show_character Cat Happy right>>
        Narrator: You decide to play a game with {$catName} on the monitor to pass the time.
        Narrator: {$catName} beams the game to your device, and you both start playing.
        Narrator: The game is a simple one where you have to match shapes and colors. It's not very exciting, but it helps to break the ice.
        <<show_character Cat Sad right>>
        Narrator: It seems that {$catName}, however, is struggling quite a bit.
            -> (win at the game)
                <<positive_indicator Cat>>
                <<set $catLikesYou += 1>>
                <<show_character Cat Happy right>>
                <<start_voice_blip Cat>>
                {$catName}: Wow, you're really good at this! Maybe you can teach me sometime.
            -> (purposefully lose the game)
                <<show_character Cat Happy2 right>>
                <<positive_indicator Cat>>
                <<set $catLikesYou += 1>>
                <<start_voice_blip Cat>>
                {$catName}: Hah, I think I won that one! 
                <<show_character Cat Flirt right>>
                {$catName}: Maybe some day I'll teach you a thing or two. But it takes quite a lot of practice to get on my level.
    -> (don't ask any more questions)
        <<stop_voice_blip Cat>>
        <<if $posSelfPerception > $negSelfPerception>>
            Narrator: You decide to end the conversation for now. It's been a while since you've talked with somebody new, but you feel more confident in yourself.
        <<else>>
            Narrator: You feel your social battery start to drain, so you decide to end the conversation for now.
        <<endif>>
        <<start_voice_blip Cat>>
        <<if $catLikesYou >= $catDislikesYou>>
            <<show_character Cat Happy right>>
            {$catName}: Aww, already? I was just starting to have fun!
            {$catName}: But okay, if you need to go, I understand. Just ping me again sometime, okay {$catThinksPlayerNameIs}?
        <<else>>
            <<show_character Cat Neutral right>>
            {$catName}: Oh, alright. That sounds good to me.
            {$catName}: Thanks for the talk, I guess.
        <<endif>>
        <<stop_voice_blip Cat>>
        <<hide_all_characters>>
        <<play_music MainTheme>>
        Narrator: You cut the connection and take a step back from your monitor.
            -> (turn off the monitor)
        <<hide_all_objects>>
        <<jump EndDay2>>
<<jump CatRecurringTreeDay2>>
===

title: CallDogDay2
position: 408,123
---
Narrator: You call {$dogName}.
Narrator: But they don't respond. So you instead call {$catName}. (this is not implemented yet)
<<jump CallCatDay2>>
===

title: CallOpossumDay2
position: 409,-43
---
Narrator: You call {$opossumName}.

Narrator: But they don't respond. So you instead call {$catName}. (this is not implemented yet)
<<jump CallCatDay2>>
===

title: CallBirdDay2
position: 411,-211
---
Narrator: You call {$birdName}.

Narrator: But they don't respond. So you instead call {$catName}. (this is not implemented yet)
<<jump CallCatDay2>>
===

title: EndDay2
position: 762,12
---
<<hide_all_characters>>
<<hide_all_objects>>
<<show MonitorOff>>
Narrator: Again, after the long conversation, you decide to go to bed.
<<if $posSelfPerception > $negSelfPerception>>
    Narrator: After talking with these new figures, you've felt a sense of growth. You feel more confident in your ability to handle whatever comes your way.
<<else>>
    Narrator: After talking with these new figures, you've felt a bit more uncertain about your place in this situation. You hope that you'll be able to figure things out soon.
<<endif>>
<<hide_all_objects>>
<<show ScreenBlocker>>
<<wait 2>>
<<hide ScreenBlocker>>
<<jump StartDay3>>
===