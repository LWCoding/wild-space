title: StartDay2
position: 51,-456
---
<<play_sound AlarmClock>>
Narrator: You wake up to a loud chime on the monitor, your head pounding yet again. You don't know what time it is, but you feel rested. 
Narrator: Your sleep cycle has been all over the place since you started this job.
Narrator: You sit up and rub your temples. Time to get ready for another day of work.
<<show_ui ClickToInteract>>
<<show MonitorOffDay2>>
===

title: OnClickedMonitorDay2
position: 55,-311
---
<<hide_all_ui_objects>>
<<show MonitorOnDay2>>
Narrator: You turn on the monitor to see today's tasks.
Narrator: Before you start your report, you notice a new message from HQ.
Narrator: It reads, "Remember your duty to report accurately and without bias. Your observations are crucial for our understanding of these new lifeforms."
Narrator: You ponder the details of your next report. Do you report the encounter with the aliens? Or keep it under wraps?
    -> (report the encounter)
        Narrator: You hesitate for a moment. You're not entirely sure what will happen if you report the aliens, but you may never see them again.
        -> (report the encounter)
            Narrator: You get to writing your report.
            <<hide_all_objects>>
            <<show_ui TypeToInteract>>
            <<show MonitorReport>>
        -> (keep it under wraps)
            <<hide_all_objects>>
            Narrator: You get to writing your report.
            <<show_ui TypeToInteract>>
            <<show MonitorDontReportDay2>>
    -> (keep it under wraps)
        <<hide_all_objects>>
        Narrator: You get to writing your report.
        <<show_ui TypeToInteract>>
        <<show MonitorDontReportDay2>>
===

title: KeptUnderWrapsDay2
position: 167,29
---
<<hide_all_ui_objects>>
Narrator: You hesitate before sending the message. You have disobeyed direct orders, but you feel it was the right choice.
<<if $posSelfPerception > $negSelfPerception>>
    Narrator: You feel a sense of pride in your decision. You trust your instincts and believe that not everything needs to be reported.
<<else>>
    Narrator: You feel a pang of guilt. However, you rationalize that some things are better left unsaid.
<<endif>>
Narrator: You turn the monitor off and take a seat.
<<hide_all_objects>>
<<show Sitting>>
Narrator: It's a fresh day. The aliens said that you can ping them if you wanted to chat. Maybe you should give it a try.
    -> (reestablish contact with the aliens)
        <<hide_all_objects>>
        <<show ChooseYourCharacterDay2>>
    -> (ignore the aliens)
        Narrator: This option isn't implemented yet. So instead, you re-establish contact with the aliens.
        <<hide_all_objects>>
        <<show ChooseYourCharacterDay2>>
        // TODO: Actually make this branch work!
===

title: CallCatDay2
position: 422,282
---
Narrator: You call {$catName}. The line remains static for a little while, but you suddenly see movement on the other end.
<<if $catLikesYou >= $catDislikesYou>>
    <<show_character Cat Happy right>>
    Narrator: {$catName} appears on the screen, looking relieved to see you.
    <<show_character Cat Happy2 right>>
    {$catName}: Hey {$playerName}, I was just thinking about you! Do you need something?
        -> I want to get to know you better.
            <<positive_indicator Cat>>
            <<set $catLikesYou += 1>>
            <<show_character Cat Happy right>>
            {$catName}: I'd like that. It's nice to have someone to talk to out here.
<<else>>
    <<show_character Cat Neutral right>>
    Narrator: {$catName} appears on the screen.
    {$catName}: Oh, it's you! Hey again, {$playerName}. What's up?
        -> I want to get to know you better.
            <<positive_indicator Cat>>
            <<set $catLikesYou += 1>>
            <<show_character Cat Thinking right>>
            {$catName}: Huh. Well, I guess we can talk a bit more.
<<endif>>
{$catName}: What did you want to talk about?
    -> What is it like out here?
        {$catName}: I actually come from a planet far, far away from where you are.
        {$catName}: We've done some surveils of your planet before! Out here, we don't have the same comforts as you do. 
        {$catName}: For example, the quakes on our planet are enough to break bones. So we have to climb around a lot to find safe spots away from the ground.
    -> What is your mission?
        {$catName}: Mission? I don't think we have... a "mission". We're just out here trying to survive, same as you.
        <<if $catLikesYou >= $catDislikesYou>>
            <<show_character Cat Happy right>>
        <<else>>
            <<show_character Cat Neutral right>>
        <<endif>>
        {$catName}: You make it seem like we're some kind of invaders or something.
    -> Do you want to play a game?
        {$catName}: A game? Sure, why not!
<<jump EndDay2>>
===

title: CallDogDay2
position: 408,123
---
Narrator: You call {$dogName}.
Narrator: But they don't respond. So you instead call {$catName}. (this is not implemented yet)
<<jump CallCatDay2>>
===

title: CallLizardDay2
position: 409,-43
---
Narrator: You call {$lizardName}.

Narrator: But they don't respond. So you instead call {$catName}. (this is not implemented yet)
<<jump CallCatDay2>>
===

title: CallBirdDay2
position: 411,-211
---
Narrator: You call {$birdName}.

Narrator: But they don't respond. So you instead call {$catName}. (this is not implemented yet)
<<jump CallCatDay2>>
===

title: EndDay2
position: 762,12
---
<<hide_all_characters>>
<<hide_all_objects>>
Narrator: Again, after the long conversation, you decide to go to bed.
<<if $posSelfPerception > $negSelfPerception>>
    Narrator: After talking with these new figures, you've felt a sense of growth. You feel more confident in your ability to handle whatever comes your way.
<<else>>
    Narrator: After talking with these new figures, you've felt a bit more uncertain about your place in this situation. You hope that you'll be able to figure things out soon.
<<endif>>
<<show ScreenBlocker>>
<<wait 2>>
<<hide ScreenBlocker>>
<<jump StartDay3>>
===